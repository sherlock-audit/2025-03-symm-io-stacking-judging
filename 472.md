Virtual Powder Condor

Medium

# Blacklisted USDC/USDT users can block the blacklisting of USDT/USDC tokens

## Summary

USDC/USDT Blacklisted users with pending rewards prevents the blacklisting of USDC/USDT as reward tokens

## Vulnerability Details

In `SymmStaking`, both USDC and USDT are designated as reward tokens.

Symmio permits the holder of the `REWARD_MANAGER_ROLE` to blacklist USDC and USDT as reward tokens. However, this functionality can be broken if any user with `pendingRewards` is blacklisted.

The issue arises because a reward token cannot be blacklisted if there are any pending rewards associated with it, even if the amount is minimal (more than 10 units).

The `pendingRewards` for USDC and USDT will only decrease when either the user claims their rewards or an authorized individual with the `REWARD_MANAGER_ROLE` claims their portion of the rewards.

In `SymmStaking._claimRewardsFor`

```solidity
	function _claimRewardsFor(address user) internal {
		uint256 length = rewardTokens.length;
		for (uint256 i = 0; i < length; ) {
			address token = rewardTokens[i];
			uint256 reward = rewards[user][token];
			if (reward > 0) {
				rewards[user][token] = 0;
@>				pendingRewards[token] -= reward; // @audit will not decrement since line below reverts for blacklisted user
@>				IERC20(token).safeTransfer(user, reward); //@audit reverts if user is blacklisted
				emit RewardClaimed(user, token, reward);
			}
			unchecked {
				++i;
			}
		}
	}
```

In `SymmStaking.configureRewardToken`

```solidity
	function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));

		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);

		isRewardToken[token] = status;
		if (!status) {
@>			if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]); //@audit removal of USDC/USDT is not possible since pendingRewards is never decremented as long as one of the addresses eligible for rewards is blacklisted
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}

		emit UpdateWhitelist(token, status);
	}
```

## Root Cause
A user blacklisted for USDT/USDC can prevent Symmio from blacklisting USDT/USDC as reward tokens.

## POC

Consider the following scenario

1. Alice possesses 10 USDC as reward tokens.
2. Alice has been blacklisted by USDC.
3. When the `REWARD_MANAGER_ROLE` tries to claim rewards for Alice, the transfer call `IERC20(token).safeTransfer(Alice, reward)` will consistently fail because Alice is blacklisted.
4. The value of pendingRewards[USDC] will remain unchanged until Alice is no longer blacklisted.
5. The protocol cannot blacklist USDC as a reward token while Alice remains blacklisted because of the `pendingRewards` associated to her.

## Impact 
Breaks contract blacklisting functionality

## Mitigation
Consider implementing a try-catch block along with additional internal accounting to manage rewards for users who are blacklisted.

## LOC
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L320
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L393