Round Strawberry Goose

Medium

# Rewards may get stuck in the contract if the reward token is a token with less than 18 decimals

### Summary

The precision loss inside the function `_addRewardsForToken()` leads to imprecise reward rate calculation for the reward tokens with decimals less than 18. (e.g. USDT, USDC, WBTC)

### Root Cause

Inside the function `_addRewardsForToken()`, the rate is calculated based on the division of the reward amounts by the period duration:

```Solidity
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];

		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration;
		}

		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
	}
```
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L366

The reward period duration is constant and equal to the `1 weeks`. If the `amount` is close to `1 week` (604800 seconds), the calculation suffers from precision loss because the rate is determined by dividing the amount by the period duration. This division can introduce rounding errors, leading to inaccurate rate calculations.

As the **USDC** is considered to be used as the reward token in the contest page, suppose the admin wants to add some USDC as the rewards. Due to this issue, for some certain amounts, the rate calculation will face significant losses.

### Internal Pre-conditions

1. The reward token decimal should be less than 18 (USDC, USDT, WBTC)

### External Pre-conditions

-

### Attack Path

-

### Impact

The users suffer reward loss for the tokens with decimals less than 18.

### PoC

```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import "../contracts/staking/SymmStaking.sol";
import "../contracts/token/symm.sol";
import "@openzeppelin/contracts-upgradeable/mocks/token/ERC20MockUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/mocks/token/ERC20DecimalsMockUpgradeable.sol";

contract ERC20Sample is ERC20DecimalsMockUpgradeable {

    function initialize(uint8 _decimals) public initializer {
        __ERC20DecimalsMock_init(_decimals);
    }

    function mint(address account, uint amount) public {
        _mint(account, amount);
    }
}

contract StakingTest is Test {

    address private admin = makeAddr("Admin");
    ERC20MockUpgradeable rewardToken1;
    ERC20MockUpgradeable rewardToken2;
    SymmStaking staking;
    Symmio token;

    function setUp() public {

        token = new Symmio("Symmio", "SYMM", admin);

        rewardToken1 = new ERC20MockUpgradeable();
        rewardToken2 = new ERC20MockUpgradeable();
        rewardToken1.mint(admin, 1e24);
        rewardToken2.mint(admin, 1e24);

        vm.startPrank(admin);
        staking = new SymmStaking();
        staking.initialize(admin, address(token));
        staking.configureRewardToken(address(rewardToken1), true);
        staking.configureRewardToken(address(rewardToken2), true);
        rewardToken1.approve(address(staking), type(uint256).max);
        rewardToken2.approve(address(staking), type(uint256).max);
        vm.stopPrank();
    }

    function testLockingAmounts() public {

        ERC20Sample wbtc = new ERC20Sample();
        wbtc.initialize(8);
        wbtc.mint(admin, 10e8);

        vm.startPrank(admin);
        wbtc.approve(address(staking), type(uint256).max);
        staking.configureRewardToken(address(wbtc), true);
        vm.stopPrank();

        address[] memory rewardTokens = new address[](1);
        rewardTokens[0] = address(wbtc);
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 1e6; // 0.01 BTC equal to $1000 with assuming 1 BTC =~ $100k
        vm.prank(admin);
        staking.notifyRewardAmount(rewardTokens, amounts);

        address alice = vm.addr(0x1002);
        deal(address(token), alice, 1000e18);

        vm.startPrank(alice);
        token.approve(address(staking), type(uint256).max);
        staking.deposit(1000e18, alice);
        vm.stopPrank();

        vm.warp(block.timestamp + 1 weeks + 1); // make sure all reward distributed
        vm.prank(alice);
        staking.claimRewards();
        uint256 balance = IERC20(address(wbtc)).balanceOf(alice);
        console.log("Alice reward: ", balance);
        assertApproxEqAbs(60e4, balance, 1e6);
        balance = IERC20(address(wbtc)).balanceOf(address(staking));
        console.log("Staking reward: ", balance);
        assertApproxEqAbs(39e4, balance, 1e6);
        console.log("Expected reward: $1,000, actual reward: $604, locked: $396");
    }
}
```

The test result is:

```Markdown
[PASS] testLockingAmounts() (gas: 1249113)
Logs:
  Alice reward:  604000
  Staking reward:  396000
  Expected reward: $1,000, actual reward: $604, locked: $396

```

As it can be seen from the test, Alice faces around 65% reward loss.

### Mitigation

Consider increasing the precision of the `rewardState[_rewardsToken].rate` (e.g. multiply the rate inside the `_addRewardsForToken()` by `1000` and divide it by the `1000` inside the `rewardPerToken()`, or use high-precision math libraries)