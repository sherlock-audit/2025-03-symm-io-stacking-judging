Formal Rosewood Hawk

Medium

# Anyone can add fraction rewards and slow down the reward per token

### Summary

Anyone can add fraction rewards and slow down the reward per token

### Root Cause

When rewards should be added to the staking contract the `SymmStakin::notifyRewardAmount` is called by the owner or owner-controlled contract, which triggers the internal `_addRewardsForToken`: 

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L366-L378

```javascript
function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];

		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration;
		}

		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
}        
```

As can be seen this will extend the period finish by `now + duration (defaulted to 7 days)`. And since the notify rewards amount function can be called by anyone: 

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L275-L291

```javascript
function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		_updateRewardsStates(address(0));
		if (tokens.length != amounts.length) revert ArraysMismatched();

		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
			address token = tokens[i];
			uint256 amount = amounts[i];

			if (amount == 0) continue;
			if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
			pendingRewards[token] += amount;
			_addRewardsForToken(token, amount);
		}
		emit RewardNotified(tokens, amounts);
	}
```

This opens griefing possibility for a malicious user:

1. Owner adds the reward token by calling `configureRewardToken`, which sets the duration to 1 week
2. Then calls `notifyRewardAmount` to add the rewards
3. Before the duration end on the 5th or 6th day, a malicious user also calls `notifyRewardAmount` by adding 1 wei
4. This will extend the duration for 7 more days:

```javascript
function rewardPerToken(address _rewardsToken) public view returns (uint256) {
		if (totalSupply == 0) {
			return rewardState[_rewardsToken].perTokenStored;
		}
		return
			rewardState[_rewardsToken].perTokenStored +
			(((lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated) * rewardState[_rewardsToken].rate * 1e18) / 
			totalSupply);

function lastTimeRewardApplicable(address _rewardsToken) public view returns (uint256) {
		return block.timestamp < rewardState[_rewardsToken].periodFinish ? block.timestamp : rewardState[_rewardsToken].periodFinish;
	}            
```

5. Users now will get slightly lower rewards per token due to the new adjustment, especially if they were willing to strategically unstake on the last days for example.
6. Attacker can repeat this to stretch and lower the reward per token indefinitely 

Consider this scenario:

Total reward: 1000 USDC
Duration: 7 days
Rate = 1000 / 7 = ~142,86 per day

1. 1 day left
2. Remaining rewards: ~ 142,86 USDC
3. New rewards added by attacker lets say 10 USDC (instead of 1)
4. New duration: 7 more days
5. New rate will be: (10 + 142.86) / 7 â‰ˆ 21.84 USDC per day

This will also negatively impact the stakers who were willing to unstake at day 7, because they will get lower rewards.

### Internal Pre-conditions

described in root cause

### External Pre-conditions

Described in root cause

### Attack Path

Described in the Root Cause

### Impact

An attacker can grief the reward per token calculations

### PoC

_No response_

### Mitigation

Add an access control on the `notifyRewardAmount` function