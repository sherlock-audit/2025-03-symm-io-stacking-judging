Quaint Heather Ostrich

Medium

# Users can bypass the unlockedAmount check in _resetVestingPlans by calling addLiquidity frequently.

### Summary

There is a check for vestingPlan.unlockedAmount in the function _resetVestingPlans:

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L222-L237

```solidity
	function _resetVestingPlans(address token, address[] memory users, uint256[] memory amounts) internal {
		if (users.length != amounts.length) revert MismatchArrays();
		uint256 len = users.length;
		for (uint256 i = 0; i < len; i++) {
			address user = users[i];
			uint256 amount = amounts[i];
			// Claim any unlocked tokens before resetting.
			_claimUnlockedToken(token, user);
			VestingPlan storage vestingPlan = vestingPlans[token][user];
>@			if (amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
			uint256 oldTotal = vestingPlan.lockedAmount();
			vestingPlan.resetAmount(amount);
			totalVested[token] = totalVested[token] - oldTotal + amount;
			emit VestingPlanReset(token, user, amount);
		}
	}
```

This means that if the newly locked amount is less than the amount already claimed, a revert will occur. However, in the function `resetAmount`, `startTime` will be reset, which means that when `_resetVestingPlans` is executed normally, the `unlockedAmount` by the user will become 0.

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/libraries/LibVestingPlan.sol#L71-L82

```solidity
	function resetAmount(VestingPlan storage self, uint256 amount) public returns (VestingPlan storage) {
		if (claimable(self) != 0) revert ShouldClaimFirst();
		if (!isSetup(self)) revert ShouldSetupFirst();
		// Rebase the vesting plan from now.
		uint256 remaining = remainingDuration(self);
		if (remaining == 0) revert PlanIsFinished();
>@		self.startTime = block.timestamp;
		self.endTime = block.timestamp + remaining;
		self.amount = amount;
		self.claimedAmount = 0;
		return self;
	}
```

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/libraries/LibVestingPlan.sol#L20-L27

```solidity
	function unlockedAmount(VestingPlan storage self) public view returns (uint256) {
		uint256 currentTime = block.timestamp;
		if (currentTime >= self.endTime) return self.amount;
		if (currentTime <= self.startTime) return 0;
		uint256 duration = self.endTime - self.startTime;
>@		uint256 elapsed = currentTime - self.startTime;
		return (self.amount * elapsed) / duration;
	}
```

Therefore, the user can call the function addLiquidity multiple times(for example, once a day), so that after `_resetVestingPlans` is executed, `unlockedAmount` will become 0. In this way, the user can bypass the unlockedAmount check.

Consider the following scenario, where a user has a 10-day SYMM LP token vestingPlan. The user calls addLiquidity once every 1 day, and only increases the number of SYMM LP tokens by 1 each time. In this way, the user can still add liquidity after 8 days, no matter how much liquidity is added, because the unlockedAmount at this time must be less than `amount`. However, if the user does not add liquidity in the first 8 days, it will be difficult for the user to add liquidity after 8 days because 80% of the SYMM LP tokens have been unlocked, unless the user is able to add a large amount of liquidity again (reaching 80% of the initial amount).

### Root Cause

After each call to _resetVestingPlans, the user's unlockedAmount will be reset to 0.

### Internal Pre-conditions

1. User has a SYMM LP token vestingPlan


### External Pre-conditions

None

### Attack Path

1. User calls the addLiquidity mutliple times with a small SYMM token, in order to reset the unlockedAmount.

### Impact

Even if a user has unlocked a large number of SYMM LP tokens, the user can still continue adding Liquidity.

### PoC

_No response_

### Mitigation

Record the total unlockedAmount.