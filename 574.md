Cheesy Felt Ladybug

Medium

# Vesting.sol should use onlyInitializing modifier

### Summary

SymmVesting and Vesting both have init functions with initializer modifier. SymmVesting inherits Vesting and calls Vesting's init function. Because of this, initializing SymmVesting fails and a DOS of SymmVesting occurs.

### Root Cause

SymmVesting.sol inherits from Vesting.sol and calls __vesting_init() in its initialize() function. Both __vesting_init() and initialize() use initialize modifier. Because of this calling SymmVesting::initialize() reverts every time.
```solidity
	function initialize(
		address admin,
		address _lockedClaimPenaltyReceiver,
		address _pool,
		address _router,
		address _permit2,
		address _vault,
		address _symm,
		address _usdc,
		address _symm_lp
>	) public initializer {
		if (
			admin == address(0) ||
			_lockedClaimPenaltyReceiver == address(0) ||
			_pool == address(0) ||
			_router == address(0) ||
			_permit2 == address(0) ||
			_vault == address(0) ||
			_symm == address(0) ||
			_usdc == address(0) ||
			_symm_lp == address(0)
		) revert ZeroAddress();
>		__vesting_init(admin, 500000000000000000, _lockedClaimPenaltyReceiver);
		POOL = IPool(_pool);
		ROUTER = IRouter(_router);
		PERMIT2 = IPermit2(_permit2);
		VAULT = _vault;
		SYMM = _symm;
		USDC = _usdc;
		SYMM_LP = _symm_lp;
	}
```
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/SymmVesting.sol#L55-L85
```solidity
>	function __vesting_init(address admin, uint256 _lockedClaimPenalty, address _lockedClaimPenaltyReceiver) public initializer {
		__AccessControlEnumerable_init();
		__Pausable_init();
		__ReentrancyGuard_init();

		lockedClaimPenalty = _lockedClaimPenalty;
		lockedClaimPenaltyReceiver = _lockedClaimPenaltyReceiver;

		if (admin == address(0) || _lockedClaimPenaltyReceiver == address(0)) revert ZeroAddress();

		_grantRole(DEFAULT_ADMIN_ROLE, admin);
		_grantRole(SETTER_ROLE, admin);
		_grantRole(PAUSER_ROLE, admin);
		_grantRole(UNPAUSER_ROLE, admin);
		_grantRole(OPERATOR_ROLE, admin);
	}
```
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L76-L91

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. Admins try to initialize SymmVesting.sol
2. The call reverts due to wrong initialization logic

### Impact

DOS of SymmVesting.sol happens as no vesting plans can be set and because of this no tokens can be claimed by users

### PoC

_No response_

### Mitigation

Use onlyInitializing modifier on __vesting_init() instead of initializer modifier