Careful Concrete Pike

Medium

# `SymmVesting#addLiquidity()` might revert due to wrong sanity check

### Summary
SymmVesting is used to manage Symmioâ€™s vesting policies. Once a vesting plan has been setup, the owner can either call [`Vesting #claimUnlockedToken()`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L145-L147) to claim linearly released SYMM token, or use locked SYMM together with their USDC to add liquidity for a designated balancer pool by calling [`SymmVesting#addLiquidity()`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/SymmVesting.sol#L99-L105). 

`addLiquidity()` includes several steps below:
- First it will check if there is any unlocked lp tokens and claim it when possible
- Then the specified amount of locked SYMM together with USDC will be added to SYMM/USDC pool for lp tokens
- The new lp tokens will be vested together with the remaining locked lp tokens 


However, the last step might call [`_resetVestingPlans()`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L222-L237), and the below check could revert the whole function:
```solidity
			if (amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
``` 
In fact there is no any reason check `amount` as above when add liquidity. Anyone should be able to add liquidity as long as they have enough USDC and own enough locked SYMM in SymmVesting contract.
### Root Cause

Unnecessary amount check in `_resetVestingPlans()` could make `SymmVesting#addLiquidity()` revert

### Internal Pre-conditions

The amount of lp token got in `addLiquidity()` is less than the previous unlocked amount of lp token

### External Pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Both `SymmVesting#addLiquidity()` and `addLiquidityByPercentage()` can not work as expected

### PoC
Copy below codes into [symmVesting.behavior.ts](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/tests/symmVesting.behavior.ts) and run `npx hardhat test`:
```solidity
		it.only('should revert with error AlreadyClaimedMoreThanThis()' , async() => {
			await symmVesting.connect(user1).addLiquidity(e(100), 0, 0)

			const plan = await symmVesting.vestingPlans(pool, user1)

			const midTime = Math.floor(Number((plan.startTime + plan.endTime) / BigInt(2)))
			const midMidTime = Math.floor(Number((BigInt(midTime) + plan.endTime) / BigInt(2)))
			// await time.increase(midTime - Number(plan.startTime))
			await network.provider.send("evm_setNextBlockTimestamp", [midMidTime]) // half of vesting lock
			await network.provider.send("evm_mine")
			

			//@audit-info addLiquidity(e(20)) revert with error AlreadyClaimedMoreThanThis()
			await expect(symmVesting.connect(user1).addLiquidity(e(20), 0, 0)).to.be.revertedWithCustomError(
				symmVesting,
				"AlreadyClaimedMoreThanThis",
			)
			//@audit-info Howerver addLiquidity(e(100)) succeeds 
			await symmVesting.connect(user1).addLiquidity(e(100), 0, 0)
		})
```
As we can see that `addLiquidity(e(20))` reverts but `addLiquidity(e(100))` succeeds
### Mitigation
The amount check should be skipped for adding liquidity:
```diff
	function _addLiquidityProcess(
		uint256 amount,
		uint256 minLpAmount,
		uint256 maxUsdcIn
	) internal returns (uint256[] memory amountsIn, uint256 lpAmount) {
		// Claim any unlocked SYMM tokens first.
		_claimUnlockedToken(SYMM, msg.sender);

		VestingPlan storage symmVestingPlan = vestingPlans[SYMM][msg.sender];
		uint256 symmLockedAmount = symmVestingPlan.lockedAmount();
		if (symmLockedAmount < amount) revert InvalidAmount();

		_ensureSufficientBalance(SYMM, amount);

		// Add liquidity to the pool.
		(amountsIn, lpAmount) = _addLiquidity(amount, minLpAmount, maxUsdcIn);

		// Update SYMM vesting plan by reducing the locked amount.
		symmVestingPlan.resetAmount(symmLockedAmount - amountsIn[0]);

		// Claim any unlocked SYMM LP tokens.
		_claimUnlockedToken(SYMM_LP, msg.sender);

		VestingPlan storage lpVestingPlan = vestingPlans[SYMM_LP][msg.sender];

		address[] memory users = new address[](1);
		users[0] = msg.sender;
		uint256[] memory amounts = new uint256[](1);
		amounts[0] = lpVestingPlan.lockedAmount() + lpAmount;

		// Increase the locked amount by the received LP tokens.
		if (lpVestingPlan.isSetup()) {
-			_resetVestingPlans(SYMM_LP, users, amounts);
+			_resetVestingPlans(SYMM_LP, users, amounts, true);
		} else {
			_setupVestingPlans(SYMM_LP, block.timestamp, symmVestingPlan.endTime, users, amounts);
		}

		emit LiquidityAdded(msg.sender, amountsIn[0], amountsIn[1], lpAmount);
	}

	function _resetVestingPlans(address token, address[] memory users, uint256[] memory amounts, bool isAddLiquidity) internal {
		if (users.length != amounts.length) revert MismatchArrays();
		uint256 len = users.length;
		for (uint256 i = 0; i < len; i++) {
			address user = users[i];
			uint256 amount = amounts[i];
			// Claim any unlocked tokens before resetting.
			_claimUnlockedToken(token, user);
			VestingPlan storage vestingPlan = vestingPlans[token][user];
-			if (amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
+			if (!isAddLiquidity && amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
			uint256 oldTotal = vestingPlan.lockedAmount();
			vestingPlan.resetAmount(amount);
			totalVested[token] = totalVested[token] - oldTotal + amount;
			emit VestingPlanReset(token, user, amount);
		}
	}
``` 