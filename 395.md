Tiny Magenta Jay

High

# Staking reward rate can be diluted indefinitely by a malicious actor

### Summary

In `SymmStaking` contract, anyone can add reward of a whitelisted token by calling `notifyRewardAmount`. Upon new rewards are added, a ongoing reward distribution event will be prolonged, and its rate will be increased. However, this can be exploited by an attacker to dilute the reward constantly, causing users to get less reward over time.

### Root Cause

[`_addRewardsForToken`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L366) is called internally in `notifyRewardAmount`:
```solidity
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];

		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration;
		}

		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
	}
```

When the rewarding period is still ongoing, it would calculates the remaining total rewards left, then effectively creates a new reward period with the new total rewards. By default, duration is 1 week. In `notifyRewardAmount`, if amount is 0, it would just skip and continue to next item in array:
```solidity
	function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		_updateRewardsStates(address(0));
		if (tokens.length != amounts.length) revert ArraysMismatched();

		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
			address token = tokens[i];
			uint256 amount = amounts[i];

			if (amount == 0) continue;
			if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
			pendingRewards[token] += amount;
			_addRewardsForToken(token, amount);
		}
		emit RewardNotified(tokens, amounts);
	}
```

Because of this, a malicious actor can keep adding 1 wei of reward once in a while, to prolong the reward period, and dilute the reward rate, in some extreme cases, it can cause reward rate to be rounded down to 0 for low decimal tokens such as USDC and USDT.

Here are some math as proof:
- In a real world scenario, 1 week is 604800 seconds, which is the duration of a single reward period. 
- We suppose initially there are 100000e6 USDC being added, so the reward rate will be about 165000 wei of USDC per second. 
- After 3.5 days, there are 50000e6 USDC left, and a malicious user adds 1 wei of USDC, the new total reward will be 50000e6+1, which we round it as just 50000e6 USDC, with the new rate being 50000e6/604800=82600 wei of USDC per second
- After another few days, the attacker do this again, the new reward will be about 40000~ish
- Attacker can do this multiple times, with little cost, as gas fee on BASE is cheap, and 1 wei of USDC is also low
- Eventually to a point, The reward will be less than 604800, and due to precision and rounding down, the reward rate will be 0.

### Internal Pre-conditions

1. Some amount of reward is added

### External Pre-conditions

N/A

### Attack Path

1. Attacker adds reward of 1 wei token once a while, diluting the reward rate, as well as prolonging the end date indefinitely

### Impact

In theory, almost all rewards will be claimed eventually, but this attack would reduce the reward rate, so for the same time, users will get less rewards by comparison. In the mathematical example above, normally for a 7 days period, user would get in total 100000e6 USDC, but with the attack, user can only get about 75000e6. If attacker is doing this more often, this can be even less. 

### PoC

_No response_

### Mitigation

Make `notifyRewardAmount` a privilege function which can only be called by trusted parties. Other users who wants to contribute to the reward pool can transfer tokens to the protocol team first.