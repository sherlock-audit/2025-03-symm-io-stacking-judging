Cold Coal Caribou

Medium

# Endless Reward Period Vulnerability with 1 wei Reward Additions

### Summary

An attacker could add 1 wei of rewards repeatedly over time and extend the finish period of a distribution, reducing the rate and not allowing it to properly end.

### Root Cause

The `SymmStaking::notifyRewardAmount` function allows anyone to add an `amount` to a rewards distribution of any reward token:
[SymmStaking.sol#L275-L292](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L275-L292)
```solidity
	function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		_updateRewardsStates(address(0));
		if (tokens.length != amounts.length) revert ArraysMismatched();
		
		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
			address token = tokens[i];
			uint256 amount = amounts[i];
			
			if (amount == 0) continue;
			if (!isRewardToken[token]) revert TokenNotWhitelisted(token);
			
			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
			pendingRewards[token] += amount;
			_addRewardsForToken(token, amount);
		}
		emit RewardNotified(tokens, amounts);
	}
```

This previous function, calls the `SymmStaking::_addRewardsForToken` function which adds the `amount` into the distribution and calculates a new rate:
[SymmStaking.sol#L366-L379](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L366-L379)
```solidity
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];
		
		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration; // @audit ISSUE HERE
		}
		
		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration; // @audit ISSUE HERE
	}
```

As you can see, a new rate is calculated: `state.rate = (amount + leftover) / state.duration;`

And a new `state.periodFinish` is set: `state.periodFinish = block.timestamp + state.duration;`

This introduces an issue where, anybody can add 1 wei of rewards of any token and extend the distribution, and reducing the rate.

In the first distribution, the `rate` is set to be constant across the `duration` (1 week) of the distribution. However, anybody could exploit this and extend the rewards distribution, making the `rate` go from a constant distribution in a week, to an exponential decay distribution in several weeks.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

- Attacker donates 1 wei to the reward distribution every single day, indeterminately extending the distribution.

### Impact

- Distribution indeterminately extended. Distribution `rate` suffers an exponential decay.

### PoC

None

### Mitigation

Only let the `admin` or a trusted party call `SymmStaking::notifyRewardAmount`.