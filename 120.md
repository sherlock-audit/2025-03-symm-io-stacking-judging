Stale Bamboo Donkey

Medium

# User can force their vesting schedule to begin immediately instead of waiting for `startTime`

### Summary

Users can manipulate their vesting schedules to start immediately instead of waiting for the future `vestingSchedule.startTime`. This also allows them to supply the pool in order to vest LP tokens earlier as well.

### Root Cause

When the admins create a vesting schedule for a user, the user has 2 options:

1) Wait time to pass for tokens to unlock and claim
2) Claim locked tokens prematurely but a penalty is applied

But let's see what happens if the user attempts to [claim 0 locked tokens](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/vesting/Vesting.sol#L281-L299):

```solidity
	function _claimLockedToken(address token, address user, uint256 amount) internal {
		// First, claim any unlocked tokens.
		_claimUnlockedToken(token, user);
		VestingPlan storage vestingPlan = vestingPlans[token][user];
		if (vestingPlan.lockedAmount() < amount) revert InvalidAmount();

		// Adjust the vesting plan
@>		vestingPlan.resetAmount(vestingPlan.lockedAmount() - amount);
		totalVested[token] -= amount;
		uint256 penalty = (amount * lockedClaimPenalty) / 1e18;

		// Ensure sufficient balance (minting if necessary)
		_ensureSufficientBalance(token, amount);

		IERC20(token).transfer(user, amount - penalty);
		IERC20(token).transfer(lockedClaimPenaltyReceiver, penalty);

		emit LockedTokenClaimed(token, user, amount, penalty);
	}
```

There are no checks that disallow amount 0 and [`vestingPlan.resetAmount()`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/vesting/libraries/LibVestingPlan.sol#L71-L82) is called:

```solidity
	function resetAmount(VestingPlan storage self, uint256 amount) public returns (VestingPlan storage) {
		if (claimable(self) != 0) revert ShouldClaimFirst();
		if (!isSetup(self)) revert ShouldSetupFirst();
		// Rebase the vesting plan from now.
		uint256 remaining = remainingDuration(self);
		if (remaining == 0) revert PlanIsFinished();
@>		self.startTime = block.timestamp;
		self.endTime = block.timestamp + remaining;
		self.amount = amount;
		self.claimedAmount = 0;
		return self;
	}
```

Which will reset the user's `vestingPlan.startTime` to `block.timestamp`.

Essentially, a user with a vesting schedule that starts in the future can make a call to claim 0 locked tokens:

- The call will pass due to a missing `amount > 0` check
- No penalty will be taken since amount is 0

This allows the user to reset their vesting schedule to start immediately, manipulating the whole functionality.

### Internal Pre-conditions

1. Vesting schedule with future start time must be schedule for the user

### External Pre-conditions

None

### Attack Path

1. Admins create vesting schedule with future start time for user
2. User calls `claimLockedToken(token, 0)`

### Impact

Users can manipulate the vesting schedule to start earlier than anticipated, which also allows them to add SYMM liquidity and start their LP vesting earlier. 

Even though the duration will be longer than what the admin setup, the end date is the same and user will still receive the total vested amount by the end date. However, if for example, a vesting is setup with a duration of 7 days, the user can make it start immediately a week earlier, with duration of 14 days. Although the vest rate will be diluted, by the originally planned "start date" they would've already unlocked half of their tokens penalty-free.

### PoC

_No response_

### Mitigation

Don't allow claiming locked tokens before vesting start time would combat this. An `amount > 0` check also seems like a good idea.