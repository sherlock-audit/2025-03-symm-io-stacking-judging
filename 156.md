Great Cinnabar Lynx

Medium

# Fee-on-transfer token accounting mismatch in `SymmStaking` contract

### Summary

Administrators can whitelist fee-on-transfer tokens according to the documentation. Additionally, standard tokens like USDT or USDC, which currently do not charge fees, may introduce them in the future.

> _"Some tokens take a transfer fee (e.g. STA, PAXG), some do not currently charge a fee but may do so in the future (e.g. USDT, USDC)."_
[Source](https://github.com/d-xo/weird-erc20?tab=readme-ov-file#fee-on-transfer) (from the contest page)

When fee-on-transfer tokens are used, the contract records pre-fee amounts but only receives post-fee quantities. This discrepancy accumulates over time, leading to a growing shortfall that can eventually prevent all remaining users from claiming their rightfully earned rewards across all token types.

### Root Cause

In the `SymmStaking` contract, the [notifyRewardAmount](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L275-L292) function does not verify that the actual received token amount matches the amount specified in the function call. It increases `pendingRewards[token]` by the pre-fee amount, not the actual amount received:

```solidity
function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
    _updateRewardsStates(address(0));
    if (tokens.length != amounts.length) revert ArraysMismatched();

    uint256 len = tokens.length;
    for (uint256 i = 0; i < len; i++) {
        address token = tokens[i];
        uint256 amount = amounts[i];

        if (amount == 0) continue;
        if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

        IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
>       pendingRewards[token] += amount;
        _addRewardsForToken(token, amount);
    }
    emit RewardNotified(tokens, amounts);
}
```

### Internal Pre-conditions

1. Admin needs to call `configureRewardToken` to whitelist a fee-on-transfer token
2. A reward notifier needs to call `notifyRewardAmount` (can be called by anyone) with the fee-on-transfer token
3. Multiple notifications over time compound the accounting error

### External Pre-conditions

1. The whitelisted token must implement a fee mechanism during transfers (e.g., 5% fee)

### Attack Path

1. Admin whitelists a token with fee-on-transfer mechanism (e.g., 5% fee)
2. Anyone calls `notifyRewardAmount` with 10,000 tokens weekly
3. Due to 5% fee, only 9,500 tokens are actually transferred to the contract
4. Contract records that 10,000 tokens are available in `pendingRewards`
5. This process repeats weekly, creating a growing discrepancy
6. After 10 weeks, the shortfall reaches approximately 5,000 tokens
7. Users who claim early receive their full rewards
8. Once ~95% of the recorded rewards have been claimed, remaining claims begin to fail
9. All subsequent claims revert due to insufficient token balance

### Impact

The issue creates a cumulative shortfall that eventually prevents a significant portion of users from claiming their earned rewards. With a typical 5% fee and weekly notifications, after 10 weeks approximately 5% of users would be completely unable to claim any rewards.

If the `safeTransfer` for any single token reverts due to insufficient balance, the entire transaction will fail. This means that even if only one of many reward tokens has the fee-on-transfer issue, users will be unable to claim any of their rewards across all token types.

### PoC

_No response_

### Mitigation

Add validation to ensure the actual received amount is used for reward calculations:

```diff

function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
    _updateRewardsStates(address(0));
    if (tokens.length != amounts.length) revert ArraysMismatched();

    uint256 len = tokens.length;
    for (uint256 i = 0; i < len; i++) {
        address token = tokens[i];
        uint256 amount = amounts[i];

        if (amount == 0) continue;
        if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

-       IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
-       pendingRewards[token] += amount;
-       _addRewardsForToken(token, amount);

+       uint256 balanceBefore = IERC20(token).balanceOf(address(this));
+       IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
+       uint256 actualReceived = IERC20(token).balanceOf(address(this)) - balanceBefore;
+       pendingRewards[token] += actualReceived; // use actual received amount
+       _addRewardsForToken(token, actualReceived); // calculate rates with actual amount
    }
    emit RewardNotified(tokens, amounts);
}
```