Shiny Mulberry Falcon

Medium

# Attacker can prevent token removal

### Summary

`SymmStaking` contract has functionality to add and remove tokens from whitelist. This feature can be easily DoSed by an attacker due to the fact that rewards can be notified by any address.

### Root Cause

`configureRewardToken` is used to add or remove tokens from whitelist. The token can't be removed if there are more than `10` units of reward token still pending for withdrawal.

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L312-L335

```solidity
function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));

		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);

		isRewardToken[token] = status;
		if (!status) {
			if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}

		emit UpdateWhitelist(token, status);
	}
```

When the protcol wants to remove the token, when the pending rewards are less than or equal to `10` units of that token, an attacker can call `notifyRewardAmount` and add `11` units of reward token to prevent the removal.

```solidity
function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		_updateRewardsStates(address(0));
		if (tokens.length != amounts.length) revert ArraysMismatched();

		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
			address token = tokens[i];
			uint256 amount = amounts[i];

			if (amount == 0) continue;
			if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
			pendingRewards[token] += amount;
			_addRewardsForToken(token, amount);
		}
		emit RewardNotified(tokens, amounts);
	}
```

### Internal Pre-conditions

1. Token must be whitelisted
2. `pendingRewards` for that token must be less than or equal to 10 units
3. Protocol wants to remove the token from the whitelist

### External Pre-conditions

1. Attacker must have `11` units of tokens per week to prevent tokens removal

### Attack Path

1. Protocol calls `configureRewardToken` to remove token from the whitelist
2. Attacker calls `notifyRewardAmount` to add `11` units of token as rewards
3. Token can't be removed

### Impact

System's functionality is disrupted as tokens can't be removed from whitelist which is one of the main goals of this function.

### PoC

_No response_

### Mitigation

Remove the check in `configureRewardToken` so that token can be removed any time when the expected rewards provided by the protocol are around zero.