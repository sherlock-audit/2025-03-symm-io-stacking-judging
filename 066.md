Cold Coal Caribou

Medium

# Reward Token De-Whitelisting DoS Because of  Precision Loss in `rewardState.rate` Calculation

### Summary

When `SymmStaking::notifyRewardAmount` is called, the parameter `amount` is added in `pendingRewards[token]` but in `SymmStaking::_addRewardsForToken` the `rewardState.rate` calculation won't distribute `amount` exactly because of precision loss in calculation, DoSing the `SymmStaking::configureRewardToken` function when removing a reward token from the whitelist.

### Root Cause

In  `SymmStaking::notifyRewardAmount`, the parameter `amount` is directly added  in `pendingRewards[token]`:
[SymmStaking.sol#L275-L292](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L275-L292)
```solidity
	function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		_updateRewardsStates(address(0));
		if (tokens.length != amounts.length) revert ArraysMismatched();
		
		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
			address token = tokens[i];
			uint256 amount = amounts[i];
			
			if (amount == 0) continue;
			if (!isRewardToken[token]) revert TokenNotWhitelisted(token);
			
			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
			pendingRewards[token] += amount;
			_addRewardsForToken(token, amount);
		}
		emit RewardNotified(tokens, amounts);
	}
```

Then, in `SymmStaking::_addRewardsForToken` the `rewardState.rate` calculation is done in order to know how will the `amount` of `token` be distributed over time (default to a week):
[SymmStaking.sol#L366-L379](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L366-L379)
```solidity
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];
		
		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration; // @audit ISSUE HERE
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration; // @audit ISSUE HERE
		}
		
		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
	}
```

However, the `rate` calculation does not consider the precision loss that can occur, which is up to 1 week in seconds (604800 wei).

There will be an under distribution of  `amount % state.duration` for the base case, or `(amount + leftover) % state.duration` in the other.

This has a several consequence because as `pendingRewards` will never get to be less than 10 wei, the `SymmStaking::configureRewardToken` function will revert when the `REWARD_MANAGER_ROLE` try to remove the `token` from the whitelist (list of available reward tokens), meaning that `status == false`:
[SymmStaking.sol#L312-L335](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L312-L335)
```solidity
	function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));
		
		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);
		
		isRewardToken[token] = status;
		if (!status) {
			if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}
		
		emit UpdateWhitelist(token, status);
	}
```

The revert occurs in the following piece of code:
```solidity
if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
```

As `SymmStaking::notifyRewardAmount` can be called by anyone, the issue can be triggered without the need of an admin passing values not divisible by 1 week in seconds.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

- Attacker creates a small distribution of more than 10 wei per reward token.
- Distributor himself causes the issue by not knowing this calculation error.

### Impact

- `SymmStaking::rewardTokens` list will only be able to grow, not decrease. Limiting protocol functionality and leading to a DoS if more tokens are added.

### PoC

None

### Mitigation

Calculate the amount that won't get distributed, return it to the distributor and substract it from the `pendingRewards`.

```diff
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];
+		uint256 undistributed;
		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
+			undistributed = amount % state.duration;
+			if(undistributed != 0) {IERC20(token).safeTransfer(msg.sender, undistributed)}
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration;
+			undistributed = (amount + leftover) % state.duration;
+			if(undistributed != 0){IERC20(token).safeTransfer(msg.sender, undistributed)}
		}
+       pendingRewards[token] -= undistributed;
		
		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
	}
```