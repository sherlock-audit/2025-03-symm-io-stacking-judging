Attractive Cornflower Coyote

Medium

# Admin cannot remove the reward token when a user is blacklisted by it

### Summary

The protocol user can be blacklisted by the reward token (e.g. USDC). If admin wants to remove USDC, the pending reward must be claimed first. However, a USDC blacklisted user cannot claim USDC actively or passively, which blocks the whole protocol with unclaimable pending when USDC is to be removed as the reward token.

### Root Cause

In `SymmStaking.sol:320`, removing a reward token only allows a dust amount in the protocol.

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L312-L335

```solidity
	function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));

		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);

		isRewardToken[token] = status;
		if (!status) {
@>			if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}

		emit UpdateWhitelist(token, status);
	}
```

Such an amount `pendingRewards[token]` can only be decreased by `_claimRewardsFor()`.

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L392

```solidity
	function _claimRewardsFor(address user) internal {
		uint256 length = rewardTokens.length;
		for (uint256 i = 0; i < length; ) {
			address token = rewardTokens[i];
			uint256 reward = rewards[user][token];
			if (reward > 0) {
				rewards[user][token] = 0;
@>				pendingRewards[token] -= reward;
				IERC20(token).safeTransfer(user, reward);
				emit RewardClaimed(user, token, reward);
			}
			unchecked {
				++i;
			}
		}
	}
```

However, if claim fails, `pendingRewards[token]` will block `configureRewardToken()`. This is the issue that a blacklisted user can cause.


### Internal Pre-conditions

Admin sets a reward token with blacklist mechanism (e.g. [USDC](https://discord.com/channels/812037309376495636/1347584645310451803/1347956948733267989)).

### External Pre-conditions

None

### Attack Path

None

### Impact

Admin cannot remove the reward token and leaves it whitelisted with risk.

### PoC

_No response_

### Mitigation

Remove the pending amount check to enforce the removal, if complete claim can be ensured.

```diff
	function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));

		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);

		isRewardToken[token] = status;
		if (!status) {
-			if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}

		emit UpdateWhitelist(token, status);
	}
```