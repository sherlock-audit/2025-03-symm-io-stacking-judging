Brief Tartan Bear

High

# User can experience DoS when calling `SymmVesting::addLiquidity` second time after some duration

### Summary

When user call `addLiquidity` they would add liquidity to POOL using the locked vested SYMM token, and then the resulting LP token is vested.
If after given duration user call `addLiquidity` again, the second LP token amount would be vested but it would wrongly [checked](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L231) against how many LP Token that already unlocked at given duration. This unnecessary check would cause revert.

### Root Cause

internal function `_resetVestingPlans` are used by [`resetVestingPlans`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L118-L124) function that can be called by `SETTER_ROLE` but also used in [SymmVesting.sol#L154-L156](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/SymmVesting.sol#L154-L156) to increase the amount LP token when user add liquidity for the second time.

this internal function only problematic if the `token` parameter is LP token:

[Vesting.sol#L222-L237](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L222-L237)
```Solidity
	function _resetVestingPlans(address token, address[] memory users, uint256[] memory amounts) internal {
		if (users.length != amounts.length) revert MismatchArrays();
		uint256 len = users.length;
		for (uint256 i = 0; i < len; i++) {
			address user = users[i];
			uint256 amount = amounts[i];
			// Claim any unlocked tokens before resetting.
			_claimUnlockedToken(token, user);
			VestingPlan storage vestingPlan = vestingPlans[token][user];
@>  		if (amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
			uint256 oldTotal = vestingPlan.lockedAmount();
			vestingPlan.resetAmount(amount);
			totalVested[token] = totalVested[token] - oldTotal + amount;
			emit VestingPlanReset(token, user, amount);
		}
	}
```

notice that it checks `amount < vestingPlan.unlockedAmount()` where:
1. the `amount` can be varying depends on POOL state and the amount user willing to add for the pool.
2. `vestingPlan.unlockedAmount()` would always increase with duration

so there are case where user would add more liquidity but unable to do so because of this checks.

### Internal Pre-conditions

1. user have vesting plan for them (1000e18 symm for 2 days)
2. user add liquidity for the first time by a fraction amount of locked token (for the test we use 60% of total amount)

### External Pre-conditions

1. after some time (1.5 days), user wants to add liquidity again with their current locked token in vesting

### Attack Path

1. user call `SymmVesting::addLiquidity` and use 90e18 symm token as amount
2. the call revert `AlreadyClaimedMoreThanThis()`

### Impact

1. user would lose potential profit (in form of fees) because they unable to increase their LP position using their own vested locked amount of SYMM token to the POOL
2. this would disrupt the system's functionality, which is the main point of this audit as stated in [README](https://github.com/sherlock-audit/2025-03-symm-io-stacking-farismln/tree/main?tab=readme-ov-file#q-additional-audit-information)

### PoC

to add the test file, do the following:
1. in terminal and root directory, use `npm i --save-dev @nomicfoundation/hardhat-foundry`
2. Add `require("@nomicfoundation/hardhat-foundry");` to the top of your hardhat.config.ts file.
3. run `npx hardhat init-foundry` this would generate `foundry.toml` in the directory
4. add the following to bottom of `foundry.toml` with your own infura api key:
```toml
[rpc_endpoints]
base = "https://base-mainnet.infura.io/v3/your_api_key"
```
5. then in the `tests` folder, add the following file and name it `VestingPoC.t.sol`:
```Solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.18;

import "../contracts/vesting/SymmVesting.sol";
import "../contracts/vesting/Vesting.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import "forge-std/Test.sol";

contract SymmVestingTest is Test {
    SymmVesting vesting;

    address pool = 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995;
    address router = 0x76578ecf9a141296Ec657847fb45B0585bCDa3a6;
    address permit2 = 0x000000000022D473030F116dDEE9F6B43aC78BA3;
    address vault = 0xbA1333333333a1BA1108E8412f11850A5C319bA9;
    address symm = 0x800822d361335b4d5F352Dac293cA4128b5B605f;
    address usdc = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
    address symm_lp = 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995;


    address owner = makeAddr("owner");
    address penaltyReceiver = makeAddr("penaltyReceiver");
    address victim = makeAddr("victim");
    address attacker = makeAddr("attacker");

    uint256 startTime = 10_000;
    uint256 duration = 2 days;

    function setUp() public {
        vm.warp(startTime);
        SymmVesting impl = new SymmVesting();
        TransparentUpgradeableProxy proxy = new TransparentUpgradeableProxy(address(impl), address(owner), abi.encodeWithSelector(impl.initialize.selector, owner, penaltyReceiver, address(pool), address(router), address(permit2), address(vault), address(symm), address(usdc), address(symm_lp)));
        vesting = SymmVesting(address(proxy));
    }

    function test_PoC_addLPWhenAlreadyHaveLP_revert() public {
        // first we add symm token to vesting contract
        uint256 vestAmount = 1000e18;
        deal(symm, address(vesting), vestAmount);

        address[] memory users = new address[](1);
        users[0] = victim;
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = vestAmount;

        // setup vesting plan for victim, 1000e18 symm token, 2 days
        vm.prank(owner);
        vesting.setupVestingPlans(symm, startTime, startTime + duration, users, amounts);

        // deal usdc to victim 1000 usdc
        deal(usdc, victim, 1000e6);

        // victim add liquidity with locked symm token
        vm.startPrank(victim);
        uint256 symmAmount = 600e18;
        // ensure the symmAmount < lockedAmount
        assertLt(symmAmount, vesting.getLockedAmountsForToken(victim, symm));
        IERC20(usdc).approve(address(vesting), 1000e6);
        (uint256[] memory amountsIn, uint256 lpAmount) = vesting.addLiquidity(symmAmount, 0, 1000e6);
        console.log("symm amount:", amountsIn[0]);
        console.log("usdc amount:", amountsIn[1]);
        console.log("lpAmount", lpAmount);

        // 3/4 duration passes
        vm.warp(startTime + 1.5 days);

        // victim add liquidity again with locked symm token
        symmAmount = 90e18;
        // ensure that symmAmount < lockedAmount
        assertLt(symmAmount, vesting.getLockedAmountsForToken(victim, symm));
        vm.expectRevert(Vesting.AlreadyClaimedMoreThanThis.selector);
        (amountsIn, lpAmount) = vesting.addLiquidity(symmAmount, 0, 1000e6);
    }
}
```
6. run `forge t --fork-url base`, the test would revert as expected:
```bash
Ran 1 test for tests/VestingPoC.t.sol:SymmVestingTest
[PASS] test_PoC_addLPWhenAlreadyHaveLP_revert() (gas: 1352590)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 19.40s (14.53s CPU time)

Ran 1 test suite in 21.48s (19.40s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

in `_resetVestingPlans` differentiate the check if the token is LP tokens. example:

```diff
diff --git a/token/contracts/vesting/Vesting.sol b/token/contracts/vesting/Vesting.sol
index afaffcd..f6c5792 100644
--- a/token/contracts/vesting/Vesting.sol
+++ b/token/contracts/vesting/Vesting.sol
@@ -228,7 +229,9 @@ contract Vesting is Initializable, AccessControlEnumerableUpgradeable, PausableU
                        // Claim any unlocked tokens before resetting.
                        _claimUnlockedToken(token, user);
                        VestingPlan storage vestingPlan = vestingPlans[token][user];
-                       if (amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
+            if (token == 0x800822d361335b4d5F352Dac293cA4128b5B605f) {
+                if (amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
+            }
                        uint256 oldTotal = vestingPlan.lockedAmount();
                        vestingPlan.resetAmount(amount);
                        totalVested[token] = totalVested[token] - oldTotal + amount;
```

if we run `forge t --fork-url base` again, the test would fail:
```bash
Failing tests:
Encountered 1 failing test in tests/VestingPoC.t.sol:SymmVestingTest
[FAIL: next call did not revert as expected] test_PoC_addLPWhenAlreadyHaveLP_revert() (gas: 1561264)

Encountered a total of 1 failing tests, 0 tests succeeded
```