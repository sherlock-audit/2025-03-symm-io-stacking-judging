Scruffy Shadow Whale

High

# Admin can withdraw staking tokens through rescueTokens() function causing permanent loss of user funds

### Summary

Lack of validation in the `rescueTokens()` function will cause a permanent loss of funds for users as admin will be able to withdraw the staking tokens, making it impossible for users to withdraw their staked tokens even though their balances remain unchanged in the contract state.

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L6

### Root Cause

The [`rescueTokens()`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L343) function in the `SymmStaking` contract allows the contract admin to withdraw any token from the contract, including the staking token itself. 

```Solidity
function rescueTokens(address token, uint256 amount, address receiver)
    external
    nonReentrant
    onlyRole(DEFAULT_ADMIN_ROLE)
{
    // @audit-issue Admin can withdraw staking tokens
    IERC20(token).safeTransfer(receiver, amount);
    emit RescueToken(token, amount, receiver);
}
```

The function lacks any check to prevent the admin from withdrawing the staking token, which could lead to a contract state where the accounting variables (`totalSupply` and `balanceOf`) no longer match the actual token balance, effectively breaking the withdrawal functionality.

### Internal Pre-conditions

1. Contract needs to be deployed
2. Users have deposited staking tokens into the contract
3. Admin calls rescueTokens() with the staking token address

### External Pre-conditions

None

### Attack Path

1. User deposits 100 tokens into the staking contract
2. Admin calls `rescueTokens(stakingToken, 100, admin)` to extract the staking tokens (possibly mistakenly, not realizing these are user funds)
3. The contract's staking token balance is reduced to 0
4. User tries to withdraw their tokens but the transaction reverts because there are no actual tokens in the contract
5. User's funds are permanently lost while their balances in the contract state remain unchanged

### Impact

The users suffer a complete loss of their principal as they can no longer withdraw their staked tokens. This represents a critical breach of the contract's core functionality:

1. User funds are permanently locked in the system with no possibility of recovery
2. The contract state becomes inaccurate as it shows balances that cannot be withdrawn
3. This breaks the core functionality of the staking contract, which is to allow users to stake and unstake their tokens

### PoC

Install foundry:

1. npm install --save-dev @nomicfoundation/hardhat-foundry
2. In the hardhat config : import "@nomicfoundation/hardhat-foundry";
3. npx hardhat init-foundry

Create a new test file called SymmStakingFoundry.t.sol and paste the code into the file.

then run it with the command:

`forge test --mt test_adminBreaksWithdrawal -vvv`

```Solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

import {MockERC20} from "../../contracts/mock/MockERC20.sol";
import {SymmStaking} from "../../contracts/staking/SymmStaking.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Test} from "forge-std/Test.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "forge-std/console2.sol";


contract SymmStakingFoundry is Test {
    MockERC20 stakingToken;
    MockERC20 usdc;
    MockERC20 usdt;
    SymmStaking staking;

    address admin = address(0x1000000000000000000000000000000000000000);

    address[] users;
    uint256[] amounts;

    uint256 startTime;
    uint256 endTime;

    // tracking for user deposits and withdrawals
    mapping(address => uint256) userDeposits;
    mapping(address => uint256) userWithdrawals;
    mapping(address => uint256) public expectedPendingRewards;
    // tracking the previous perTokenStored values
    mapping(address => uint256) private lastPerTokenStored;

    constructor() payable {
        vm.startPrank(admin);
        stakingToken = new MockERC20("SYMM", "SYMM");
        usdc = new MockERC20("USDC", "USDC");
        usdt = new MockERC20("USDT", "USDT");
        staking = new SymmStaking();
        staking.initialize(admin, address(stakingToken));
        vm.stopPrank();

        assert(stakingToken.balanceOf(address(admin)) == 10_000_000e18);
        assert(usdc.balanceOf(address(admin)) == 10_000_000e18);
        assert(usdt.balanceOf(address(admin)) == 10_000_000e18);
        assert(staking.stakingToken() == address(stakingToken));

        usdc.approve(address(staking), 10_000_000e18);
        usdt.approve(address(staking), 10_000_000e18);
        users.push(address(0x2000000000000000000000000000000000000000));
        users.push(address(0x3000000000000000000000000000000000000000));
        users.push(address(0x4000000000000000000000000000000000000000));
        users.push(address(0x5000000000000000000000000000000000000000));
        users.push(address(0x6000000000000000000000000000000000000000));

        for (uint256 i = 0; i < users.length; i++) {
            uint256 amount = 2000000e18;

            amounts.push(amount);

            vm.prank(admin);
            stakingToken.transfer(users[i], amount);

            vm.prank(users[i]);
            stakingToken.approve(address(staking), amount);

            assert(stakingToken.balanceOf(users[i]) == amount);
            assert(stakingToken.allowance(users[i], address(staking)) == amount);
        }

        configureRewardToken(true, true, true);
    }

    function configureRewardToken(bool setUsdc, bool setUsdt, bool setMockUSDC) public {
        vm.startPrank(admin);
        staking.configureRewardToken(address(usdc), setUsdc);
        staking.configureRewardToken(address(usdt), setUsdt);
        vm.stopPrank();
    }

    function test_adminBreaksWithdrawal() public {
        address user = users[0];
        uint256 amount = 100e18;

        vm.prank(user);
        staking.deposit(amount, user);

        // balance
        uint256 userBalance = staking.balanceOf(user);
        assertEq(userBalance, amount);

        // admin balance of stakingToken
        uint256 adminBalanceBefore = stakingToken.balanceOf(admin);
        // Admin calls rescueToken to get the stakingToken which shouldn't be possible because
        // it is the user's balance
        vm.prank(admin);
        staking.rescueTokens(address(stakingToken), amount, admin);
        uint256 adminBalanceAfter = stakingToken.balanceOf(admin);
        assertEq(adminBalanceAfter, adminBalanceBefore + amount);

        // user balance doesn't change
        userBalance = staking.balanceOf(user);
        console2.log("\nuserBalance", userBalance);
        assertEq(userBalance, amount);

        vm.startPrank(user);
        vm.expectRevert();
        staking.withdraw(amount, user);
        vm.stopPrank();
    }

    function _getRewardState(address token) internal view returns (SymmStaking.TokenRewardState memory) {
        (uint256 duration, uint256 periodFinish, uint256 rate, uint256 lastUpdated, uint256 perTokenStored) =
            staking.rewardState(token);
        return SymmStaking.TokenRewardState({
            duration: duration,
            periodFinish: periodFinish,
            rate: rate,
            lastUpdated: lastUpdated,
            perTokenStored: perTokenStored
        });
    }
}

```



### Mitigation

 implement a safety check to ensure the contract maintains enough tokens to cover all user balances:

```Solidity
function rescueTokens(address token, uint256 amount, address receiver)
    external
    nonReentrant
    onlyRole(DEFAULT_ADMIN_ROLE)
{
    if (token == stakingToken) {
        uint256 contractBalance = IERC20(stakingToken).balanceOf(address(this));
        require(contractBalance - amount >= totalSupply, "Cannot withdraw staked tokens");
    }
    
    IERC20(token).safeTransfer(receiver, amount);
    emit RescueToken(token, amount, receiver);
}
```