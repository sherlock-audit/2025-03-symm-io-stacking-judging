Polished Chambray Sealion

High

# [H-01] - Missing Storage Gap in Upgradeable Contract Leads to Storage Collision and Data Corruption

### Summary

The `SymmStaking` contract ia designed as an upgradeable contract using the Transparent Upgradeable Proxy pattern: https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L15

It lacks a storage gap (e.g. `uint256[50] private __gap;`) at the end of the contract. This omission allows new state variables introduced in future upgrades to overwrite existing storage slots, such as `totalSupply` or `balanceOf`, leading to unintended data corruption. Without a reserved gap, maintaining a consistent storage layout across upgrades becomes error-prone, especially with complex inheritance or variable additions.

### Root Cause

In upgradeable contracts, like those using the Transparent Upgradeable Proxy pattern, a storage gap is a critical best practice. It reserves empty storage slots to accommodate new state variables in future upgrades without disrupting the existing storage layout.

In the `SymmStaking.sol` contract, there’s no such gap. This means that if a future version of the contract adds new variables, it could shift the positions of existing variables—like totalSupply, balanceOf, or rewardState—leading to storage collisions. 



### Internal Pre-conditions

One of the following needs to be the case. Either:
A. Admin upgrades the contract with a new variable added right before or after an existing (possibly crucial) variable

Or:
B. Admin upgrades the contract and changes the order of an import. For example importing 'PausableUpgradeable' before 'ReentrancyGuardUpgradeable'

See:
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L7
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L8


### External Pre-conditions

- None required; the vulnerability depends solely on a contract upgrade.

### Attack Path

1. User calls `deposit(999 ether)` which sets `totalSupply` to 999 ether in `SymmStaking`.
2. Admin deploys `SymmStakingV2` with a new variable (e.g., `newFeature`) in storage slot 1.
3. Admin calls `ProxyAdmin.upgradeAndCall()` to upgrade the proxy to `SymmStakingV2`.
4. Admin sets `NewFeature` to 10 ether in `SymmStakingV2`, overwriting `totalSupply`’s slot, corrupting it from 999 ether to 10 ether.

### Impact

High. Storage collisions can corrupt critical data like `totalSupply` or `balanceOf`, potentially causing loss of staked funds, incorrect reward calculations, or complete contract malfunction. Given `SymmStaking` manages user stakes and rewards, this could result in significant financial loss or operational failure.

### PoC

```solidity

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.18;

import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/console.sol";

import {Initializable} from "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {TransparentUpgradeableProxy, ITransparentUpgradeableProxy} from "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import {ProxyAdmin} from "@openzeppelin/contracts/proxy/transparent/ProxyAdmin.sol";


// V1 Contract with initial Storage Layout
contract StakingV1 is Initializable {
    using SafeERC20 for IERC20;

    address public stakingToken; // Slot 0
    uint256 public totalSupply;  // Slot 1 
    mapping(address => uint256) public balanceOf; // Slot 2

    function initialize(address _stakingToken) external initializer {
        stakingToken = _stakingToken;
    }

    function deposit(uint256 amount) external {
        IERC20(stakingToken).safeTransferFrom(msg.sender, address(this), amount);
        totalSupply += amount;
        balanceOf[msg.sender] += amount;
    }
}

// In the V2 Upgrade - all that needs to happen for the storage layout to break severely,
// is the introduction of a new variable near one of the old (unchanged) variables
// -----------------------------------------------------
// SIDENOTE: This is not the only way the storage layout can break.
// Since Solidity places inherited variables in storage before the child contract’s variables, in the order of inheritance.
// 
// Example:
// If a new version simply REORDERS inheritance (e.g., PausableUpgradeable before ReentrancyGuardUpgradeable):
// Old storage layout: _status (slot 1), _paused (slot 2).
// New storage layout: _paused (slot 1), _status (slot 2).
// 
// 
// ROOT CAUSE remains the same: lack of a storage gap reserving space for future upgrades
// -----------------------------------------------------
// Keeping track of storage layout manually is error-prone and can lead to severe bugs
contract StakingV2 {
    address public stakingToken; // Slot 0
    uint256 public newFeature;   // Slot 1 (collides with totalSupply from V1)
    mapping(address => uint256) public balanceOf; // Slot 2

    function setNewFeature(uint256 value) external {
        newFeature = value;
    }

    // Required for interface compatibility
    function totalSupply() external pure returns (uint256) {
        return 0;
    }
}

// Mock ERC20 token to simulate StakingToken
contract MockERC20 is IERC20 {
    mapping(address => uint256) public override balanceOf;
    mapping(address => mapping(address => uint256)) public override allowance;
    uint256 public override totalSupply;

    function mint(address to, uint256 amount) external {
        balanceOf[to] += amount;
        totalSupply += amount;
    }

    function transferFrom(address, address, uint256) external pure returns (bool) {
        // Not used
        return true;
    }

    function approve(address spender, uint256 amount) external returns (bool) {
        allowance[msg.sender][spender] = amount;
        return true;
    }

    function transfer(address, uint256) external override pure returns (bool) {
        // Not used
        return true;
    }
}

contract StakingCollisionTest is Test {
    StakingV1 public stakingV1;
    StakingV2 public stakingV2;
    TransparentUpgradeableProxy public proxy;
    MockERC20 public stakingToken;

    address public admin = address(0x1);
    address public user = address(0x2);

    function setUp() public {
        // Deploy token and contracts
        stakingToken = new MockERC20();
        stakingV1 = new StakingV1();
        proxy = new TransparentUpgradeableProxy(
            address(stakingV1),
            admin,
            abi.encodeWithSelector(StakingV1.initialize.selector, address(stakingToken))
        );
        stakingV1 = StakingV1(address(proxy));

        // Mint and approve tokens
        stakingToken.mint(user, 1000 ether);
        vm.prank(user);
        stakingToken.approve(address(proxy), 1000 ether);
    }

    function testStorageCollision() public {
        // Deposit in V1
        vm.prank(user);
        stakingV1.deposit(999 ether);
        assertEq(stakingV1.totalSupply(), 999 ether, "Deposit failed");

        uint256 slot1Before = uint256(vm.load(address(stakingV1), bytes32(uint256(1))));
        console.log("Value of storage slot1 (totalSupply) before upgrade: ", slot1Before);

        // Create V2 contract and setup a ProxyAdmin
        stakingV2 = new StakingV2();
        bytes32 adminSlot = bytes32(uint256(keccak256("eip1967.proxy.admin")) - 1);
        address proxyAdminAddr = address(uint160(uint256(vm.load(address(proxy), adminSlot))));
        ProxyAdmin proxyAdmin = ProxyAdmin(proxyAdminAddr);
        vm.prank(admin);

        // Upgrade to V2
        proxyAdmin.upgradeAndCall(ITransparentUpgradeableProxy(address(proxy)), address(stakingV2), "");

        // Set new feature in V2
        vm.prank(user);
        StakingV2(address(proxy)).setNewFeature(10 ether);

        // Check storage slot 1 (reserved for totalSupply at 999 ether) for collision
        uint256 slot1After = uint256(vm.load(address(proxy), bytes32(uint256(1))));
        console.log("Value of storage slot1 (totalSupply) after upgrade: ", slot1After);

        // Now, by setting newFeature to 10 ether in V2,
        // totalSupply (for which slot 1 was reserved) gets overwritten to 10 ether (storage collision)
        assertEq(slot1After, 10 ether, "Storage collision did not occur");
    }
}

```

### Mitigation

Add a storage gap at the end of the `SymmStaking` contract to reserve space for future variables, ensuring safe upgrades. For example:

```solidity
uint256[50] private __gap;
```

Adding the storage gap at the end, reserves slots (e.g. 9–58). New variables in upgrades use these reserved slots (starting at 9), leaving slots 0–8 (like totalSupply in 1) untouched, avoiding collisions.

It’s like leaving empty pages in a notebook—future additions don’t scribble over what’s already written. Without it, every upgrade risks clobbering existing data, as shown in the PoC.
