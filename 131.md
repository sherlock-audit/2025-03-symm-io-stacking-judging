Faint Ceramic Scorpion

Medium

# Potential Failure in `SymmVesting` Contract Initialization

### Summary

The `SymmVesting` contract's `initialize` method calls `__vesting_init` from the parent `Vesting` contract, but both functions use the `initializer` modifier from OpenZeppelin. Since `initializer` ensures a function runs only once, `SymmVesting.initialize` succeeds initially but fails when calling `__vesting_init`, as `initializer` detects the contract as already initialized. This prevents `SymmVesting` from completing initialization, making it unusable. 

### Root Cause

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L76

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/SymmVesting.sol#L55

In the `SymmVesting` contract, the `initialize` method is defined as follows:  
```solidity
function initialize(
    address admin,
    address _lockedClaimPenaltyReceiver,
    address _pool,
    address _router,
    address _permit2,
    address _vault,
    address _symm,
    address _usdc,
    address _symm_lp
) public initializer {
    if (
        admin == address(0) ||
        _lockedClaimPenaltyReceiver == address(0) ||
        _pool == address(0) ||
        _router == address(0) ||
        _permit2 == address(0) ||
        _vault == address(0) ||
        _symm == address(0) ||
        _usdc == address(0) ||
        _symm_lp == address(0)
    ) revert ZeroAddress();

    __vesting_init(admin, 500000000000000000, _lockedClaimPenaltyReceiver); // Calls parent contract's initializer

    POOL = IPool(_pool);
    ROUTER = IRouter(_router);
    PERMIT2 = IPermit2(_permit2);
    VAULT = _vault;
    SYMM = _symm;
    USDC = _usdc;
    SYMM_LP = _symm_lp;
}
```
The `SymmVesting` contract inherits from `Vesting`, which also has an initializer function:  
```solidity
function __vesting_init(address admin, uint256 _lockedClaimPenalty, address _lockedClaimPenaltyReceiver) public initializer {
    __AccessControlEnumerable_init();
    __Pausable_init();
    __ReentrancyGuard_init();

    lockedClaimPenalty = _lockedClaimPenalty;
    lockedClaimPenaltyReceiver = _lockedClaimPenaltyReceiver;

    if (admin == address(0) || _lockedClaimPenaltyReceiver == address(0)) revert ZeroAddress();

    _grantRole(DEFAULT_ADMIN_ROLE, admin);
    _grantRole(SETTER_ROLE, admin);
    _grantRole(PAUSER_ROLE, admin);
    _grantRole(UNPAUSER_ROLE, admin);
    _grantRole(OPERATOR_ROLE, admin);
}
```
Both `initialize` and `__vesting_init` are marked with `initializer`.

The `initializer` modifier in OpenZeppelin's `Initializable` contract ensures that a function can only be called once. The relevant implementation is:  
```solidity
modifier initializer() {
    require(!_initialized, "Initializable: contract is already initialized");
    _;
    _initialized = true;
}
```
- When `SymmVesting.initialize` is called, it **successfully executes** because `initialized` is `false`.  
- However, inside `initialize`, it **calls `__vesting_init`**, which **also has `initializer`**.  
- Since the `initializer` modifier checks if the contract has already been initialized, the second call **fails** because `initialized` is now `true`.  
- As a result, the **contract remains uninitialized**, preventing `SymmVesting` from functioning properly.


### Internal Pre-conditions

Contract Initialization

### External Pre-conditions

Contract Initialization

### Attack Path

Contract Initialization

### Impact

The issue prevents `SymmVesting` from being fully initialized, making the contract unusable. 

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

import "forge-std/Test.sol";
import "../contracts/vesting/SymmVesting.sol";
import "../contracts/token/symm.sol";
import { ERC1967Proxy } from "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

contract SymmVestingTest is Test {
	SymmVesting public symmVesting;

	address private POOL;
	address private ROUTER;
	address private PERMIT2;
	address private VAULT;
	address private SYMM;
	address private USDC;
	address private SYMM_LP;
	address private lockedClaimPenaltyReceiver;
	address private admin;
	address public Alice = makeAddr("Alice");
	address public Bob = makeAddr("Bob");
	Symmio public symmio;
	ERC1967Proxy private proxy;

	function setUp() public {
		vm.createSelectFork("https://mainnet.base.org", 27350300);
		POOL = vm.envAddress("POOL");
		ROUTER = vm.envAddress("ROUTER");
		PERMIT2 = vm.envAddress("PERMIT2");
		VAULT = vm.envAddress("VAULT");
		SYMM = vm.envAddress("SYMM");
		USDC = vm.envAddress("USDC");
		SYMM_LP = vm.envAddress("SYMM_LP");
		lockedClaimPenaltyReceiver = vm.envAddress("LOCKED_CLAIM_PENALTY_RECEIVER");
		admin = vm.envAddress("SYMM_ADMIN");

		proxy = new ERC1967Proxy(address(new SymmVesting()), "");
		symmVesting = SymmVesting(address(proxy));
		symmVesting.initialize(admin, lockedClaimPenaltyReceiver, POOL, ROUTER, PERMIT2, VAULT, SYMM, USDC, SYMM_LP);
		symmio = Symmio(SYMM);

		vm.startPrank(admin);
		symmio.grantRole(symmio.MINTER_ROLE(), address(symmVesting));
		vm.stopPrank();

		deal(USDC, Alice, 10000e6);
		deal(USDC, Bob, 10000e6);

		vm.prank(Alice);
		IERC20(USDC).approve(address(symmVesting), type(uint256).max);

		vm.prank(Bob);
		IERC20(USDC).approve(address(symmVesting), type(uint256).max);

		// setupVestingPlans
		uint256[] memory amounts = new uint256[](2);
		amounts[0] = 1e6 ether;
		amounts[1] = 1e6 ether;
		address[] memory users = new address[](2);
		users[0] = Alice;
		users[1] = Bob;

		vm.prank(admin);
		symmVesting.setupVestingPlans(address(symmio), block.timestamp, block.timestamp + 180 days, users, amounts);

		vm.label(SYMM, "SYMM");
		vm.label(USDC, "USDC");
		vm.label(SYMM_LP, "SYMM_LP");
		vm.label(lockedClaimPenaltyReceiver, "lockedClaimPenaltyReceiver");
		vm.label(admin, "admin");
		vm.label(address(symmVesting), "SymmVesting");
		vm.label(address(symmio), "Symmio");
		vm.label(POOL, "Pool");
		vm.label(ROUTER, "Router");
		vm.label(PERMIT2, "Permit2");
		vm.label(VAULT, "Vault");
	}

	function test_claimUnlockedToken() public {
		vm.warp(block.timestamp + 90 days);
		vm.prank(Alice);
		symmVesting.claimUnlockedToken(address(symmio));
		console.log("Alice balance %d ether", symmio.balanceOf(Alice) / 1e18);
		assertApproxEqAbs(symmio.balanceOf(Alice), 5e5 ether, 1 ether);

		(uint256 amount, uint256 claimedAmount, uint256 startTime, uint256 endTime) = symmVesting.vestingPlans(SYMM, Alice);
		console.log("amount", amount);
		console.log("claimed amount", claimedAmount);
		console.log("start time", startTime);
		console.log("end time", endTime);
	}

	function testSetup() public {
	}
}
```

Result:
```log
Ran 1 test for test/SymmVesting.t.sol:SymmVestingTest
[FAIL: InvalidInitialization()] setUp() (gas: 0)
Traces:
  [4949468] SymmVestingTest::setUp()
    ...
    ├─ [67212] → new ERC1967Proxy@0x2e234DAe75C793f67A35089C9d99245E1C58470b
    │   ├─ emit Upgraded(implementation: SymmVesting: [0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f])
    │   └─ ← [Return] 212 bytes of code
    ├─ [26921] ERC1967Proxy::fallback(0x8CF65060CdA270a3886452A1A1cb656BECEE5bA4, 0x0000000000000000000000000000000000000888, 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995, 0x76578ecf9a141296Ec657847fb45B0585bCDa3a6, 0x000000000022D473030F116dDEE9F6B43aC78BA3, 0xbA1333333333a1BA1108E8412f11850A5C319bA9, 0x800822d361335b4d5F352Dac293cA4128b5B605f, 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913, 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995)
    │   ├─ [26401] SymmVesting::initialize(0x8CF65060CdA270a3886452A1A1cb656BECEE5bA4, 0x0000000000000000000000000000000000000888, 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995, 0x76578ecf9a141296Ec657847fb45B0585bCDa3a6, 0x000000000022D473030F116dDEE9F6B43aC78BA3, 0xbA1333333333a1BA1108E8412f11850A5C319bA9, 0x800822d361335b4d5F352Dac293cA4128b5B605f, 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913, 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995) [delegatecall]       
    │   │   └─ ← [Revert] InvalidInitialization()
    │   └─ ← [Revert] InvalidInitialization()
    └─ ← [Revert] InvalidInitialization()

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 1.40s (0.00ns CPU time)

Ran 1 test suite in 2.61s (1.40s CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/SymmVesting.t.sol:SymmVestingTest
[FAIL: InvalidInitialization()] setUp() (gas: 0)

Encountered a total of 1 failing tests, 0 tests succeeded
```

### Mitigation

Modify `Vesting`:
```solidity
function __vesting_init(address admin, uint256 _lockedClaimPenalty, address _lockedClaimPenaltyReceiver) internal onlyInitializing { ... }
```
This allows `__vesting_init` to run in **a later initialization stage**, avoiding conflicts with `SymmVesting.initialize`.
