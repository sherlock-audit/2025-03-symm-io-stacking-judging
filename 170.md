Petite Yellow Anteater

Medium

# SymmStaking.sol :: configureRewardToken() can be exploited in a griefing attack if an attacker deposits an amount to a blacklisted address, preventing a whitelisted token from being removed.

### Summary

`configureRewardToken()` is used by the manager to add or remove reward tokens. The issue arises when an attacker deposits tokens to a blacklisted address, which prevents the token from being removed from the whitelist.

### Root Cause

As stated in the contest specifications, USDC will be used as the reward token, and this token is subject to a [blacklist](https://basescan.org/address/0x2Ce6311ddAE708829bc0784C967b7d77D19FD779#code#F15#L27).
>Only whitelisted tokens can work with the codebase, and these include stable-coins such as USDC, USDT, and USDE and Tokens like SYMM.

With this in mind, the problem can be explained. [configureRewardToken()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L312-L335) is implemented as follows.
```solidity
function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));

		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);

		isRewardToken[token] = status;
		if (!status) {
@>			if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}

		emit UpdateWhitelist(token, status);
	}
```
As you can see, if `pendingRewards` is greater than 10, the token cannot be removed from the whitelist. Now, let's take a look at the `deposit()`.
```solidity
function deposit(uint256 amount, address receiver) external nonReentrant whenNotPaused {
		_updateRewardsStates(receiver);

		if (amount == 0) revert ZeroAmount();
		if (receiver == address(0)) revert ZeroAddress();
		IERC20(stakingToken).safeTransferFrom(msg.sender, address(this), amount);
		totalSupply += amount;
		balanceOf[receiver] += amount;
		emit Deposit(msg.sender, amount, receiver);
	}
```
As you can see, any user can send an amount to any address to increase their `balanceOf`. The issue arises when a malicious user increases the balance of a blacklisted address. 

This becomes problematic because if the manager wants to remove USDC from the reward tokens and a user hasn't claimed their reward, the manger can call `claimFor()` on behalf of the user to reduce the `pendingRewards` to 0. 

This would enable the removal of the token, as the condition `pendingRewards[token] > 10` would no longer be true. However, when the manager calls `claimFor()` with the balcklisted address, it will revert because rewards cannot be sent to a blacklisted address.
```solidity
function _claimRewardsFor(address user) internal {
		uint256 length = rewardTokens.length;
		for (uint256 i = 0; i < length; ) {
			address token = rewardTokens[i];
			uint256 reward = rewards[user][token];
			if (reward > 0) {
				rewards[user][token] = 0;
				pendingRewards[token] -= reward;
				IERC20(token).safeTransfer(user, reward); -> here the transction will revert because user is blacklisted
				emit RewardClaimed(user, token, reward);
			}
			unchecked {
				++i;
			}
		}
	}
```
This will prevent the USDC from ever being removed from the whitelist, allowing `notifyRewardAmount()` to be used with a token that, in theory, should not be able to add rewards.

As outlined in the issue criteria of Sherlock:
>User Blacklist: User getting blacklisted by a token/contract causing harm only to themselves is not a valid medium/high.

In this case, it affects the protocol because the manager is unable to remove USDC from the whitelist.

### Internal Pre-conditions

None.

### External Pre-conditions

The attacker deposits an amount into a blacklisted address.

### Attack Path

1. The attacker deposits an amount using the `deposit()`, targeting a blacklisted USDC address as the `receiver`.  
2. After some time, the same user invokes `deposit()`, sending just 1 wei to the blacklisted address as the `receiver` to accumulate some rewards for the blacklisted address, for example, during this period, a total of 100 is accumulated as a reward.
3. The manager attempts to remove USDC from the whitelist but notices there are still 100 pending rewards for the blacklisted address. The manager then calls `claimFor()` with the blacklisted address. (Note: to remove a token from the whitelist, the pending rewards must be 10 or fewer.)  
4. The transaction reverts because transferring USDC rewards to a blacklisted USDC address is not allowed.  
5. As a result, the manager is unable to remove USDC from the whitelist.

### Impact

USDC can never be removed from the whitelisted tokens.

### PoC

The attack vector can be used as a POC.

### Mitigation

One solution to resolve this issue, add new `receiver` argument to both the `claimFor()` and `_claimRewardsFor()` to specify where the rewards should be sent. 

The updated functions could be structured as follows (note that `_claimRewardsFor()` also needs to be updated).
```solidity
function claimFor(address user, address receiver) external nonReentrant onlyRole(REWARD_MANAGER_ROLE) whenNotPaused {
		_updateRewardsStates(user);
		_claimRewardsFor(user, receiver);
}
```