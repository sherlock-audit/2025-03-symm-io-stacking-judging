Massive Flaxen Ferret

High

# In SymmStaking.sol, a malicious actor can cause the system to distribute less rewards via notifyRewardAmount().

### Summary

The notifyRewardAmount() is used to distribute rewards to stakers. It calls _addRewardsForToken() which calculate the reward rate and recalculate the rate if called during a distribution. This calculation suffers from precision loss and the attacker can abuse this to lower the reward rate, making the total distributed amount less than intended or inflated if the admin redistribute them during the distribution.

### Root Cause

The [notifyRewardAmount()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token%2Fcontracts%2Fstaking%2FSymmStaking.sol#L275) can be called by anyone to transfer an amount of token, which will calculate the reward rate in _addRewardsForToken(). 

```solidity
        function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
                _updateRewardsStates(address(0));
                if (tokens.length != amounts.length) revert ArraysMismatched();

                uint256 len = tokens.length;
                for (uint256 i = 0; i < len; i++) {
                        address token = tokens[i];
                        uint256 amount = amounts[i];

                        if (amount == 0) continue;
                        if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

                        IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
                        pendingRewards[token] += amount;
@>                      _addRewardsForToken(token, amount);
                }
                emit RewardNotified(tokens, amounts);
```

In [_addRewardsForToken()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token%2Fcontracts%2Fstaking%2FSymmStaking.sol#L366), the rate is recalculated when there is an ongoing distribution. 

```solidity
        function _addRewardsForToken(address token, uint256 amount) internal {
                TokenRewardState storage state = rewardState[token];

                if (block.timestamp >= state.periodFinish) {
                        state.rate = amount / state.duration;
                } else {
                        uint256 remaining = state.periodFinish - block.timestamp;
                        uint256 leftover = remaining * state.rate;
@>                      state.rate = (amount + leftover) / state.duration;
                }

                state.lastUpdated = block.timestamp;
                state.periodFinish = block.timestamp + state.duration;
        }
```
This calculation suffers from precision loss with every recalculation, causing less rewards to be distributed. Since the duration never change (set to 604800 by defaut), if the attacker calls with amount as 1 during a distribution, this will make the calculation (remaining * state.rate) + 1 / 604800, leading to a lower reward rate and extending the distribution period. Due to precision loss, the reward rate decreases exponential with every call with amount as 1. As the rate is lower, the leftover amount will be lower and the new rate will be lower and this continues with every call. 

### Internal Pre-conditions

1. Admin needs to distribute rewards like USDC with 6 decimals, making the attack more effective and stakers must be present. 

### External Pre-conditions

1. The attacker calls notifyRewardAmount() with amount as 1 as frequently as possible to maximise loss. 

### Attack Path

1. The admin distribute 1e9 amount of usdc. 
2. The first reward rate will be 1e9 / 604800 = 1653 due to precision loss. 
3. If the attacker calls notifyRewardAmount with 1 as amount at the next block, the leftover amount will be 1653 * 604799 = 999,732,747. The new rate will be 999,732,747 + 1 / 604800 = 1652 (1652.99727 but results in 1652 due to precision loss).
4. If the attacker calls at the next block, the leftover amount will be 1652 * 604799 = 999,127,948 and the rate will be 999,127,948 + 1 / 604800 = 1651. 
5. The attacker can do this as frequently as possible, causing the reward rate to suffer from precision loss exponentially with every call, disturbing the distribution system. 

### Impact

The stakers get less reward then intended as reward rate suffers from precision loss. The attacker loses gas fees with every call and doesnt gain anything but the reward rate will never be correct and it will get lower as he can execute this attack again and again and keep the distribution ongoing. Although the admin can transfer out the funds via rescueTokens(), he cant redistributed them while the distribution is ongoing and reward rate is non zero as this will inflate the rewards. Overall, the distribution system be DOSed and users can get no rewards if the attacker keep going til the reward rate is zero or get less rewards if the attacker keep the distribution ongoing and the reward rate low but non-zero. 

### PoC

_No response_

### Mitigation

Put access control on notifyRewardAmount().