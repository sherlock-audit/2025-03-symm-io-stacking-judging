Flaky Satin Hamster

Medium

# Reward tokens may be lost if no staking occurs or if the staking duration is shorter than the intended reward period.

### Summary

The staking contract implements a reward distribution mechanism for users who stake their tokens. Rewards accrue linearly based on the duration between staking and claiming, with a predefined `DEFAULT_REWARDS_DURATION` of one week. The contract is structured under the assumption that the entire reward pool will be fully distributed within this timeframe.

However, once this one-week period elapses, any undistributed rewards become irretrievable, effectively rendering them permanently locked within the contract. This issue may arise under the following circumstances:

1. No staking activity – If no users participate in staking 

2. Partial staking – If users stake their tokens but withdraw them before the full one-week duration, only a fraction of the rewards will be distributed, leaving the remainder stranded.

3. Difference of time in staking of users.

The Proof of Concept (PoC) for this issue demonstrates all three scenarios, highlighting a critical flaw: once the designated timeframe concludes, any unclaimed rewards are irreversibly trapped with no mechanism for recovery.

### Root Cause
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L275
Assumption that the whole reward will be distributed in 1 week.
```solidity
function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));

		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);

		isRewardToken[token] = status;
		if (!status) {
			if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
@>			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}

		emit UpdateWhitelist(token, status);
	}

```
There is no function to get the remaining funds back.

### Internal Pre-conditions

None

### External Pre-conditions

No stacking in that one week of rewards distribution 

### Attack Path

N/a

### Impact

No stacking in that one week of rewards distribution 

Impact
Remaining rewards will be stuck forever. (High impact)

Likelihood
Low , No stacking is required for this to happen.

Severity is decided to be Medium after considering the High Impact and Low Likelihood

### PoC

For displaying this issue, first you need to setup it=> by following the instruction 

```bash
forge init --force
```
```bash
forge install foundry-rs/forge-std
```
in `foundry.toml` file changing the src from `src` to `contract`. 
Removing the sample test case from the /test folder and pasting the following code in a new file created and running the following command 
```bash
forge test -vv
```
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {SymmStaking} from "../contracts/staking/SymmStaking.sol"; 
import {MockERC20} from "../contracts/mock/MockERC20.sol";

/**
 * @title SymmStaking Reward Issue Test
 * @notice Tests demonstrating a potential issue with rewards calculation after withdrawal
 */
contract SymmStakingRewardTest is Test {
    // Contract instances
    SymmStaking public symmStaking;
    MockERC20 public symToken;
    MockERC20 public rewardToken;
    
    // Test addresses
    address public user;
    address public attacker;
    address public admin;
    
    function setUp() public {
        // Deploy contracts
        symmStaking = new SymmStaking();
        symToken = new MockERC20("SymToken", "SYM");
        rewardToken = new MockERC20("RewardToken", "RWD");
        
        // Create test addresses
        user = makeAddr("user");
        attacker = makeAddr("attacker");
        admin = makeAddr("admin");

        // Initialize staking contract
        symmStaking.initialize(address(admin), address(symToken));

        // Distribute tokens
        symToken.transfer(user, 1000e18);
        symToken.transfer(attacker, 10e18);
        rewardToken.transfer(admin, 10000e18);
        
        // Approvals
        vm.prank(user);
        symToken.approve(address(symmStaking), type(uint256).max);

        vm.prank(attacker);
        symToken.approve(address(symmStaking), type(uint256).max);

        vm.prank(admin);
        rewardToken.approve(address(symmStaking), type(uint256).max);
        
        // Configure reward token
        vm.prank(admin);
        symmStaking.configureRewardToken(address(rewardToken), true);
    }

    function test_rewardsWithnoStaking() public {
        
        
        
        // ADMIN ADDS REWARDS
        vm.startPrank(admin);
        address[] memory tokens = new address[](1);
        tokens[0] = address(rewardToken);
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 7000e18; // 7000 reward tokens for 1 week period
        symmStaking.notifyRewardAmount(tokens, amounts);
        vm.stopPrank();
        
        vm.warp(block.timestamp + 7 days);
        vm.prank(user);
        symmStaking.deposit(1000e18,user);
        vm.warp(block.timestamp + 2 days);
        console.log("The earned rewards",symmStaking.earned(user,address(rewardToken)));
        vm.prank(user);
        symmStaking.withdraw(1000e18,user);

        console.log("The balance of funds in the contract",rewardToken.balanceOf(address(symmStaking)));// this is the amount stuck.

    }
    function test_rewardswithPartialStaking() public{
        vm.prank(user);
        symmStaking.deposit(1000e18,user);

        vm.startPrank(admin);
        address[] memory tokens = new address[](1);
        tokens[0] = address(rewardToken);
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 7000e18; // 7000 reward tokens for 1 week period
        symmStaking.notifyRewardAmount(tokens, amounts);
        vm.stopPrank();

        vm.warp(block.timestamp + 3 days);

        vm.prank(user);
        symmStaking.withdraw(1000e18,user);
        console.log("The rewards earned during 3 days", symmStaking.earned(user,address(rewardToken)));
        vm.prank(user);
        symmStaking.claimRewards();


        vm.warp(block.timestamp + 4 days);

        console.log("The contract balance after this",rewardToken.balanceOf(address(symmStaking)));

        vm.prank(user);
        symmStaking.deposit(1000e18,user);
        vm.warp(block.timestamp + 3 days);
        console.log("the rewards during second 3 days",symmStaking.earned(user,address(rewardToken)));
    }
    
}
```
The results for the test would be 

```bash
Ran 2 tests for test/SymmStaking.t.sol:SymmStakingRewardTest
[PASS] test_rewardsWithnoStaking() (gas: 238111)
Logs:
  The earned rewards 0
  The balance of funds in the contract 7000000000000000000000

[PASS] test_rewardswithPartialStaking() (gas: 389074)
Logs:
  The rewards earned during 3 days 2999999999999999980000
  The contract balance after this 4000000000000000020000
  the rewards during second 3 days 0

Suite result: ok. 2 passed; 0 failed; 0 skipped; finished in 7.85ms (3.43ms CPU time)
```
This shows that the remaining funds will be stuck forever, by these both tests we can also conclude about the third case
where 
1. user1 stakes some tokens for 2 days withdraws the token back 
2. nothing happens for 1 day
3. user2 stakes stakes some token for rest of the days
In that condition as well the rewards for one day would be lost. (In this case around approximately 1000 token).

### Mitigation

To solve the above problem, we should implement a function which can only be called by `REWARD_MANAGER_ROLE` role holder. Which refunds back the remaining reward token to the manager.