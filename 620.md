Brisk Parchment Sparrow

High

# The `PendingRewards` mapping is Not Updated in the `rescueTokens` functions, Thus Prevents Token Whitelist Removal

### Summary

The `rescueTokens` function is intended to allow an admin to withdraw tokens from the contract. However, it fails to update the `pendingRewards` mapping for the rescued token. As a result, if there are any pending rewards for that token, the subsequent call to remove the token from the whitelist via `configureRewardToken` will revert (due to the check that `pendingRewards[token] > 10`). This prevents the admin from temporarily removing a reward token for maintenance or upgrades.

### Root Cause

The `rescueTokens` function only transfers the tokens and emits an event without updating `pendingRewards` if the token was a reward token. The relevant [code snippet](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L343-L346) is:

```solidity
function rescueTokens(address token, uint256 amount, address receiver) external nonReentrant onlyRole(DEFAULT_ADMIN_ROLE) {
    IERC20(token).safeTransfer(receiver, amount);
    emit RescueToken(token, amount, receiver);
}
```

Because it does not subtract the rescued amount from `pendingRewards`, any nonzero pending rewards will remain and cause `configureRewardToken` to revert when attempting to remove the token temporarily

### Internal Pre-conditions

1. The reward token is actively used, so `pendingRewards[token]` is nonzero.
2. The `rescueTokens` function is called to withdraw the reward tokens but does not update `pendingRewards[token]`.
3. The `configureRewardToken` function checks if `pendingRewards[token] > 10` and reverts if true.

### External Pre-conditions

1. The reward token is funded via reward notifications, causing `pendingRewards[token]` to accumulate.
2. An admin attempts to remove the token from the whitelist (for example, for maintenance).

### Attack Path

1. A reward token (e.g., SYMM used as a reward) accrues rewards and `pendingRewards[token]` becomes nonzero.
2. The admin calls `rescueTokens` to withdraw the reward tokens; however, `pendingRewards[token]` remains unchanged because the function doesnâ€™t update it.
3. When the admin subsequently calls `configureRewardToken` to remove the token (status = false), the check `if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);` causes the transaction to revert.

### Impact

Administrators are unable to remove a reward token from the whitelist (e.g., for maintenance or system upgrades) because stale `pendingRewards` cause the removal to revert.

### PoC

A foundry test has been written to show this. Make sure to install the hardhat-foundry plugin to run this test sucessfully.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

import "forge-std/Test.sol";
import "forge-std/console.sol";
import "../contracts/staking/SymmStaking.sol";
import {ERC20} from "@openzeppelin-contracts/token/ERC20/ERC20.sol";

/// @title SYMMMOCK
/// @notice A simple ERC20 mock token with 18 decimals, used for staking and rewards.
contract SYMMMOCK is ERC20 {
    constructor() ERC20("SYMMMOCK", "SYMM") {}

    /// @notice Mint tokens for testing.
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

/// @title MockUSDC
/// @notice A mock ERC20 token with 6 decimals, simulating USDC.
contract MockUSDC is ERC20 {
    constructor() ERC20("MockUSDC", "mUSDC") {}

    /// @notice Mint tokens for testing.
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }

    function decimals() public view virtual override returns (uint8) {
        return 6;
    }
}

contract SymmStakingTest is Test {
    SymmStaking staking;
    SYMMMOCK stakingToken;
    SYMMMOCK rewardToken;

    address admin = address(1);
    address user1 = address(2);
    address user2 = address(3);

    function setUp() public {
        // Deploy mock tokens.
        stakingToken = new SYMMMOCK();
        rewardToken = new SYMMMOCK();

        // Mint tokens for admin, user1, and user2.
        stakingToken.mint(admin, 1e24);
        stakingToken.mint(user1, 1e24);
        stakingToken.mint(user2, 1e24);

        // Mint reward tokens to admin.
        rewardToken.mint(admin, 1e24);

        // Deploy and initialize the staking contract.
        vm.prank(admin);
        staking = new SymmStaking();
        staking.initialize(admin, address(stakingToken));

        // Admin whitelists the reward token.
        vm.prank(admin);
        staking.configureRewardToken(address(rewardToken), true);

        // Approve staking tokens for user1 and user2.
        vm.prank(user1);
        stakingToken.approve(address(staking), 1e24);
        vm.prank(user2);
        stakingToken.approve(address(staking), 1e24);

        // Approve reward tokens for the staking contract from admin.
        vm.prank(admin);
        rewardToken.approve(address(staking), 1e24);
    }


    function test_cannot_update() public {
    uint256 stakedAmount = 100_000e18;
    vm.prank(user1);
    staking.deposit(stakedAmount, user1);
    assertEq(staking.balanceOf(user1), stakedAmount, "Stake amount not recorded correctly");

    // 1. Confirm initial reward per token for rewardToken (SYMM) is 0.
    uint256 rptInitial = staking.rewardPerToken(address(rewardToken));
    assertEq(rptInitial, 0, "Initial reward per token should be 0");

    // 2. Notify rewards with 1000 SYMM for the reward token.
    uint256 rewardAmount = 1000e18;
    address[] memory tokensArray = new address[](1);
    tokensArray[0] = address(rewardToken);
    uint256[] memory amountsArray = new uint256[](1);
    amountsArray[0] = rewardAmount;
    vm.prank(admin);
    staking.notifyRewardAmount(tokensArray, amountsArray);

    vm.warp(block.timestamp + 2);

    // 3. Check that reward per token is updated.
    uint256 rptAfter = staking.rewardPerToken(address(rewardToken));
    assertGt(rptAfter, 0, "Reward per token should be > 0 after reward notification");

    // 4. Remove the reward token from the whitelist.
    vm.prank(admin);
    staking.rescueTokens(address(rewardToken), rewardAmount, address(admin));
    assertEq(rewardToken.balanceOf(address(staking)), 0, "Reward token not rescued properly");
    
    vm.prank(admin);
    staking.configureRewardToken(address(rewardToken), false);
}

}
```

The test will fail with the error as expected, proving this is true.

### Mitigation

Modify the `rescueTokens` function so that it updates `pendingRewards` by subtracting the rescued amount if it is a reward token