Brisk Parchment Sparrow

High

# Stale `perTokenStored` Persists Across Whitelist Removal and Re-Whitelisting

### Summary

When a reward token is removed from the whitelist using `configureRewardToken` and then later re-whitelisted, the contract does not reset the associated reward state (particularly the `perTokenStored` value). As a result, when new rewards are later notified, the stale `perTokenStored` value persists and skews subsequent reward calculations.

### Root Cause

In the `configureRewardToken` function, when a token is removed (status set to false), the function does not reset `rewardState[token].perTokenStored`  For example, the [code snippet](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L319-L332) is:

```solidity
if (!status) {
    if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
    uint256 len = rewardTokens.length;
    for (uint256 i = 0; i < len; i++) {
        if (rewardTokens[i] == token) {
            rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
            rewardTokens.pop();
            break;
        }
    }
} else {
    rewardTokens.push(token);
    rewardState[token].duration = DEFAULT_REWARDS_DURATION;
}
```

Here, when a token is removed, `rewardState[token].perTokenStored` is not reset. Later, when the token is re-whitelisted, the `_addRewardsForToken` function updates only rate, `lastUpdated`, and `periodFinish`, leaving the stale `perTokenStored` intact.

### Internal Pre-conditions

1. A reward token is whitelisted and accrues rewards, so `rewardState[token].perTokenStored` is updated from 0.
2. The token is removed from the whitelist via `configureRewardToken(false)` without resetting `rewardState[token]`.
3. Pending rewards are cleared to avoid an "ongoing reward period" revert.
4. The token is then re-whitelisted via `configureRewardToken(true)`, but `rewardState[token].perTokenStored` remains unchanged.

### External Pre-conditions

1. The protocol or admin decides to remove a reward token from the whitelist temporarily (e.g., for maintenance or upgrades).
2. Later, the token is re-added to the whitelist and additional rewards are notified.

### Attack Path

1. The reward token is initially whitelisted and used in reward calculations, updating `perTokenStored` to a nonzero "stale" value.
2. The admin clears pending rewards and then removes the token from the whitelist via `configureRewardToken(false)` (pending rewards must be low for removal).
3. Later, the admin re-whitelists the token via `configureRewardToken(true);` however, the stale `perTokenStored` value is not reset.
4. When new rewards are notified, the `rewardPerToken` calculation starts from the stale baseline, leading to incorrect (and inflated) reward accrual for stakers.

### Impact

Stakers receive incorrect reward calculations because new rewards accumulate on top of an outdated perTokenStored value. This can cause stakers to receive an inflated reward amount that does not accurately reflect their participation over the correct time period.

### PoC

The following test written with foundry demonstrates the issue:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

import "forge-std/Test.sol";
import "forge-std/console.sol";
import "../contracts/staking/SymmStaking.sol";
import {ERC20} from "@openzeppelin-contracts/token/ERC20/ERC20.sol";

/// @title SYMMMOCK
/// @notice A simple ERC20 mock token with 18 decimals, used for staking and rewards.
contract SYMMMOCK is ERC20 {
    constructor() ERC20("SYMMMOCK", "SYMM") {}

    /// @notice Mint tokens for testing.
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

/// @title MockUSDC
/// @notice A mock ERC20 token with 6 decimals, simulating USDC.
contract MockUSDC is ERC20 {
    constructor() ERC20("MockUSDC", "mUSDC") {}

    /// @notice Mint tokens for testing.
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }

    function decimals() public view virtual override returns (uint8) {
        return 6;
    }
}

contract SymmStakingTest is Test {
    SymmStaking staking;
    SYMMMOCK stakingToken;
    SYMMMOCK rewardToken;

    address admin = address(1);
    address user1 = address(2);
    address user2 = address(3);

    function setUp() public {
        // Deploy mock tokens.
        stakingToken = new SYMMMOCK();
        rewardToken = new SYMMMOCK();

        // Mint tokens for admin, user1, and user2.
        stakingToken.mint(admin, 1e24);
        stakingToken.mint(user1, 1e24);
        stakingToken.mint(user2, 1e24);

        // Mint reward tokens to admin.
        rewardToken.mint(admin, 1e24);

        // Deploy and initialize the staking contract.
        vm.prank(admin);
        staking = new SymmStaking();
        staking.initialize(admin, address(stakingToken));

        // Admin whitelists the reward token.
        vm.prank(admin);
        staking.configureRewardToken(address(rewardToken), true);

        // Approve staking tokens for user1 and user2.
        vm.prank(user1);
        stakingToken.approve(address(staking), 1e24);
        vm.prank(user2);
        stakingToken.approve(address(staking), 1e24);

        // Approve reward tokens for the staking contract from admin.
        vm.prank(admin);
        rewardToken.approve(address(staking), 1e24);
    }

    function test_StalePerTokenStoredPersists() public {
        uint256 stakedAmount = 100_000e18;
        vm.prank(user1);
        staking.deposit(stakedAmount, user1);
        assertEq(staking.balanceOf(user1), stakedAmount, "Stake amount not recorded correctly");

        // 1. Confirm initial reward per token for rewardToken (SYMM) is 0.
        uint256 rptInitial = staking.rewardPerToken(address(rewardToken));
        assertEq(rptInitial, 0, "Initial reward per token should be 0");

        // 2. Notify rewards with 1000 SYMM (using 18 decimals) for the reward token.
        uint256 rewardAmount = 1000e18;
        address[] memory tokensArray = new address[](1);
        tokensArray[0] = address(rewardToken);
        uint256[] memory amountsArray = new uint256[](1);
        amountsArray[0] = rewardAmount;
        vm.prank(admin);
        staking.notifyRewardAmount(tokensArray, amountsArray);

        vm.warp(block.timestamp + 2);

        // 3. Check that reward per token is updated.
        uint256 rptAfter = staking.rewardPerToken(address(rewardToken));
        assertGt(rptAfter, 0, "Reward per token should be > 0 after reward notification");

        // Save the current value as the stale baseline.
        uint256 staleValue = rptAfter;
        assertEq(staleValue, 33068783068, "omor");

        // 4. Remove the reward token from the whitelist.
        vm.prank(admin);
        staking.rescueTokens(address(rewardToken), rewardAmount, address(admin));
        assertEq(rewardToken.balanceOf(address(staking)), 0, "not rescued");
        vm.prank(admin);
        staking.configureRewardToken(address(rewardToken), false);

        // 5. Re-add the reward token to the whitelist.
        vm.prank(admin);
        staking.configureRewardToken(address(rewardToken), true);

        // Check that the stale perTokenStored value persists.
        uint256 rptAfterReWhitelist = staking.rewardPerToken(address(rewardToken));
        assertGe(rptAfterReWhitelist, staleValue, "Stale perTokenStored should persist after re-whitelisting");

        // 6. Notify new rewards (e.g., 100 SYMM) for the reward token.
        uint256 newReward = 100e18;
        amountsArray[0] = newReward;
        vm.prank(admin);
        staking.notifyRewardAmount(tokensArray, amountsArray);

        // Advance time by another 10 seconds.
        vm.warp(block.timestamp + 10);

        // 7. Check that the reward per token has increased above the stale baseline.
        uint256 rptFinal = staking.rewardPerToken(address(rewardToken));
        assertGt(rptFinal, rptAfterReWhitelist, "New rewards should increment reward per token on top of stale value");
    }

}
```

N/B: the `rescueTokens` function must be updated to correctly update the `pendingRewards` mapping first.

This test confirms that after removing and re-whitelisting a reward token, the stale `perTokenStored` value persists and causes new rewards to be added on top of it, leading to incorrect reward calculations.

### Mitigation

Reset the `rewardState` for a token when it is removed from the whitelist. Alternatively, enforce that any token being re-whitelisted must have its `rewardState` reinitialized before accepting new rewards, ensuring that calculations start from a clean slate.