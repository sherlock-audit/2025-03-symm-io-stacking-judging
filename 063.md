Cold Coal Caribou

Medium

# Reward Token De-Whitelisting DoS via Malicious Deposit

### Summary

A malicious depositor could call `SymmStaking::deposit` with a blacklisted address as the `receiver` parameter, and DoS the `configureRewardToken` function when the admin intends to remove any reward token from the whitelist because of the check: `if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);`.

### Root Cause

In the `SymmStaking::deposit` function, the `msg.sender` is paying the `stakingToken` for the `receiver`:
[SymmStaking.sol#L234-L243](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L234-L243)
```solidity
	function deposit(uint256 amount, address receiver) external nonReentrant whenNotPaused {
		_updateRewardsStates(receiver);
		
		if (amount == 0) revert ZeroAmount();
		if (receiver == address(0)) revert ZeroAddress();
		IERC20(stakingToken).safeTransferFrom(msg.sender, address(this), amount);
		totalSupply += amount;
		balanceOf[receiver] += amount;
		emit Deposit(msg.sender, amount, receiver);
	}
```

There are zero restrictions in what is the address of the `receiver`.

Some of the tokens that will be used in the contracts are `USDT` and `USDC` which are tokens known for having blacklisted addresses. Most of the times, addresses related to hacks. These tokens revert any transaction that involves blacklisted address.

With that in mind, now check that the `SymmStaking::configureRewardToken` function:
[SymmStaking.sol#L312-L335](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L312-L335)
```solidity
	function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));
		
		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);
		
		isRewardToken[token] = status;
		if (!status) {
			if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}
		
		emit UpdateWhitelist(token, status);
	}
```

We can see that when the `REWARD_MANAGER_ROLE` wants to remove the `token` from the whitelist (list of available reward tokens), meaning that `status == false`, there is a check:
```solidity
if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
```

This means that if there are more than 10 wei still to be paid or distributed, the token cannot be removed from the whitelist.

Now, let's see where this `pendingRewards` value is increased and decreased.
Increased only in:
[SymmStaking.sol#L275-L292](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L275-L292)
```solidity
	function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		_updateRewardsStates(address(0));
		if (tokens.length != amounts.length) revert ArraysMismatched();
		
		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
			address token = tokens[i];
			uint256 amount = amounts[i];
			
			if (amount == 0) continue;
			if (!isRewardToken[token]) revert TokenNotWhitelisted(token);
			
			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
			pendingRewards[token] += amount;
			_addRewardsForToken(token, amount);
		}
		emit RewardNotified(tokens, amounts);
	}
```

Decreased only in:
[SymmStaking.sol#L385-L400](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L385-L400)
```solidity
	function _claimRewardsFor(address user) internal {
		uint256 length = rewardTokens.length;
		for (uint256 i = 0; i < length; ) {
			address token = rewardTokens[i];
			uint256 reward = rewards[user][token];
			if (reward > 0) {
				rewards[user][token] = 0;
				pendingRewards[token] -= reward;
				IERC20(token).safeTransfer(user, reward);
				emit RewardClaimed(user, token, reward);
			}
			unchecked {
				++i;
			}
		}
	}
```

It can be seen that the only way to get `pendingRewards` for a given `token` to be less than 10 wei, is by executing this function that can be either executed by the `msg.sender` (the blacklisted account, which is not possible because the attacker won't have the control of that address and he won't revert his own attack), and the admin.

However, it is clear that the whole function will revert when the `ERC20` transfer occurs to the `user` because it will be a blacklisted account. This means that none of the `pendingRewards` for all the rewards tokens, will be reduced. 

This means that for all the rewards tokens that the malicious address has more than 10 wei in rewards, the de-whitelisting will be DoSed.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

- Attacker deposits a small amount with a blacklisted account for some reward token that has blacklisted accounts.
- When the blacklisted account earned more than 10 wei in rewards for the token reward in which is blacklisted, all calls to `configureRewardToken` will revert when trying to de-whitelist a reward token. Forever. 

### Impact

- `SymmStaking::rewardTokens` list will only be able to grow, not decrease. Limiting protocol functionality and leading to a DoS if more tokens are added.

### PoC

None

### Mitigation

Add a function in which an admin can modify the `pendingRewards[token]` of any address for any `token` to handle this situations.