Macho Opaque Carp

Medium

# Attacker Blocks USDC Distribution via Frequent Updates

### Summary

USDC's lower precision of 6 decimals and frequent reward updates will cause stakers to receive 0 of the allotted weekly USDC rewards in the SymmStaking contract.

### Root Cause

Using the same reward-per-token calculation for USDC as for SYMM is a mistake, as USDC only has 6 decimals of precision compared to SYMM's 18 decimals. The calculation in [rewardPerToken()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L200-L201) involves multiplying the reward rate by 1e18 and dividing by the total staked amount (in 18 decimals), which results in truncation to 0 for low-precision tokens like USDC.

### Attack Path

- The SymmStaking contract has a 10_000e6 USDC (10,000 USDC with 6 decimals) rewards. The [rewardState[token].rate](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L370)  is now equa 16534 (10_000e6 / 1 week in seconds, i.e., 10,000 * 10^6 / 604,800 seconds).
- An attacker triggers frequent reward updates by calling `deposit()` or `withdraw()` every 1-3 blocks, which invokes `_updateRewardsStates()` and recalculates `rewardPerToken()` for USDC.
- After 3 blocks (assuming 2 seconds per block on base, totaling 6 seconds), the reward-per-token for USDC is calculated as:
```solidity
((lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated) * rewardState[_rewardsToken].rate * 1e18) / totalSupply
```
- lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated = 6 seconds.
- rewardState[_rewardsToken].rate = 16534.
- totalSupply = 100_000e18 (assuming at least 100,000 SYMM staked, with 18 decimals).
- Calculation: (6 * 16534 * 1e18) / 100_000e18 ≈ 0.
- Due to integer division and the small numerator relative to the denominator, the result truncates to 0. Since `rewardPerToken()` returns 0, no USDC rewards are accrued to stakers.

Note: The attacker doesn’t need to act maliciously if organic user activity (e.g., frequent deposits/withdrawals) already triggers reward updates on every block.

### Impact

All stakers will receive 0 USDC rewards.

### PoC
<details>

  <summary>Click to view PoC</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

import {SymmStaking} from "../staking/SymmStaking.sol";

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract MockERC20 is ERC20 {
    constructor() ERC20("Mock Token", "MTK") {}

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

contract SymmStakingTest is Test {
    SymmStaking st;
    MockERC20 symm;
    MockERC20 usdc;

    function setUp() public {
        st = new SymmStaking();

        symm = new MockERC20();
        usdc = new MockERC20();

        st.initialize(address(this), address(symm));

        symm.mint(address(this), 10000000e18);
        usdc.mint(address(this), 1000000e6);

        symm.approve(address(st), 100_000e18);
        usdc.approve(address(st), 10_000e6);

        usdc.transfer(address(st), 10_000e6);
    }

    function testUSDCRounding() public {
        // Stake 100,000 SYMM tokens into the staking contract for this test contract
        // This sets totalSupply to 100_000e18, mimicking a realistic staking scenario
        st.deposit(100_000e18, address(this));

        st.configureRewardToken(address(usdc), true);

        // Define the reward amount: 10,000 USDC (6 decimals)
        uint256 reward = 10_000e6;

        address[] memory token = new address[](1);
        token[0] = address(usdc);

        uint256[] memory amount = new uint256[](1);
        amount[0] = reward;

        // Notify the staking contract of 10,000 USDC as rewards
        // This sets the reward rate to 10_000e6 / 1 week (604,800 seconds) ≈ 16534
        st.notifyRewardAmount(token, amount);

        // Simulate the passage of 2 seconds by warping the block timestamp
        // This mimics a small time delta (e.g., 1 block on base) to test reward calculation
        vm.warp(block.timestamp + 2);

        // Check the reward per token for USDC
        // Expected behavior (bug): Due to low decimal precision (6 vs 18) and small time delta,
        // the calculation ((2 * 16534 * 1e18) / 100_000e18) truncates to 0
        assertEq(st.rewardPerToken(address(usdc)), 0);
    }
}

```
Run the following command to execute the POC:
```solidity
forge test --mt testUSDCRounding 
```
</details>

### Mitigation

A possible mitigation is to convert the USDC rewards to an 18-decimal value when computing for `rewardRate`. When claiming rewards, convert the earned USDC rewards back to 6-decimal precision. If it is 0, store the unclaimed partial values of USDC until they can be claimed whole.