Massive Flaxen Ferret

Medium

# In SymmStaking.sol, reward tokens cannot be removed from whitelist, due to precision loss and pendingReward check.

### Summary

When the protocol removes a reward token from a whitelist via configureRewardToken(), a check is applied to ensure unclaimed reward tokens cannot be removed. But because of precision loss, some dust amount will be left in the `pendingRewards` mapping used in the check. This makes the function reverts when the protocol tries to remove a token from whitelist, causing the functions to loop through unnecessary tokens in _updateRewardState().


### Root Cause

In [configureRewardToken ()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token%2Fcontracts%2Fstaking%2FSymmStaking.sol#L312), the `pendingReward` mapping is used in the check to ensure the protocol cant removed a token when there is unclaimed funds. 

```solidity
        function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
                _updateRewardsStates(address(0));

                if (token == address(0)) revert ZeroAddress();
                if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);

                isRewardToken[token] = status;
                if (!status) {
@>                      if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
                        uint256 len = rewardTokens.length;
                        for (uint256 i = 0; i < len; i++) {
                                if (rewardTokens[i] == token) {
                                        rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
                                        rewardTokens.pop();
                                        break;
                                }
                        }
...
```
This value is updated when the rewards are notified via [notifyRewardAmount()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token%2Fcontracts%2Fstaking%2FSymmStaking.sol#L275).

```solidity
        function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
                _updateRewardsStates(address(0));
                if (tokens.length != amounts.length) revert ArraysMismatched();

                uint256 len = tokens.length;
                for (uint256 i = 0; i < len; i++) {
                        address token = tokens[i];
                        uint256 amount = amounts[i];

                        if (amount == 0) continue;
                        if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

                        IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
@>                      pendingRewards[token] += amount;
                        _addRewardsForToken(token, amount);
                }
                emit RewardNotified(tokens, amounts);
```
The amount transferred from the caller is added to the mapping.

However, the reward rate of the token is calculated like this in [_addRewardsForToken()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token%2Fcontracts%2Fstaking%2FSymmStaking.sol#L366).

```solidity
        function _addRewardsForToken(address token, uint256 amount) internal {
                TokenRewardState storage state = rewardState[token];

@>              if (block.timestamp >= state.periodFinish) {
                        state.rate = amount / state.duration;
                } else {
                        uint256 remaining = state.periodFinish - block.timestamp;
                        uint256 leftover = remaining * state.rate;
                        state.rate = (amount + leftover) / state.duration;
...
```
This results in precision loss, and `(rate * duration)` will be less than `amount` stored in `pendingRewards` mapping. 

When users claim, this value is decreased by the claimed amount. Even when the user is the sole staker, and he claim at the very end of the distribution, he will only be able to claim up to `rate * duration` amount of tokens. This leave some dust in the mapping which will be more than the value `10` used in the check. 

This will make the function revert and the token cannot be removed from the whitelist. And as there is no way to reset the `pendingReward` mapping to be < 10. This can be more problematic as the protocol loops through the reward tokens frequently in every function due to [_updateRewardsStates()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token%2Fcontracts%2Fstaking%2FSymmStaking.sol#L406), leading to higher gas cost.

### Internal Pre-conditions

1. A reward token must be whitelisted and distributed to stakers.
2. The `amount` distributed should leave a remainder > 10 when divided by 604800 ( 1 week, default rewards duration). Which is very likely as there are only very few numbers which will leave a remainder < 10.
3. The protocol remove the said token from whitelist.

### External Pre-conditions

A user staked some amount of token.

### Attack Path

1. A user stakes 10e18 amount of tokens and he is the sole staker. 
2. The protocol distributed an amount of reward tokens, such that amount % 604800 > 10. 
3. For example if a the protocol distributed 1000 USDC (1e9). The rate will be 1653. The most the staker will be able to claim is (1653 * 604800 = 999734400) leaving 265600 dust amount in the `pendingReward` mapping.
4. Even after the users claim all the rewards, the protocol wont be able to remove the token from whitelist as 265600 > 10, causing a revert in the configureRewardToken(). 

Note: This is true with any amount of stakers too, as long as the protocol distribute an amount, where amount % 604800 > 10.

### Impact

Reward tokens cannot be removed from whitelist, and if the protocol distribute different reward tokens, it will make all the functions in the contract use more gas as they hav to go through unnecessary tokens in the for loops. 

### PoC

_No response_

### Mitigation

Increase the amount used in the check to a bigger number like 1e6, or remove the check. 