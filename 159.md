Ancient Cinnamon Hawk

Medium

# `REWARD_MANAGER_ROLE` Unable to Remove Reward Token from Rewards Tokens List


## Root Cause

A precision loss accumulates when users withdraw rewards.

This causes the `pendingRewards` mapping to consistently exceed the threshold of `10`.

As a result, the following [check](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L320) in the [configureRewardToken()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L312) function reverts whenever `REWARD_MANAGER_ROLE` attempts to remove a reward token:

```javascript
if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
```

## Recommended Mitigation

Remove the check. only remove reward token when there's no stakers (`totalSupply == 0`).

## Summary

To claim rewards, users call the `claimRewards()` function. This function internally calls the `earned()` function, which in turn calls the `rewardPerToken()` function.

- **`earned()` Function:** Calculates the total rewards a user has accumulated since their last reward update.
- **`rewardPerToken()` Function:** Computes the total rewards generated per staked token over the elapsed time since the last update.

Both calculations involve division. In Solidity, any remainder from a division operation is discarded (i.e., the result is rounded down), leading to a loss of precision.

Over time, this precision loss accumulates as users stake and withdraw rewards. Consequently, the `pendingRewards` for a token will remain above `10`, preventing the `configureRewardToken()` function from executing successfully to remove the reward token.

## Impact

The admin is unable to remove the reward token (tx always reverts), and as a result, the token remains in the reward tokens list indefinitely.

## POC

```javascript
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.18;




// ============= Imports ============= //
//
import   {    Test, console   }   from   "forge-std/Test.sol";
import   {    SymmStaking     }   from   "../src/staking/SymmStaking.sol";
import   {    Symmio          }   from   "../src/token/symm.sol";
import   {    MockERC20       }   from   "lib/solmate/src/test/utils/mocks/MockERC20.sol";


// ============= Notes ============= //
//
// - change symm.sol solidity version to 0.8.18 so it compiles if you're encountring issues.
// - install solmate library, we're going to need to create mock usdc contract

contract SymmStakingTest is Test {

    
    address admin      =   makeAddr("admin");
    address stakingToken;

    // reward token is USDC (6 decimals)
    address usdc;

    // symmStaking contract
    SymmStaking symmStaking;


    address[50] stakers;


    function setUp() external {
        vm.startPrank(admin);

                // 1. initialize SymmStaking Contract
                _initializeSymmstaking();

                // 2. add USDC to SymmStaking reward tokens list
                _addRewardToken();

                // 3. deposit rewards into staking contract
                _depositRewards();

                // 4. mint Symm Tokens stakers
                _mintSymmTokens();

        vm.stopPrank();
    }


    // run the test with: forge test --match-test test_attack --fork-url https://base-rpc.publicnode.com  -vv
    function test_attack() public {

        // 1. stakers deposit
        _stakerDepositsSymmTokens();

        // 2. one week passes
        skip(1 weeks);

        // 3. stakers withdraw rewards
        _stakerWithdrawsRewards();

        // 4. admin tries to remove usdc from reward tokens list but can't
        _adminRemoveTokenFromRewardTokensList();

    }
    

    // ============= Following Functions Below, are used to demonstrate the attack ============= //

    function _stakerDepositsSymmTokens() internal {
        for (uint256 i = 0; i < stakers.length; i++) {
            vm.startPrank(stakers[i]);

                // 1. each staker, deposits 10 SYMM Tokens to staking contract
                Symmio(stakingToken).approve(address(symmStaking), 10e18);
                symmStaking.deposit(10e18, stakers[i]);

            vm.stopPrank();
        }
    }


    function _stakerWithdrawsRewards() internal {
        for (uint256 i = 0; i < stakers.length; i++) {
            vm.startPrank(stakers[i]);

                // 1. each staker, claims rewards that has been accumulated after 1 week
                symmStaking.claimRewards();

            console.log("staker number", i + 1,":");
            console.log("           received: ", MockERC20(usdc).balanceOf(stakers[i]));
            console.log("           how much left: ", MockERC20(usdc).balanceOf(address(symmStaking)));
            console.log("");
            vm.stopPrank();
        }
    }


    function _adminRemoveTokenFromRewardTokensList() internal {
        vm.startPrank(admin);
        console.log("");
        console.log("");
        console.log("");
        console.log("admin calls configureRewardToken() function but unfortunately reverts beacuse of accumulated reward tokens that is caused by precision loss and the reward token remains whitelisted forever.");
        console.log("");
        console.log("");
        console.log("");

        vm.expectRevert();

                // 1. admin tries to remove usdc from reward tokens list
                symmStaking.configureRewardToken(usdc, false);

        vm.stopPrank();
    }

    // ============= Following Functions Below, are used to setUp SymmStaking Contract ============= //

    function _initializeSymmstaking() internal {

        // 1. deploy and set staking token. also grant minter role to admin.
        stakingToken = address(new Symmio("Symmio", "SYMM", admin));
        Symmio(stakingToken).grantRole(keccak256("MINTER_ROLE"), admin);

        // 2. initialize SymmStaking contract
        symmStaking = new SymmStaking();
        symmStaking.initialize(admin, stakingToken);

    }

    function _addRewardToken() internal {

        // 1. deploy usdc mock contract
        usdc = address(new MockERC20("USDC", "USDC", 6));

        // 2. add usdc to symmStaking contract reward tokens list
        symmStaking.configureRewardToken(usdc, true);

    }

    function _depositRewards() internal {

        // 1. mint some amount of usdc to admin
        MockERC20(usdc).mint(admin, 10_000e6);

        // 2. deposit rewards (usdc) into symmStaking contract
        address[] memory token    =   new address[](1);
        uint256[] memory amount   =   new uint256[](1);

        token[0]  =  usdc;
        amount[0] =  10_000e6;

        MockERC20(usdc).approve(address(symmStaking), 10_000e6); // $10k reward in usdc


        symmStaking.notifyRewardAmount(token, amount);
        
    }

    function _mintSymmTokens() internal {
        // 1. mint symm tokens to each staker
        for (uint256 i = 0; i < stakers.length; i++) {
            string memory username = string(abi.encode("staker", i + vm.randomUint()));
            stakers[i] = makeAddr(username);

            Symmio(stakingToken).mint(stakers[i], 10e18);
        }
    }

}
```