Silly Beige Walrus

High

# Attacker can dilute reward mechanism by invoking `notifyRewardAmount()`

## Summary
The `notifyRewardAmount()` function calculates `state.rate` based on the initial duration. Attacker can exploit this function to dilute the distribution.

## Root Cause

The [SymmStaking.sol](https://github.com/SYMM-IO/token/blob/1d014156b1d9f0ab3259026127b9220eb2da3292/contracts/staking/SymmStaking.sol#L289) function `notifyRewardAmount()` calls the [SymmStaking.sol](https://github.com/SYMM-IO/token/blob/1d014156b1d9f0ab3259026127b9220eb2da3292/contracts/staking/SymmStaking.sol#L366) function `_addRewardsForToken()`.

```solidity
	function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		_updateRewardsStates(address(0));
		if (tokens.length != amounts.length) revert ArraysMismatched();

		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
			address token = tokens[i];
			uint256 amount = amounts[i];

			if (amount == 0) continue;
			if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
			pendingRewards[token] += amount;
	>>		_addRewardsForToken(token, amount);
		}
		emit RewardNotified(tokens, amounts);
	}
```

Within the `_addRewardsForToken()` function, the reward token amount is added, and `state.periodFinish` is extended by the initial duration from the current `block.timestamp`. Additionally, it recalculates the new `state.rate` using the initial duration.

```solidity
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];

		if (block.timestamp >= state.periodFinish) {
			state.rate = amount / state.duration;
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
			state.rate = (amount + leftover) / state.duration;
		}

		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
	}
```

An attacker can dilute the rewards distribution by repeatedly invoking this function. Consider the following scenario:

- Total rewards available for distribution is 500 USDC with a duration of 1 week. Thus, `state.rate = 500e6 / 604800 = 826`.
- After 1 hour, the attacker calls `notifyRewardAmount()` with 0.000001 USDC, updating `state.rate` to `(826 * 601200 + 1) / 604800 = 821`.
- After 2 hours, the attacker calls `notifyRewardAmount()` again, resulting in `state.rate = (821 * 597600 + 1) / 604800 = 811`.
- ...

Through this method, the attacker can dilute the rewards distribution, causing stakers to receive fewer rewards than they should.

## Internal Pre-conditions


## External Pre-conditions


## Attack Path


## Impact
Stakers will receive fewer rewards than expected.

## Mitigation

Implement access control to the `notifyRewardAmount()` function to restrict who can invoke it.