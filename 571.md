Jovial Neon Crane

Medium

# Any user can delay staking rewards for tokens with low decimals with little cost

### Summary

Whenever, any function on the `SymmStaking` contract is invoked [_updateRewardsStates](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L406-L424) is invoked to update the `rewardPerToken` and the rewards per account.

```solidity
function _updateRewardsStates(address account) internal {
        uint256 length = rewardTokens.length;
        for (uint256 i = 0; i < length;) {
            address token = rewardTokens[i];
            TokenRewardState storage state = rewardState[token];

            //@audit - User can call notifyRewardAmount or claimRewards every block and not allow this to move because of rounding down
            state.perTokenStored = rewardPerToken(token);
            state.lastUpdated = lastTimeRewardApplicable(token);

            if (account != address(0)) {
                rewards[account][token] = earned(account, token);
                userRewardPerTokenPaid[account][token] = state.perTokenStored;
            }
            unchecked {
                ++i;
            }
        }
    }
```

It updates the `strate.perTokenStored` by calling the `rewardPerToken` function which computes the current `rewardPerToken`, according to the rate and the time passed.

```solidity
 function rewardPerToken(address _rewardsToken) public view returns (uint256) {
        if (totalSupply == 0) {
            return rewardState[_rewardsToken].perTokenStored;
        }

        return rewardState[_rewardsToken].perTokenStored
            + (
                (
                    (lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated)
                        * rewardState[_rewardsToken].rate * 1e18
                ) / totalSupply
            );
    }
```


In certain situations malicious user can delay the rewards by repeatedly calling any function that updates the rewardPerToken in a small intervals of time to round down the calculation to 0.

### Root Cause

```solidity
(
                (
                    (lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated)
                        * rewardState[_rewardsToken].rate * 1e18
                ) / totalSupply
            )
```

In situations with tokens with low decimals this could get rounded down to 0.

### Internal Pre-conditions

1. The reward tokens is token with decimals <= 8
2. There is a significant amount of tokens staked

### External Pre-conditions

None

### Attack Path

The default duration of the rewards is set at 1 week (604,800 seconds).

1. There are `3_000_000e18` SYMM tokens staked.
2. The team distributes `48_384e6` USDC for the duration of 1 week, making the rate `0.08e6`.
3. A malicious actor notices this and calls `notifyRewardAmount` with empty `tokens` and `amounts` 30 seconds after the rewards are distributed.

```solidity
  function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts)
        external
        nonReentrant
        whenNotPaused
    {
        _updateRewardsStates(address(0));

        if (tokens.length != amounts.length) revert ArraysMismatched();

        uint256 len = tokens.length;
        for (uint256 i = 0; i < len; i++) {
            address token = tokens[i];
            uint256 amount = amounts[i];

            if (amount == 0) continue;
            if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

            IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
            pendingRewards[token] += amount;
            _addRewardsForToken(token, amount);
        }
        emit RewardNotified(tokens, amounts);
    }
```

Since `_updateRewardsStates` is called, `perTokenStored` updates to:
`0 + (30 * 0.08e6 * 1e18) / 3_000_000e18 = 0`, and `lastUpdated` is set to the current timestamp.

4. The user repeats this process every 30 seconds until the end of the week.
5. If unnoticed, the attacker can call `notifyRewardAmount` with 1 wei of USDC to reset the rate, forcing the team to manually rescue the tokens and redistribute them, disrupting the staking cycle.

### Cost Analysis

The gas cost for invoking this function with 2 whitelisted tokens is approximately **15k gas**.

With the current Base gas price between **0.05 and 0.1 gwei**, we use the upper bound for accuracy:

- **Gas cost per call:** `(15,000 * 0.0000000001) = 0.0000015 ETH`
- **ETH price:** $2,112.00 (at the time of writing)
- **Cost per call in USD:** `$0.0031680024`

Considering the function needs to be called every **30 seconds**, it must be executed **604,800 / 30 = 20,160** times in one week.

- **Total cost for one week:** `$63.80`

This cost is extremely high due to the big deposit of **48,384e6 USDC**, if the team deposits less than that it will make the rate smaller, which will cause the attacker to need to call into bigger intervals, lowering the cost.

Example with **4,838e6 USDC**

- Rate = 0.007999e6
-  (340  * 0.007999e6 * 1e18) / 3_000_000e18 = 0
- The attacker has to call this for 1Â 778 weekly which will make his weekly cost:
   - $0.0031680024 * 1778 = $5,63

### Impact

The user can delay rewards for tokens with low decimals for arbitrary time

### PoC

_No response_

### Mitigation

Consider scaling up the rewardsPerToken and then lower it to make sure even on small intervals the progress is kept.