Radiant Coral Boar

Medium

# Missing totalVested Update in _addLiquidityProcess

### Summary

The [_addLiquidityProcess()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/SymmVesting.sol#L141C3-L143C1) function fails to update the totalVested state variable when tokens are used to add liquidity. This leads to an inflated totalVested value, leading to state inconsistency and breaks internal accounting.
https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/SymmVesting.sol#L141C3-L143C1

### Root Cause

The root cause is the missing update to totalVested in the `_addLiquidityProcess` function. When a user adds liquidity, their locked token amount is reduced, but the global totalVested state variable is not updated to reflect this reduction. This creates a discrepancy between the actual locked tokens and the recorded totalVested value.

```solidity
function _addLiquidityProcess(
		uint256 amount,
		uint256 minLpAmount,
		uint256 maxUsdcIn
	) internal returns (uint256[] memory amountsIn, uint256 lpAmount) {
		// Claim any unlocked SYMM tokens first.
		_claimUnlockedToken(SYMM, msg.sender);

		VestingPlan storage symmVestingPlan = vestingPlans[SYMM][msg.sender];
		uint256 symmLockedAmount = symmVestingPlan.lockedAmount();
		if (symmLockedAmount < amount) revert InvalidAmount();

		_ensureSufficientBalance(SYMM, amount);

		// Add liquidity to the pool.
		(amountsIn, lpAmount) = _addLiquidity(amount, minLpAmount, maxUsdcIn);

		// Update SYMM vesting plan by reducing the locked amount.
		symmVestingPlan.resetAmount(symmLockedAmount - amountsIn[0]);

		// Claim any unlocked SYMM LP tokens.
		_claimUnlockedToken(SYMM_LP, msg.sender);
```

The total Vested is used in other functions which would lead to incorrect calculation and state inconsistency. These include;

```solidity
function _setupVestingPlans(address token, uint256 startTime, uint256 endTime, address[] memory users, uint256[] memory amounts) internal {
		if (users.length != amounts.length) revert MismatchArrays();
		uint256 len = users.length;
		for (uint256 i = 0; i < len; i++) {
			address user = users[i];
			uint256 amount = amounts[i];
			totalVested[token] += amount;
			VestingPlan storage vestingPlan = vestingPlans[token][user];
			vestingPlan.setup(amount, startTime, endTime);
			emit VestingPlanSetup(token, user, amount, startTime, endTime);
		}
	}
```
AND
```solIdity
function _resetVestingPlans(address token, address[] memory users, uint256[] memory amounts) internal {
		if (users.length != amounts.length) revert MismatchArrays();
		uint256 len = users.length;
		for (uint256 i = 0; i < len; i++) {
			address user = users[i];
			uint256 amount = amounts[i];
			// Claim any unlocked tokens before resetting.
			_claimUnlockedToken(token, user);
			VestingPlan storage vestingPlan = vestingPlans[token][user];
			if (amount < vestingPlan.unlockedAmount()) revert AlreadyClaimedMoreThanThis();
			uint256 oldTotal = vestingPlan.lockedAmount();
			vestingPlan.resetAmount(amount);
			totalVested[token] = totalVested[token] - oldTotal + amount;
			emit VestingPlanReset(token, user, amount);
		}
```
AND

```solidiIty
function _claimUnlockedToken(address token, address user) internal {
		VestingPlan storage vestingPlan = vestingPlans[token][user];
		uint256 claimableAmount = vestingPlan.claimable();

		// Adjust the vesting plan
		totalVested[token] -= claimableAmount;
		vestingPlan.claimedAmount += claimableAmount;

		// Ensure sufficient balance (minting if necessary)
		_ensureSufficientBalance(token, claimableAmount);

		IERC20(token).transfer(user, claimableAmount);

		emit UnlockedTokenClaimed(token, user, claimableAmount);
	}
```
AND
```solidity
function _claimLockedToken(address token, address user, uint256 amount) internal {
		// First, claim any unlocked tokens.
		_claimUnlockedToken(token, user);
		VestingPlan storage vestingPlan = vestingPlans[token][user];
		if (vestingPlan.lockedAmount() < amount) revert InvalidAmount();

		// Adjust the vesting plan
		vestingPlan.resetAmount(vestingPlan.lockedAmount() - amount);
		totalVested[token] -= amount;
		uint256 penalty = (amount * lockedClaimPenalty) / 1e18;

		// Ensure sufficient balance (minting if necessary)
		_ensureSufficientBalance(token, amount);

		IERC20(token).transfer(user, amount - penalty);
		IERC20(token).transfer(lockedClaimPenaltyReceiver, penalty);

		emit LockedTokenClaimed(token, user, amount, penalty);
	}
```

### Internal Pre-conditions

1. The `_addLiquidityProcess` function is called to add liquidity using vested tokens.

2. The user’s locked token amount is reduced, but totalVested is not updated.

3. Other functions (_setupVestingPlans, _resetVestingPlans, _claimUnlockedToken, _claimLockedToken) rely on totalVested for calculations and state management.



### External Pre-conditions

1. Users who add liquidity using vested tokens are affected.

### Attack Path

Step 1: A user calls `_addLiquidityProcess` to add liquidity using vested tokens.

Step 2: The user’s locked token amount is reduced, but totalVested is not updated.

Step 3: The inflated totalVested value leads to state inconsistency and breaks the internal accounting.

### Impact

1. State Inconsistency: The totalVested value does not reflect the actual locked tokens, leading to logical errors in vesting plan management
2. Breaks internal accounting
3. Based on the provided documentation, it strongly implies that long term holders will receive rewards (such as protocol fee shares and additional $SYMM tokens) based on their share of the total vested tokens. This leads to incorrect reward distribution for long term holders.
https://docs.symmio.foundation/token-related/tokenomics/vesting-and-early-unlock

### PoC

_No response_

### Mitigation

Add the missing update to totalVested in the _addLiquidityProcess function.