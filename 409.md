Pet Ebony Mule

High

# Nested initializer prevents `SymmVesting` from being deployed

### Summary

The `SymmVesting.sol` contract inherits from `Vesting.sol` and its functions. In order to initialize the contract `SymmVesting.sol`, the `initialize()` function is used which has the `initializer` modifier.

The logic of the function internally calls `__vesting_init()` which also has the `initializer` modifier.

<https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/SymmVesting.sol#L55-L77>

```solidity
function initialize(
	address admin,
	address _lockedClaimPenaltyReceiver,
	address _pool,
	address _router,
	address _permit2,
	address _vault,
	address _symm,
	address _usdc,
	address _symm_lp
) public initializer {     <@
	// --- snip ---
	__vesting_init(admin, 500000000000000000, _lockedClaimPenaltyReceiver);
```

<https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/vesting/Vesting.sol#L76>

```solidity
function __vesting_init(address admin, uint256 _lockedClaimPenalty, address _lockedClaimPenaltyReceiver) public initializer {
```

Calling nested `initializer` functions is not possible due to how Openzeppelin implements this modifier. Indeed, it considers this pattern as a "double initialization" which will revert in order to prevent the proxy variables from being set twice (e.g. an `initializer` can only be called once).



### Root Cause

The `initialize()` function as well as the `__vesting_init()` function which is called internally both have the `initializer` modifier.

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

None

### Impact

High. The contract can't be deployed on chain.

### PoC

Install hardhat-foundry:

```bash
cd token/
pnpm add --save-dev @nomicfoundation/hardhat-foundry
```

Import hardhat-foundry by putting the following line in `hardhat.config.ts`:

```js
import "@nomicfoundation/hardhat-foundry";
```

Setup the project

```bash
npx hardhat init-foundry
forge remappings > remappings.txt
```

Copy-paste the test and run it with `forge test --match-test "test_revertInitialize" -vvvv`

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import "forge-std/Test.sol";
import "contracts/vesting/SymmVesting.sol";
import "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import "@openzeppelin/contracts/proxy/transparent/ProxyAdmin.sol";

contract TestRevertInitialize is Test {
    address dummy = makeAddr("dummy");
    address admin = makeAddr("admin");

    function test_revertInitialize() public {
        SymmVesting vestingImpl = new SymmVesting();

        ProxyAdmin proxyAdmin = new ProxyAdmin(admin);

        TransparentUpgradeableProxy proxy = new TransparentUpgradeableProxy(
            address(vestingImpl),
            address(proxyAdmin),
            ""
        );
        
        // initialize() on Proxy fails...
        SymmVesting wrapper = SymmVesting(address(proxy));
        vm.expectRevert();
        wrapper.initialize(dummy, dummy, dummy, dummy, dummy, dummy, dummy, dummy, dummy);

        // initialize() on implementation also fails...
        vm.expectRevert();
        vestingImpl.initialize(dummy, dummy, dummy, dummy, dummy, dummy, dummy, dummy, dummy);
    }
}
```


### Mitigation

Mark the `__vesting_init()` as `onlyInitializing` rather than `initializer`.

In case the `Vesting.sol` is supposed to be deployed as-is, without being inherited by another contract, consider adding its own `initialize()` function which will internally call `__vesting_init()`