Shallow Amethyst Turtle

Medium

# Lack of Access Control in `SymmStaking::notifyRewardAmount` Allows DoS via Reward Period Extension

https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/main/token/contracts/staking/SymmStaking.sol#L275

## Summary

The `notifyRewardAmount` function lacks access control, allowing any user to extend the reward distribution duration by depositing a minimal amount (e.g., 1 wei). This can be exploited by an attacker to cause a Denial of Service (DoS) by significantly extending the `state.periodFinish` timestamp in the `_addRewardsForToken` function, disrupting the reward distribution mechanism.

## Vulnerability Details

The vulnerability lies in the `notifyRewardAmount` function, which does not enforce any access control. This allows any user to call the function and deposit a small amount of tokens (e.g., 1 wei) to trigger the `_addRewardsForToken` function. Inside `_addRewardsForToken`, the `state.periodFinish` is updated to `block.timestamp + state.duration`, regardless of the amount deposited. 

An attacker can repeatedly call this function with minimal amounts to extend the `state.periodFinish` timestamp indefinitely, effectively causing a DoS by preventing legitimate reward distributions from occurring in a timely manner.

### Code Snippet:
```solidity
function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts)
    external
    nonReentrant
    whenNotPaused
{
    //@audit-issue No access control
    _updateRewardsStates(address(0));
    if (tokens.length != amounts.length) revert ArraysMismatched();

    uint256 len = tokens.length;
    for (uint256 i = 0; i < len; i++) {
        address token = tokens[i];
        uint256 amount = amounts[i];

        if (amount == 0) continue;
        if (!isRewardToken[token]) revert TokenNotWhitelisted(token);

        IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
        pendingRewards[token] += amount;
        _addRewardsForToken(token, amount);
    }
    emit RewardNotified(tokens, amounts);
}

function _addRewardsForToken(address token, uint256 amount) internal {
    TokenRewardState storage state = rewardState[token];

    if (block.timestamp >= state.periodFinish) {
        state.rate = amount / state.duration;
    } else {
        uint256 remaining = state.periodFinish - block.timestamp;
        uint256 leftover = remaining * state.rate;
        state.rate = (amount + leftover) / state.duration;
    }

    state.lastUpdated = block.timestamp;
@>    state.periodFinish = block.timestamp + state.duration;
}
```
## Proof of Concept
Add this test to `test/symmStaking.behavior.ts` file, 
```javascript
it("attacker should increase reward period finish by depositing 1 wei", async function () {
			const depositUser1 = "604800"
			await stakingToken.connect(user1).approve(await symmStaking.getAddress(), depositUser1)
			await usdtToken.connect(admin).transfer(user2, 10000)

			const rewardAmount = "5000000000000000000000" // 5000 tokens
			await usdtToken.connect(admin).approve(await symmStaking.getAddress(), rewardAmount)
			await symmStaking.connect(admin).configureRewardToken(await usdtToken.getAddress(), true)
			await symmStaking.connect(admin).notifyRewardAmount([await usdtToken.getAddress()], [rewardAmount])
			
			await symmStaking.connect(user1).deposit(depositUser1, user1.address)
			await time.increase(604700) // Simulate time pass
			
			const InitialRewardState = await symmStaking.rewardState(usdtToken)
			
			// Attacker calls notifyRewardAmount
			await usdtToken.connect(user2).approve(await symmStaking.getAddress(), 1)
			await symmStaking.connect(user2).notifyRewardAmount([await usdtToken.getAddress()], [1])
			
			const finalRewardState = await symmStaking.rewardState(usdtToken)

			expect(finalRewardState.periodFinish).to.be.greaterThan(InitialRewardState.periodFinish);
		
		})
```
From the test, when the attacker calls `notifyRewardAmount`, this extends the period finish for the tokenreward by the duration, just by depositing 1 wei

## Impact

- **Denial of Service (DoS):** An attacker can repeatedly extend the `state.periodFinish` timestamp, preventing legitimate reward distributions from occurring in a timely manner.
- **Disruption of Reward Mechanism:** The reward distribution mechanism can be significantly disrupted, leading to unfair distribution of rewards or even rendering the system unusable.
- **Financial Loss:** Legitimate users may be unable to claim their rewards on time, leading to potential financial losses or dissatisfaction.

## Tools Used

- Manual code review
- hardhat

## Recommendations

1. **Implement Access Control:**
   - Restrict the `notifyRewardAmount` function to only allow specific privileged addresses (e.g., the contract owner or a designated reward distributor) to call it. This can be done using the ` onlyRole(REWARD_MANAGER_ROLE)` modifier or a custom access control mechanism.

```diff
   function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts)
       external
+     onlyRole(REWARD_MANAGER_ROLE)
       nonReentrant
       whenNotPaused
   {
       // Existing logic...
   }
```
