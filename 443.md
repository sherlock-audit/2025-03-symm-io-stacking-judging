Amusing Mint Shark

Medium

# Unwhitelisting of Reward Tokens in SymmiStaking Can Be DoSed Through Multiple Vectors

### Summary

The unwhitelisting block in `configureRewardToken` relies on a flawed check `(pendingRewards[token] > 10)` which allows anyone to prevent the `REWARD_MANAGER_ROLE` from unwhitelisting reward tokens. This can also occur naturally through dust accumulation in `pendingRewards[token]` from rate calculations or incorrect pending reward accounting from `rescueTokens` function.

[unwhitelisting](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L319-L328):
```solidity
	function configureRewardToken(address token, bool status) external onlyRole(REWARD_MANAGER_ROLE) {
		_updateRewardsStates(address(0));


		if (token == address(0)) revert ZeroAddress();
		if (isRewardToken[token] == status) revert TokenWhitelistStatusUnchanged(token, status);


		isRewardToken[token] = status;
		if (!status) {
	@>		if (pendingRewards[token] > 10) revert OngoingRewardPeriodForToken(token, pendingRewards[token]);
			uint256 len = rewardTokens.length;
			for (uint256 i = 0; i < len; i++) {
				if (rewardTokens[i] == token) {
					rewardTokens[i] = rewardTokens[rewardTokens.length - 1];
					rewardTokens.pop();
					break;
				}
			}
		} else {
			rewardTokens.push(token);
			rewardState[token].duration = DEFAULT_REWARDS_DURATION;
		}


		emit UpdateWhitelist(token, status);
	}
```


### Root Cause

The root cause is the reliance on the `pendingRewards[token] > 10` check to determine if a reward token can be unwhitelisted. This check is opens pathway for a few DoS vectors:

[Reward Addition](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L275-L292):
1. Anyone can call `notifyRewardAmount` to add rewards which increases the `pendingRewards[token]`.
```solidity
	function notifyRewardAmount(address[] calldata tokens, uint256[] calldata amounts) external nonReentrant whenNotPaused {
		_updateRewardsStates(address(0));
		if (tokens.length != amounts.length) revert ArraysMismatched();


		uint256 len = tokens.length;
		for (uint256 i = 0; i < len; i++) {
			address token = tokens[i];
			uint256 amount = amounts[i];


			if (amount == 0) continue;
			if (!isRewardToken[token]) revert TokenNotWhitelisted(token);


			IERC20(token).safeTransferFrom(msg.sender, address(this), amount);
@>			pendingRewards[token] += amount;
			_addRewardsForToken(token, amount);
		}
		emit RewardNotified(tokens, amounts);
    }
```

2. Dust Accumulation and Incorrect Accounting: Added rewards are directly added to `pendingRewards[token]` but only `rate * duration` is distributed and claimed by users. Therefore, dust amounts accumulate in the `pendingRewards[token]` and over time can exceed the threshold (i.e. `> 10`). Also, the `rescueTokens` function does not subtract withdrawn rewards from `pendingRewards[token]` since [rewards can be withdrawn from the staking contract in a certain case](https://audits.sherlock.xyz/contests/838).

[Dust Accumulation](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L366-L379):
```solidity
	function _addRewardsForToken(address token, uint256 amount) internal {
		TokenRewardState storage state = rewardState[token];


		if (block.timestamp >= state.periodFinish) {
@>			state.rate = amount / state.duration;
		} else {
			uint256 remaining = state.periodFinish - block.timestamp;
			uint256 leftover = remaining * state.rate;
@>			state.rate = (amount + leftover) / state.duration;
		}


		state.lastUpdated = block.timestamp;
		state.periodFinish = block.timestamp + state.duration;
	}
```

[No pendingRewards accounting](https://github.com/sherlock-audit/2025-03-symm-io-stacking/blob/d7cf7fc96af1c25b53a7b500a98b411cd018c0d3/token/contracts/staking/SymmStaking.sol#L343-L346):
```solidity
	function rescueTokens(address token, uint256 amount, address receiver) external nonReentrant onlyRole(DEFAULT_ADMIN_ROLE) {
		IERC20(token).safeTransfer(receiver, amount);
		emit RescueToken(token, amount, receiver);
	}
```


### Internal Pre-conditions

1. A reward token must be whitelisted (`isRewardToken[token] == true`).
2. `pendingRewards[token]` must be greater than 10 to prevent unwhitelisting.
3. The `REWARD_MANAGER_ROLE` must attempt to unwhitelist the token.


### External Pre-conditions

1. An attacker must have a small amount (atleast > 10) of the reward token to call `notifyRewardAmount`.
2. Dust accumulation exceeds the check thresshold (i.e. > 10).
3. The `DEFAULT_ADMIN_ROLE` withdraw rewards using `rescueTokens`.


### Attack Path

**Scenario 1: Manual Reward Addition**
1. The attacker calls `notifyRewardAmount` with a small amount (at least> 10, e.g., 11 tokens) for the target reward token.
2. `pendingRewards[token]` is increased and exceeds the threshold (i.e. > 10).
3. The `REWARD_MANAGER_ROLE` attempts to unwhitelist the token but reverts due to the check `pendingRewards[token] > 10`.

**Scenario 2: Dust Accumulation and Incorrect Accounting**
1. Over time, dust amounts accumulate in `pendingRewards[token]` due to rate calculations.
2. Once the dust exceeds 10, the `REWARD_MANAGER_ROLE` cannot unwhitelist the token.

**OR**
1. Assume there are available rewards (above 10) with no stakers
2. The `DEFAULT_ADMIN_ROLE` withdraws rewards using `rescueTokens` but `pendingRewards[token]` is not updated.
3. `pendingRewards[token]` remains (above 10) which prevents unwhitelisting even though no rewards are being distributed and have been withdrawn.


### Impact

* The `REWARD_MANAGER_ROLE` is unable to unwhitelist reward tokens.

### PoC

N/A

### Mitigation

1. Replace `pendingRewards[token] > 10` Check: Use a more robust check, such as verifying if `periodFinish` has passed, to determine if a token can be unwhitelisted.
```solidity
if (block.timestamp < rewardState[token].periodFinish) revert OngoingRewardPeriodForToken(token);
```
2. Update Accounting in `rescueTokens`: Subtract the withdrawn amount from `pendingRewards[token]` when `rescueTokens` is called for a reward token.
```solidity
if (isRewardToken[token]) pendingRewards[token] -= amount;
```